000010  CBL SQL('QUALIFIER CICS BINDFILE')
000020 IDENTIFICATION DIVISION.
000030   PROGRAM-ID. "bil02000".
000040*PROGRAM-NAME. CENTRALIZED BILLING INTERFACE.
000050   AUTHOR. JACK MASON.
000060   INSTALLATION. DELUXE FINANCIAL SERVICES, INC.
000070   DATE-WRITTEN. 11-11-98.
000080   DATE-COMPILED.
000090******************************************************************
000100*
000110*RUN INSTRUCTIONS                                         10-03-11
000120*
000130*I16T     = C016 INPUT FROM DATA ENTRY & PARMLIBS
000140*           CENTRALIZED BILLING INTERFACE CARDS
000150*
000160*I3DO     = DATA OBJECT 003 INPUT FROM BIL775
000170*           UNIVERSAL PRODUCT CODE
000180*
000190*I550     = 550 INPUT FROM BIL502
000200*           BILLING MASTER FILE
000210*
000220*I645P    = C645 INPUT FROM PARMLIB
000230*           BILLING MONTH ENDING DATES
000240*
000250*O545     = 545 OUTPUT TO BIL450
000260*           BILLING MASTER FILE
000270*
000280*O16T     = C016 OUTPUT TO ???
000290*           CENTRALIZED BILLING INTERFACE CARDS
000300*
000310*PRINTER1 = 545 RECORDS CREATED, 132 CHARACTERS/LINE
000320*
000330*SYSOUT   = REVISION INFO, FILE COUNTS, ERROR MESSAGES; ANY PAPER
000340*
000350*PARM     = 'DATE=MM-DD-YY'      BILLING WEEK-ENDING DATE (FRIDAY)
000360*
000370******************************************************************
000380*
000390*RETURN CODES                                             11-11-98
000400*
000410*  16 = RUN ABORTED.  SEE SYSOUT.
000420*         - INVALID PARM DATA
000430*         - EMPTY FILE, I645P OR I16T
000440*         - I645P SEQUENCE ERROR
000450*         - INVALID I645P DATA (DETECTED IN INITIALIZATION)
000460*         - I550 VSAM ERROR (EXCEPT ON CLOSE)
000470*         - NON-ZERO DATA WINDOW SERVICES RETURN-CODE (EXCEPT ON
000480*             CLOSE)
000490*
000500*  08 = ERROR.  RUN CONTINUED.  SEE SYSOUT.
000510*         - I16T CARD(S) REJECTED FOR INVALID DATA
000520*
000530*  04 = INFO ONLY.  RUN CONTINUED.  SEE SYSOUT.
000540*         - EXTRANEOUS PARM DATA
000550*         - VSAM ERROR ON I550 CLOSE
000560*         - NON-ZERO DATA WINDOW SERVICES RETURN-CODE (ON CLOSE)
000570*
000580******************************************************************
000590*
000600*NARRATIVE                                                10-03-11
000610*
000620*PURPOSE:
000630*  TO CREATE 545 RECORDS FOR CSI & DELUXE NET BILLING.
000640*
000650*SOURCE OF INFORMATION:
000660*  THE C645 CARDS IN YEAR, MONTH ORDER
000670*    SORT FIELDS=(1,4,A,6,2,A).
000680*  THE C016 CARDS IN ANY ORDER.
000690*  THE 550 FILE (VSAM).
000700*  DATA OBJECT 003.
000710*  THE PARM BILLING DATE.
000720*
000730*PROGRAM FLOW:
000740*  IN INITIALIZATION THE PROGRAM;
000750*    EDITS THE PARM BILLING DATE (IT MUST BE A FRIDAY),
000760*    OPENS FILES,
000770*    CHECKS AGAINST EMPTY INPUT CARD FILES,
000780*    HANDLES THE C645 CARDS TO DETERMINE IF THE BILLING DATE IS
000790*      ALSO THE END OF A LONGER PERIOD (MONTH/QUARTER/YEAR),
000800*    BUILDS A SKELETON 545 RECORD WITH ALL CONSTANT & DEFAULT
000810*      VALUES.
000820*  PROCESSING IS DRIVEN BY THE C016 CARDS.  THE PROGRAM BYPASSES
000830*    ANY CARDS FOR PERIODS WHICH DO NOT END ON THE PARM BILLING
000840*    DATE.  FOR ALL OTHER CARDS, IT CUSTOMIZES A 545 RECORD WHILE
000850*    EDITING THE CARD DATA.  PART OF THAT PROCESS INVOLVES
000860*    SELECTING THE BEST (BY TYPE OF OFFICE; 10, 25, THEN OTHERS IN
000870*    ASCENDING ORDER) 550 RECORD THAT MATCHES ALL NON-BLANK PARTS
000880*    OF THE C016 INSTITUTION.  SOME 545 FIELDS COME FROM THE 550
000890*    RECORD THUS CHOSEN.  IF THE C016 CARD PASSES ALL EDIT TESTS,
000900*    THE PROGRAM WRITES IT TO THE OUTPUT 545 FILE.
000910*  AT THE END OF THE I16T FILE, THE PROGRAM CLOSES FILES, DISPLAYS
000920*    FILE COUNTS, AND ENDS.
000930*
000940******************************************************************
000950*
000960*REVISION HISTORY
000970*
000980*11-11-98  PROGRAM WRITTEN.  REQUEST #97187.  PROJECT #903194.
000990*           CAPS #7853.                             JACK MASON  01
001000*
001010*05-29-01  MODIFIED FOR 545 FILE CLEAN-UP (REMOVED REFERENCES TO
001020*            FIELDS BEING DROPPED FROM COPYBOOKS).
001030*          CAPS #24072.                             JACK MASON  02
001040*
001050*04-29-08  MODIFIED TO INCLUDE A FIELD FOR CREDITS
001060*            IMS: JAON-6ZPGJD                       BILAL KHAN  03
001070*
001080*05-17-09  MODIFIED TO INCLUDE A FIELD FOR CREDITS
001090*            IMS: JAON-7N8LF5                       BILAL KHAN  04
001100*
001110*04-12-10  ERROR CORRECTION.  ROUTINE TO ASSIGN IMPRINT QUANTITY
001120*            FOR CREDITS WAS INCORRECTLY DEFAULTING TO +1,
001130*            CORRECTED TO -1.
001140*          CORE SIMP PROJECT (IMS=JDBT-6VJSF9).     JACK MASON  05
001150*
001160**----------------------------------------------------------------
001170**  DATE      DESCRIPTION                        INITIALS
001180** -------  ------------------------------------------------------
001190** 051310   UPDATED FOR DLX MIGRATION            COGNIZANT
001200** -------  ------------------------------------------------------
001210** 051310  PROGRAM MODIFIED TO HANDLE LDS.CSR ROUTINES ARE COMMENT
001220**         CODE IS ADDED TO HANDLE LDS AS FLAT FILES.
001230*
001240*05-02-11  MODIFIED TO INSTALL PRICE FOR MAJOR '75' AS NORMAL
001250*            DELIVERY (RATHER THAN NORMAL BASE), AND ADJUSTED
001260*            REPORT HEADING ACCORDINGLY.
001270*          CORE SIMP PROJECT (IMS=JDBT-6VJSF9).     JACK MASON  06
001280*
001290*10-03-11  MODIFIED FOR REVISED C016 CARD:
001300*            -  ADDED NORMAL DELIVERY (AND REMOVED LOGIC THAT
001310*                 SHIFTED BASE TO DELIVERY FOR MAJOR '75').
001320*            -  ADDED OVERRIDE SEPARATE INVOICE CODE.
001330*            -  REMOVED SECONDARY BRANCH & TYPE CODE.
001340*            -  REDUCED CUSTOMER NAME TO 34 BYTES (FROM 38) TO
001350*                 KEEP RECORD LENGTH AT 135 (DATA) PLUS X'0A'.
001360*          ELIMINATED REGIONAL OUTPUT 545 FILES.
001370*          REFORMATTED REPORT IN ACCORD WITH THE OTHER CHANGES.
001380*          ADDED AN OUTPUT FILE, O16T, FOR REJECTED C16 CARDS.
001390*          CORE SIMP PROJECT.  IMS = JDBT-6VJSF9.   JACK MASON  07
001400*
001410*11-03-14  MODIFIED FOR NEW FIELD (ACCOUNT TYPE) ON I16T AND
001420*            O545.                                  JACK MASON  08
001430*
001440******************************************************************
001450*
001460 ENVIRONMENT DIVISION.
001470 CONFIGURATION SECTION.
001480   SOURCE-COMPUTER. UNIX.
001490   OBJECT-COMPUTER. UNIX.
001500*
001510 INPUT-OUTPUT SECTION.
001520   FILE-CONTROL.
001530     SELECT I16T-FILE     ASSIGN RSD-I16T.
001540     SELECT I550-FILE     ASSIGN I550
001550                            ORGANIZATION IS INDEXED
001560                            ACCESS MODE IS DYNAMIC
001570                            RECORD KEY IS I550-RECORD-KEY
001580                            FILE STATUS IS WS-I550-FILE-STATUS.
001590     SELECT I645P-FILE    ASSIGN RSD-I645P.
001600     SELECT O16T-FILE     ASSIGN RSD-O16T.
001610     SELECT O545-FILE     ASSIGN RSD-O545.
001620     SELECT I3DO-LDS-FILE    ASSIGN RSD-I3DO
001630                 FILE STATUS IS WS-I3DO-FILE-STATUS.
001640     SELECT PRINTER1-FILE ASSIGN PRINTER1
001650              ORGANIZATION IS LINE SEQUENTIAL.
001660*
001670 DATA DIVISION.
001680 FILE SECTION.
001690*
001700 FD  I16T-FILE
001710     RECORDING MODE IS F
001720     BLOCK CONTAINS 0 RECORDS
001730     RECORD CONTAINS 135 CHARACTERS
001740     LABEL RECORDS STANDARD.
001750   01  I16T-RECORD.
001760     03  I16T-FIRST-52-BYTES.
001770       05  I16T-INSTITUTION.
001780         07  I16T-BC                             PIC X(8).
001790         07  I16T-PRI-BRANCH                     PIC X(5).
001800       05  FILLER                                PIC X.
001810       05  I16T-PROD-MAJ-THRU-SUFFIX.
001820         07  I16T-PROD-MAJ-THRU-NBR              PIC X(10).
001830         07  I16T-PROD-SUFFIX                    PIC X.
001840         07  I16T-CREDIT-FLAG                    PIC X.
001850       05  I16T-NORMAL-BASE-X.
001860         07  I16T-NORMAL-BASE          DISPLAY   PIC 9(7)V9(2).
001870       05  FILLER                                PIC X.
001880       05  I16T-NORMAL-DELIV-X.
001890         07  I16T-NORMAL-DELIV         DISPLAY   PIC 9(5)V9(2).
001900       05  FILLER                                PIC X.
001910       05  I16T-FREQ-CODE                        PIC X.
001920         88  I16T-VALID-FREQ-CODE  VALUES 'A', 'M', 'Q', 'W', '1'.
001930         88  I16T-FREQ-ONE-TIME                    VALUE '1'.
001940         88  I16T-FREQ-PERIODIC         VALUES 'A', 'M', 'Q', 'W'.
001950         88  I16T-FREQ-WEEKLY                      VALUE 'W'.
001960         88  I16T-FREQ-MONTHLY                     VALUE 'M'.
001970         88  I16T-FREQ-QUARTERLY                   VALUE 'Q'.
001980         88  I16T-FREQ-ANNUAL                      VALUE 'A'.
001990       05  FILLER                                PIC X.
002000       05  I16T-ONE-TIME-DATE                    PIC X(6).
002010*                * MMDDYY *
002020     03  FILLER                                  PIC X.
002030     03  I16T-BILL-TO-ACCT                       PIC X(20).
002040     03  I16T-ACCT-TYPE                          PIC X.
002050     03  I16T-PURCHASE-ORDER                     PIC X(15).
002060     03  FILLER                                  PIC X.
002070     03  I16T-CUSTOMER-NAME                      PIC X(34).
002080     03  FILLER                                  PIC X.
002090     03  I16T-IMPRINT-QTY-X.
002100       05  I16T-IMPRINT-QTY            DISPLAY   PIC 9(7).
002110     03  FILLER                                  PIC X.
002120     03  I16T-SEP-INV-CD                         PIC X(2).
002130*
002140 FD  I550-FILE
002150*
002160 COPY T0550I.
002170*
002180 FD  I645P-FILE
002190     RECORDING MODE IS F
002200     BLOCK CONTAINS 0 RECORDS
002210     RECORD CONTAINS 80 CHARACTERS
002220     LABEL RECORDS STANDARD.
002230   01  I645P-RECORD.
002240     03  I645P-INFO.
002250       05  I645P-SEQ-YEAR-X.
002260         07  I645P-SEQ-YEAR            DISPLAY   PIC 9(4).
002270       05  FILLER                                PIC X.
002280       05  I645P-SEQ-MONTH-X.
002290         07  I645P-SEQ-MONTH           DISPLAY   PIC 9(2).
002300       05  FILLER                                PIC X.
002310       05  I645P-BILLING-DATE.
002320         07  I645P-BILL-DATE-MM                  PIC X(2).
002330         07  I645P-BILL-DATE-SLASH-1             PIC X.
002340         07  I645P-BILL-DATE-DD                  PIC X(2).
002350         07  I645P-BILL-DATE-SLASH-2             PIC X.
002360         07  I645P-BILL-DATE-YY                  PIC X(2).
002370     03  FILLER                                  PIC X(64).
002380*
002390 FD  O545-FILE
002400*
002410 COPY T0545O.
002420*
002430*
002440*
002450 FD  O16T-FILE
002460     RECORDING MODE IS F
002470     BLOCK CONTAINS 0 RECORDS
002480     RECORD CONTAINS 135 CHARACTERS
002490     LABEL RECORDS STANDARD.
002500   01  O16T-RECORD                               PIC X(135).
002510*
002520 FD  PRINTER1-FILE
002530     RECORDING MODE IS F
002540     RECORD CONTAINS 132 CHARACTERS.
002550   01  PRINTER1-RECORD.
002560     03  PR-INST-BC                              PIC X(8).
002570     03  FILLER                                  PIC X.
002580     03  PR-INST-PRI-BR                          PIC X(5).
002590     03  FILLER                                  PIC X(4).
002600     03  PR-SEP-INV-CD                           PIC X(2).
002610     03  FILLER                                  PIC X(5).
002620     03  PR-PROD-EXP                             PIC X(2).
002630     03  FILLER                                  PIC X.
002640     03  PR-PROD-MAJ                             PIC X(2).
002650     03  PR-PROD-MIN                             PIC X(2).
002660     03  FILLER                                  PIC X.
002670     03  PR-PROD-NBR                             PIC X(6).
002680     03  FILLER                                  PIC X.
002690     03  PR-PROD-SFX                             PIC X.
002700     03  FILLER                                  PIC X.
002710     03  PR-PROD-DQ                              PIC X(5).
002720     03  PR-IMP-QTY                              PIC --,---,--9.
002730     03  FILLER                                  PIC X(5).
002740     03  PR-PRICE-CD                             PIC X.
002750     03  FILLER                                  PIC X(3).
002760     03  PR-NORMAL-BASE                         PIC --,---,---.99.
002770     03  FILLER                                  PIC X(2).
002780     03  PR-NORMAL-DELIV                         PIC ---,---.99.
002790     03  FILLER                                  PIC X(3).
002800     03  PR-BILL-TO-ACCT                         PIC X(20).
002810     03  FILLER                                  PIC X(3).
002820     03  PR-PURCH-ORD                            PIC X(15).
002830*
002840   FD I3DO-LDS-FILE.
002850    01 I3DO-LDS-REC PIC X(1048576).
002860*
002870*
002880 WORKING-STORAGE SECTION.
002890 COPY TDLXMIGE.
002900     EXEC SQL INCLUDE SQLCA END-EXEC.
002910*
002920 01  WS-FLAGS-AND-SUCH                           VALUE SPACES.
002930   03  WS-I550-FILE-STATUS        PIC X(2).
002940     88  WS-I550-SUCCESSFUL         VALUE '00'.
002950     88  WS-I550-END-OF-FILE        VALUE '10'.
002960     88  WS-I550-NO-SUCH-RECORD     VALUES '20', '23'.
002970     88  WS-I550-SUCCESSFUL-OPEN    VALUES '00', '97'.
002980   03  WS-I550-FILE-FLAG          PIC X.
002990     88  OPENED-I550-FILE           VALUE 'Y'.
003000   03  WS-I3DO-OPEN-FLAG          PIC X.
003010     88  OPENED-I3DO                VALUE 'Y'.
003020   03  WS-I645P-FILE-FLAG         PIC X.
003030     88  END-I645P-FILE             VALUE 'E'.
003040   03  WS-I16T-FILE-FLAG          PIC X.
003050     88  END-I16T-FILE              VALUE 'E'.
003060   03  WS-DATE-EDIT-FLAG          PIC X.
003070     88  VALID-BILLING-DATE         VALUE SPACE.
003080     88  INVALID-DATE               VALUE 'I'.
003090     88  NOT-A-FRIDAY               VALUE 'D'.
003100   03  WS-RUN-TYPE                PIC X.
003110     88  WEEKLY-RUN                 VALUE SPACE.
003120     88  MONTH-END-RUN              VALUES 'M', 'Q', 'A'.
003130     88  QUARTER-END-RUN            VALUES 'Q', 'A'.
003140     88  YEAR-END-RUN               VALUE 'A'.
003150   03  WS-BEST-OFFICE-TYPE        PIC X(2).
003160     88  ADMIN-550-REC-FOUND        VALUE '10'.
003170     88  MAIN-550-REC-FOUND         VALUE '25'.
003180     88  NO-550-REC-FOUND           VALUE '99'.
003190   03  WS-CARD-ERROR-FLAG         PIC X.
003200     88  ERRORS-IN-CARD             VALUE 'Y'.
003210   03  WS-I645P-ABORT-FLAG        PIC X.
003220     88  ABORT-FOR-I645P-ERRORS     VALUE 'Y'.
003230   03  WS-RETURN-08-FLAG          PIC X.
003240     88  RETURN-08                  VALUE 'Y'.
003250   03  WS-RETURN-04-FLAG          PIC X.
003260     88  RETURN-04                  VALUE 'Y'.
003270*
003280 01  WS-RUN-COUNTS.
003290   03  WS-I645P-CARD-COUNT        PIC S9(5)      COMP-3 VALUE +0.
003300   03  WS-I16T-CARD-COUNT         PIC S9(5)      COMP-3 VALUE +0.
003310   03  WS-I16T-REJECT-COUNT       PIC S9(5)      COMP-3 VALUE +0.
003320   03  WS-I16T-BYPASS-COUNT       PIC S9(5)      COMP-3 VALUE +0.
003330   03  WS-O545-RECORD-COUNT       PIC S9(5)      COMP-3 VALUE +0.
003340*
003350 01  WS-PRINT-CONTROLS.
003360   03  WS-LINE-COUNT              PIC S9(3)      COMP VALUE +090.
003370   03  WS-LINE-SPACER             PIC S9         COMP VALUE +2.
003380*
003390 01  WS-HEADINGS-ETC.
003400   03  WS-HEADING-1.
003410     05  FILLER                   PIC X          VALUE '-'.
003420     05  WS-HD1-PROGRAM-NUMBER    PIC X(8)       VALUE SPACE.
003430     05  FILLER                   PIC X(21)      VALUE '-'.
003440     05  FILLER             PIC X(38) VALUE '545 RECORDS CREATED'.
003450     05  FILLER                   PIC X(14) VALUE 'BILLING DATE:'.
003460     05  WS-HD1-BILL-DATE                      VALUE '00-00-0000'.
003470       07  WS-HD1-BILL-MM         PIC X(2).
003480       07  FILLER                 PIC X.
003490       07  WS-HD1-BILL-DD         PIC X(2).
003500       07  FILLER                 PIC X.
003510       07  WS-HD1-BILL-YYYY       PIC 9(4)       DISPLAY.
003520     05  FILLER                   PIC X(20)      VALUE SPACE.
003530     05  FILLER                   PIC X(10)     VALUE 'RUN DATE:'.
003540     05  WS-HD1-RUN-DATE                       VALUE '00-00-0000'.
003550       07  WS-HD1-RUN-MM          PIC X(2).
003560       07  FILLER                 PIC X.
003570       07  WS-HD1-RUN-DD          PIC X(2).
003580       07  FILLER                 PIC X.
003590       07  WS-HD1-RUN-YYYY        PIC X(4).
003600   03  WS-HEADING-2.
003610     05  FILLER                   PIC X(44)      VALUE
003620                   '   BC    BRCH   SEP INV    PRODUCT CODE & DQ'.
003630     05  FILLER                   PIC X(44)      VALUE
003640                   '      QUANTITY  PRICE   NORMAL BASE  NORMAL '.
003650     05  FILLER                   PIC X(44)      VALUE
003660                   'DEL   BILL TO ACCOUNT        PURCHASE ORDER '.
003670   03  WS-END-OF-REPORT.
003680     05  FILLER                   PIC X(20)      VALUE ALL '-'.
003690     05  FILLER                 PIC X(15) VALUE ' END OF REPORT '.
003700     05  FILLER                   PIC X(20)      VALUE ALL '-'.
003710*
003720 01  WS-I550-MAX-KEY              PIC X(20).
003730*
003740 01  WS-SKELETON-545-REC.
003750   03  WS-SKEL-545-LENGTH         PIC S9(3)      COMP-3.
003760   03  WS-SKEL-545-REST-OF-REC.
003770     05  FILLER                   PIC X(32).
003780     05  WS-SKEL-545-SEQ-NBR      PIC 9(5)       DISPLAY.
003790     05  FILLER                   PIC X(729).
003800*
003810 01  WS-INIT-DATE-WORK.
003820   03  WS-PARM-DATE.
003830     05  WS-PARM-DATE-MM          PIC X(2).
003840     05  WS-PARM-DATE-DASH-1      PIC X.
003850     05  WS-PARM-DATE-DD          PIC X(2).
003860     05  WS-PARM-DATE-DASH-2      PIC X.
003870     05  WS-PARM-DATE-YY          PIC X(2).
003880   03  WS-EDIT-DATE.
003890     05  WS-EDIT-DATE-YEAR        PIC 9(4)       DISPLAY.
003900     05  FILLER REDEFINES WS-EDIT-DATE-YEAR.
003910       07  WS-EDIT-DATE-CC-X.
003920         09  WS-EDIT-DATE-CC      PIC 9(2)       DISPLAY.
003930       07  WS-EDIT-DATE-YY-X.
003940         09  WS-EDIT-DATE-YY      PIC 9(2)       DISPLAY.
003950     05  WS-EDIT-DATE-MMDD.
003960       07  WS-EDIT-DATE-MM-X.
003970         09  WS-EDIT-DATE-MM      PIC 9(2)       DISPLAY.
003980       07  WS-EDIT-DATE-DD-X.
003990         09  WS-EDIT-DATE-DD      PIC 9(2)       DISPLAY.
004000   03  WS-IDW-4DIG-WORK           PIC 9(4)       DISPLAY.
004010   03  WS-IDW-2DIG-WORK           PIC 9(2)       DISPLAY.
004020   03  WS-IDW-LEAP-YEAR-FLAG      PIC 9          DISPLAY.
004030     88  IDW-LEAP-YEAR              VALUE 0.
004040   03  WS-IDW-WEEK-DAY-FLAG       PIC 9          DISPLAY.
004050     88  IDW-FRIDAY                 VALUE 4.
004060*
004070 01  WS-DAYS-BEFORE-TBL           VALUE
004080                           '000031059090120151181212243273304334'.
004090   03  WS-DAYS-BEFORE             OCCURS 12 TIMES
004100                                  PIC 9(3)       DISPLAY.
004110*
004120 01  WS-ALPHA-TABLE  VALUE 'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789'.
004130   03  WS-ALPHA-TABLE-BYTE        OCCURS 36 TIMES
004140                                  PIC X.
004150*
004160 01  WS-INIT-CONSTANT-WORK.
004170   03  WS-ALPHA-SUBSCRIPT         PIC S9(3)      COMP VALUE +0.
004180   03  WS-INIT-REMAINDER          PIC S9(3)      COMP VALUE +0.
004190   03  WS-INIT-3-DIGIT-NBR        PIC 9(3)       DISPLAY VALUE 0.
004200   03  FILLER REDEFINES WS-INIT-3-DIGIT-NBR.
004210     05  WS-INIT-FIRST-2          PIC 9(2)       DISPLAY.
004220     05  WS-INIT-LAST-1           PIC 9          DISPLAY.
004230   03  WS-INIT-HOLD-HUNDERDTHS    PIC 9          DISPLAY VALUE 0.
004240*
004250 01  WS-I645P-SEQS                               VALUE LOW-VALUES.
004260   03  WS-I645P-SEQ.
004270     05  WS-I645P-SEQ-YEAR-X      PIC X(4).
004280     05  WS-I645P-SEQ-MONTH-X     PIC X(2).
004290   03  WS-PREV-I645P-SEQ          PIC X(6).
004300*
004310*
004320 01  WS-CLOSING-DISPLAYS.
004330   03  WS-CD-I16T-LINE.
004340     05  WS-CD-FILE-LINE.
004350       07  WS-CD-FILE-NAME        PIC X(7)       VALUE 'I645P: '.
004360       07  FILLER                 PIC X(2)       VALUE SPACE.
004370       07  WS-CD-RECORD-COUNT     PIC ZZ,ZZ9.
004380       07  FILLER                 PIC X          VALUE SPACE.
004390       07  WS-CD-I-O-TEXT         PIC X(15)    VALUE 'CARDS READ'.
004400     05  FILLER                   PIC X(2)       VALUE SPACE.
004410     05  WS-CD-REJECT-COUNT       PIC ZZ,ZZ9.
004420     05  FILLER              PIC X(20) VALUE ' REJECTED (ERRORS)'.
004430     05  WS-CD-BYPASS-COUNT       PIC ZZ,ZZ9.
004440     05  FILLER     PIC X(27) VALUE ' BYPASSED (FREQUENCY CODES)'.
004450   03  WS-RETURN-CODE-LINE.
004460     05  FILLER                   PIC X(14) VALUE 'RETURN CODE ='.
004470     05  WS-RETURN-CODE           PIC 9(2)       DISPLAY VALUE 00.
004480     05  FILLER                   PIC X          VALUE '.'.
004490*
004500   01 I PIC 9(15) VALUE ZERO.
004510   01 J PIC 9(15) VALUE ZERO.
004520   01 LEN-COUNT PIC 9(15) VALUE ZERO.
004530   01 WK-I3DO-EOF-FILE PIC X VALUE 'N'.
004540   01 I3DO-LDS-DATA PIC X(16777216).
004550   01 WS-I3DO-FILE-STATUS PIC 9(2).
004560   01 WS-MAX-LENGTH PIC 9(9) VALUE 16777216.
004570   01 WS-RECORD-LENGTH PIC 9(9) VALUE 1048576.
004580*
004590 01  LS-WINDOW.
004600   03  LS-PROD-REC-COUNT          PIC 9(5)  COMP.
004610   03  FILLER                     PIC X(12).
004620   03  LS-PRODUCT-RECORD          OCCURS 0 TO 200000 TIMES
004630                                  DEPENDING ON LS-PROD-REC-COUNT
004640                                  ASCENDING KEY IS LS-PRODUCT
004650                                  INDEXED BY LS-PROD-REC-INDX.
004660     05  LS-PRODUCT               PIC X(16).
004670*                * MAJOR/MINOR/NBR/SUFFIX/DQ *
004680     05  FILLER                   PIC X(65).
004690*
004700 COPY CPY108.
004710*
004720 01  LS-PARM-INFO.
004730   03  LS-PARM-LENGTH             PIC S9(4)      COMP.
004740   03  LS-PARM-DATA               PIC X(100).
004750*
004760 01  WS-DISPLAY-PARM.
004770   03  WS-DP-CAPTION             PIC X(14) VALUE 'PARM OPTIONS: '.
004780   03  FILLER                     PIC X          VALUE '"'.
004790   03  WS-DP-STATEMENT            PIC X(20)      VALUE SPACE.
004800   03  FILLER                     PIC X          VALUE '"'.
004810*
004820 COPY CPY004.
004830*
004840 01  WS-PROGRAM-NUMBER            PIC X(8)       VALUE 'BIL02008'.
004850 01  WS-REVISION-DATE             PIC X(8)       VALUE '11-03-14'.
004860*
004870*
004880 LINKAGE SECTION.
004890*
004900 COPY AIXLNCPY.
004910*
004920*
004930 PROCEDURE DIVISION USING BY VALUE PARMCNT
004940           BY REFERENCE OS-PARM.
004950*
004960 0000-SQL-DB-CONNECT.
004970     MOVE 'DLXVSAM' TO DBNAME.
004980 COPY CPYMIGUD.
004990*
005000*
005010   0000-MAIN-LINE.
005020     PERFORM 8000-INITIALIZE.
005030     PERFORM 1000-EACH-I16T-CARD UNTIL END-I16T-FILE.
005040     PERFORM 9000-CLOSE.
005050     STOP RUN.
005060*
005070   1000-EACH-I16T-CARD.
005080     ADD 1 TO WS-I16T-CARD-COUNT.
005090     IF (I16T-FREQ-ANNUAL
005100             AND (NOT YEAR-END-RUN))
005110         OR (I16T-FREQ-QUARTERLY
005120             AND (NOT QUARTER-END-RUN))
005130         OR (I16T-FREQ-MONTHLY
005140             AND (NOT MONTH-END-RUN))
005150       ADD 1 TO WS-I16T-BYPASS-COUNT
005160     ELSE
005170       MOVE WS-SKEL-545-LENGTH TO O545-LENGTH-REST-OF-REC
005180       MOVE WS-SKEL-545-REST-OF-REC TO O545-REST-OF-REC
005190       PERFORM 2000-PROCESS-I16T-CARD
005200       IF ERRORS-IN-CARD
005210         MOVE I16T-RECORD TO O16T-RECORD
005220         WRITE O16T-RECORD
005230         MOVE SPACE TO WS-CARD-ERROR-FLAG
005240         MOVE 'Y' TO WS-RETURN-08-FLAG
005250         ADD 1 TO WS-I16T-REJECT-COUNT
005260       ELSE
005270         PERFORM 1600-REPORT-O545-REC
005280         PERFORM 1500-WRITE-O545-REC
005290         ADD 1 TO WS-SKEL-545-SEQ-NBR.
005300     PERFORM 1100-READ-I16T-FILE.
005310*
005320   1100-READ-I16T-FILE.
005330     READ I16T-FILE AT END
005340      MOVE 'E' TO WS-I16T-FILE-FLAG.
005350*
005360   1500-WRITE-O545-REC.
005370     WRITE O545-REC.
005380     ADD 1 TO WS-O545-RECORD-COUNT.
005390*
005400   1600-REPORT-O545-REC.
005410     IF WS-LINE-COUNT GREATER +59
005420       WRITE PRINTER1-RECORD FROM WS-HEADING-1 AFTER PAGE
005430       MOVE +1 TO WS-LINE-COUNT
005440       MOVE +2 TO WS-LINE-SPACER
005450       MOVE WS-HEADING-2 TO PRINTER1-RECORD
005460       PERFORM 1700-WRITE-PRINTER1-REC.
005470     MOVE SPACE TO PRINTER1-RECORD.
005480     MOVE O545-BC TO PR-INST-BC.
005490     MOVE O545-PRIMARY-BRANCH TO PR-INST-PRI-BR.
005500     MOVE O545-PAY-INV-SEP-INV TO PR-SEP-INV-CD.
005510     MOVE O545-PRODUCT-SALE-TYPE TO PR-PROD-EXP.
005520     MOVE O545-PRODUCT-MAJ-PROD-GROUP TO PR-PROD-MAJ.
005530     MOVE O545-PRODUCT-PRICE-CATEGORY TO PR-PROD-MIN.
005540     MOVE O545-PRODUCT-NUMBER TO PR-PROD-NBR.
005550     MOVE O545-PRODUCT-DEPOSIT-SUFFIX TO PR-PROD-SFX.
005560     MOVE O545-DESCRIPTIVE-QTY TO PR-PROD-DQ.
005570     MOVE O545-IMPRINT-QUANTITY TO PR-IMP-QTY.
005580     MOVE O545-PRICE-CODE TO PR-PRICE-CD.
005590     MOVE O545-NORMAL-BASE TO PR-NORMAL-BASE.
005600     MOVE O545-NORMAL-DELIVERY TO PR-NORMAL-DELIV.
005610     MOVE O545-BILL-ACCOUNT TO PR-BILL-TO-ACCT.
005620     MOVE O545-PURCHASE-ORDER TO PR-PURCH-ORD.
005630     PERFORM 1700-WRITE-PRINTER1-REC.
005640     MOVE +1 TO WS-LINE-SPACER.
005650*
005660   1700-WRITE-PRINTER1-REC.
005670     WRITE PRINTER1-RECORD AFTER WS-LINE-SPACER.
005680     ADD WS-LINE-SPACER TO WS-LINE-COUNT.
005690*
005700   2000-PROCESS-I16T-CARD.
005710     IF (I16T-BC IS NUMERIC)
005720        AND ((I16T-PRI-BRANCH EQUAL SPACE)
005730             OR (I16T-PRI-BRANCH IS NUMERIC))
005740       PERFORM 2100-GET-I550-INFO
005750     ELSE
005760       PERFORM 2900-REJECT-I16T-CARD
005770       IF I16T-BC NOT NUMERIC
005780         DISPLAY '   NON-NUMERIC BC "' I16T-BC '"' UPON SYSOUT
005790       END-IF
005800       IF (I16T-PRI-BRANCH NOT EQUAL SPACE)
005810           AND (I16T-PRI-BRANCH NOT NUMERIC)
005820         DISPLAY '   NON-BLANK NON-NUMERIC PRIMARY BRANCH "'
005830               I16T-PRI-BRANCH '"' UPON SYSOUT.
005840*
005850     IF (I16T-PROD-MAJ-THRU-NBR IS NUMERIC)
005860         AND (I16T-PROD-SUFFIX IS ALPHABETIC)
005870         AND (I16T-PROD-SUFFIX NOT EQUAL SPACE)
005880       MOVE I16T-PROD-MAJ-THRU-SUFFIX TO O545-PRODUCT-ID (3:11)
005890       SEARCH ALL LS-PRODUCT-RECORD
005900         AT END
005910           PERFORM 2900-REJECT-I16T-CARD
005920           DISPLAY '   NO I3DO (58 FILE) RECORD FOR PRODUCT "'
005930                 I16T-PROD-MAJ-THRU-SUFFIX '" D.Q. 00001'
005940                 UPON SYSOUT
005950         WHEN LS-PRODUCT (LS-PROD-REC-INDX) EQUAL
005960               O545-PRODUCT-ID (3:16)
005970           MOVE O545-PRODUCT-CODE TO O545-MAIN-PR-PROD-CODE
005980       END-SEARCH
005990     ELSE
006000       PERFORM 2900-REJECT-I16T-CARD
006010       DISPLAY '   INVALID PRODUCT "' I16T-PROD-MAJ-THRU-SUFFIX
006020             '"' UPON SYSOUT.
006030     IF I16T-CREDIT-FLAG NOT EQUAL 'C' AND SPACE
006040       PERFORM 2900-REJECT-I16T-CARD
006050       DISPLAY '  INVALID CREDIT FLAG ' UPON SYSOUT
006060     ELSE
006070       MOVE I16T-CREDIT-FLAG TO O545-CREDIT-NO-CHARGE-FLAG (1:1).
006080     IF I16T-NORMAL-BASE-X NOT EQUAL SPACE
006090       IF I16T-NORMAL-BASE-X IS NUMERIC
006100         IF (I16T-NORMAL-BASE NOT EQUAL ZERO)
006110             OR ((I16T-NORMAL-DELIV-X IS NUMERIC)
006120                 AND (I16T-NORMAL-DELIV NOT EQUAL ZERO))
006130           IF I16T-CREDIT-FLAG EQUAL 'C'
006140             MULTIPLY I16T-NORMAL-BASE BY -1 GIVING
006150                   O545-NORMAL-BASE
006160           ELSE
006170             MOVE I16T-NORMAL-BASE TO O545-NORMAL-BASE
006180           END-IF
006190           MOVE '3' TO O545-PRICE-CODE
006200         ELSE
006210           PERFORM 2900-REJECT-I16T-CARD
006220           DISPLAY '   NORMAL BASE = ZERO' UPON SYSOUT
006230       ELSE
006240         PERFORM 2900-REJECT-I16T-CARD
006250         DISPLAY '   NON-NUMERIC NORMAL BASE "'
006260               I16T-NORMAL-BASE-X '"' UPON SYSOUT.
006270*
006280     IF I16T-NORMAL-DELIV-X NOT EQUAL SPACE
006290       IF I16T-NORMAL-DELIV-X IS NUMERIC
006300         IF O545-PRICE-CODE EQUAL '1'
006310           IF I16T-PROD-MAJ-THRU-NBR (1:2) EQUAL '75'
006320             MOVE '3' TO O545-PRICE-CODE
006330           ELSE
006340             MOVE '2' TO O545-PRICE-CODE
006350           END-IF
006360         END-IF
006370         IF I16T-CREDIT-FLAG EQUAL 'C'
006380           SUBTRACT I16T-NORMAL-DELIV FROM +0.00 GIVING
006390                 O545-NORMAL-DELIVERY
006400         ELSE
006410           MOVE I16T-NORMAL-DELIV TO O545-NORMAL-DELIVERY
006420         END-IF
006430       ELSE
006440         PERFORM 2900-REJECT-I16T-CARD
006450         DISPLAY '   NON-BLANK NON-NUMERIC NORMAL DELIVERY "'
006460               I16T-NORMAL-DELIV-X '"' UPON SYSOUT.
006470*
006480     IF NOT I16T-VALID-FREQ-CODE
006490       PERFORM 2900-REJECT-I16T-CARD
006500       DISPLAY '   INVALID FREQUENCY CODE "' I16T-FREQ-CODE '"'
006510             UPON SYSOUT.
006520*
006530     IF I16T-ONE-TIME-DATE NOT EQUAL SPACE
006540       IF I16T-ONE-TIME-DATE NOT EQUAL O545-BILLING-DATE
006550         PERFORM 2900-REJECT-I16T-CARD
006560         DISPLAY '   ONE-TIME BILLING DATE "' I16T-ONE-TIME-DATE
006570               '" NOT EQUAL TO PARM BILLING DATE' UPON SYSOUT
006580       END-IF
006590       IF I16T-FREQ-PERIODIC
006600         PERFORM 2900-REJECT-I16T-CARD
006610         DISPLAY '   ONE-TIME BILLING DATE "' I16T-ONE-TIME-DATE
006620               '" WITH FREQUENCY CODE "' I16T-FREQ-CODE '"'
006630               UPON SYSOUT.
006640*
006650     IF I16T-BILL-TO-ACCT (1:1) NOT EQUAL SPACE
006660       MOVE I16T-BILL-TO-ACCT TO O545-WIP-ACCOUNT-NUMBER
006670             O545-BILL-ACCOUNT
006680     ELSE
006690       IF I16T-BILL-TO-ACCT (2:19) NOT EQUAL SPACE
006700         PERFORM 2900-REJECT-I16T-CARD
006710         DISPLAY '   BILL TO ACCOUNT "' I16T-BILL-TO-ACCT
006720               '" NOT LEFT JUSTIFIED' UPON SYSOUT.
006730*
006740     IF I16T-ACCT-TYPE NOT EQUAL SPACE
006750       MOVE I16T-ACCT-TYPE TO O545-ACCOUNT-TYPE
006760       IF I16T-ACCT-TYPE NOT EQUAL 'D' AND 'S' AND 'G'
006770         PERFORM 2900-REJECT-I16T-CARD
006780         DISPLAY '   INVALID ACCOUNT TYPE "' I16T-ACCT-TYPE '"'
006790               UPON SYSOUT
006800       END-IF
006810       IF I16T-BILL-TO-ACCT EQUAL SPACES
006820         PERFORM 2900-REJECT-I16T-CARD
006830         DISPLAY '   NON-BLANK ACCOUNT TYPE "' I16T-ACCT-TYPE
006840               '" PRESENT WITHOUT AN ACCOUNT NUMBER' UPON SYSOUT.
006850*
006860     IF I16T-PURCHASE-ORDER (1:1) NOT EQUAL SPACE
006870       MOVE I16T-PURCHASE-ORDER TO O545-PURCHASE-ORDER
006880     ELSE
006890       IF I16T-PURCHASE-ORDER (2:14) NOT EQUAL SPACE
006900         PERFORM 2900-REJECT-I16T-CARD
006910         DISPLAY '   PURCHASE ORDER "' I16T-PURCHASE-ORDER
006920               '" NOT LEFT JUSTIFIED' UPON SYSOUT.
006930*
006940     IF I16T-CUSTOMER-NAME (1:1) NOT EQUAL SPACE
006950       MOVE I16T-CUSTOMER-NAME TO O545-CUSTOMER-NAME
006960     ELSE
006970       IF I16T-CUSTOMER-NAME (2:33) NOT EQUAL SPACE
006980         PERFORM 2900-REJECT-I16T-CARD
006990         DISPLAY '   CUSTOMER NAME "' I16T-CUSTOMER-NAME
007000               '" NOT LEFT JUSTIFIED' UPON SYSOUT.
007010*
007020     IF I16T-IMPRINT-QTY-X EQUAL SPACE
007030       IF I16T-CREDIT-FLAG EQUAL 'C'
007040         MOVE -1 TO O545-IMPRINT-QUANTITY
007050       END-IF
007060     ELSE
007070       IF I16T-IMPRINT-QTY-X IS NUMERIC
007080         IF I16T-IMPRINT-QTY NOT EQUAL ZERO
007090            IF I16T-CREDIT-FLAG EQUAL 'C'
007100               MULTIPLY I16T-IMPRINT-QTY BY -1 GIVING
007110                                        O545-IMPRINT-QUANTITY
007120            ELSE
007130               MOVE I16T-IMPRINT-QTY TO O545-IMPRINT-QUANTITY
007140         ELSE
007150           PERFORM 2900-REJECT-I16T-CARD
007160           DISPLAY '   IMPRINT QUANTITY = ZERO' UPON SYSOUT
007170       ELSE
007180         PERFORM 2900-REJECT-I16T-CARD
007190         DISPLAY '   NON-NUMERIC IMPRINT QUANTITY "'
007200               I16T-IMPRINT-QTY-X '"' UPON SYSOUT.
007210*
007220     IF I16T-SEP-INV-CD NOT EQUAL SPACE
007230       IF I16T-SEP-INV-CD IS NUMERIC
007240         MOVE O545-PRICING-INSTITUTION TO
007250               O545-PAY-INV-INSTITUTION
007260         MOVE I16T-SEP-INV-CD TO O545-PAY-INV-SEP-INV
007270       ELSE
007280         PERFORM 2900-REJECT-I16T-CARD
007290         DISPLAY '   NON-BLANK NON-NUMERIC OVERRIDE SEPARATE INVOI
007300-              'CE CODE "' I16T-SEP-INV-CD '"' UPON SYSOUT.
007310*
007320   2100-GET-I550-INFO.
007330     MOVE I16T-INSTITUTION TO
007340           WS-I550-MAX-KEY (1:13) I550-RECORD-KEY (1:13).
007350     MOVE '0000000' TO
007360           WS-I550-MAX-KEY (14:7) I550-RECORD-KEY (14:7).
007370     IF WS-I550-MAX-KEY (9:5) EQUAL SPACE
007380       MOVE HIGH-VALUES TO WS-I550-MAX-KEY (9:5).
007390     MOVE '99' TO WS-BEST-OFFICE-TYPE.
007400     START I550-FILE KEY NOT LESS I550-RECORD-KEY.
007410     IF WS-I550-SUCCESSFUL
007420       PERFORM 2120-READ-I550-FILE-NEXT
007430     ELSE
007440       IF (NOT WS-I550-END-OF-FILE)
007450           AND (NOT WS-I550-NO-SUCH-RECORD)
007460         DISPLAY '* VSAM ERROR ON I550 START.  KEY NOT LESS "'
007470               I16T-INSTITUTION '". FILE STATUS = '
007480               WS-I550-FILE-STATUS '. *' UPON SYSOUT
007490         GO TO 9900-DISPLAY-ABORT.
007500*
007510     PERFORM 2110-EACH-I550-RECORD
007520         UNTIL ADMIN-550-REC-FOUND
007530             OR (NOT WS-I550-SUCCESSFUL)
007540             OR (I550-RECORD-KEY GREATER WS-I550-MAX-KEY).
007550*
007560     IF NO-550-REC-FOUND
007570       PERFORM 2900-REJECT-I16T-CARD
007580       DISPLAY '   NO MATCHING I550 RECORD FOUND' UPON SYSOUT
007590     ELSE
007600       MOVE O545-BC TO O545-WIP-BC
007610       MOVE O545-PRICING-INSTITUTION TO
007620             O545-TITLEPLATE-INSTITUTION
007630       MOVE O545-SERVICING-PLANT TO O545-ORDER-ENTRY-PLANT
007640             O545-PRODUCING-PLANT O545-SHIPPING-PLANT.
007650*
007660   2110-EACH-I550-RECORD.
007670     IF (I16T-PRI-BRANCH EQUAL SPACE OR I550-PRIMARY-BRANCH)
007680         AND (I550-SECONDARY-BRANCH EQUAL '00000')
007690         AND (I550-BILLING-TYPE EQUAL '00')
007700         AND (NOT I550-SUMMARY-ONLY-BRANCH)
007710         AND ((I550-TYPE-OF-OFFICE EQUAL '10')
007720             OR ((NOT MAIN-550-REC-FOUND)
007730                 AND ((I550-TYPE-OF-OFFICE EQUAL '25')
007740                     OR (I550-TYPE-OF-OFFICE LESS
007750                           WS-BEST-OFFICE-TYPE))))
007760       MOVE I550-TYPE-OF-OFFICE TO WS-BEST-OFFICE-TYPE
007770       MOVE I550-RECORD-KEY TO O545-PRICING-INSTITUTION
007780       MOVE I550-SERVICING-PLANT TO O545-SERVICING-PLANT
007790       MOVE I550-RECEIVABLES-PLANT TO O545-RECEIVABLES-PLANT
007800       MOVE I550-5-DIGIT-ZIP-CODE TO O545-DELIVERY-ZIP-CODE.
007810     IF NOT ADMIN-550-REC-FOUND
007820       PERFORM 2120-READ-I550-FILE-NEXT.
007830*
007840   2120-READ-I550-FILE-NEXT.
007850     READ I550-FILE NEXT RECORD.
007860     IF (NOT WS-I550-SUCCESSFUL)
007870         AND (NOT WS-I550-END-OF-FILE)
007880       DISPLAY '* VSAM ERROR ON I550 READ. FILE STATUS = '
007890             WS-I550-FILE-STATUS '. *' UPON SYSOUT
007900       GO TO 9900-DISPLAY-ABORT.
007910*
007920   2900-REJECT-I16T-CARD.
007930     IF NOT ERRORS-IN-CARD
007940       MOVE 'Y' TO WS-CARD-ERROR-FLAG
007950       DISPLAY 'I16T CARD "' I16T-FIRST-52-BYTES
007960             '"... REJECTED FOR:' UPON SYSOUT.
007970*
007980   8000-INITIALIZE.
007990*
008000 COPY CPY005.
008010*
008020     PERFORM 8100-EDIT-DISPLAY-PARM-INFO.
008030     PERFORM 8200-INIT-FILES.
008040     PERFORM 8300-ESTABLISH-RUN-TYPE.
008050     PERFORM 8400-INIT-SKELETON-545-REC.
008060     MOVE WS-PROGRAM-NUMBER TO WS-HD1-PROGRAM-NUMBER.
008070     MOVE WS-SYSOUT-RUN-MONTH TO WS-HD1-RUN-MM.
008080     MOVE WS-SYSOUT-RUN-DAY TO WS-HD1-RUN-DD.
008090     MOVE CPY4-FOUR-DIGIT-YEAR TO WS-HD1-RUN-YYYY.
008100*
008110   8100-EDIT-DISPLAY-PARM-INFO.
008120 COPY AIXPRCPY.
008130*
008140     MOVE LS-PARM-LENGTH TO CPY108-PARM-LENGTH.
008150     MOVE LS-PARM-DATA TO CPY108-PARM-DATA.
008160     CALL 'mis11500' USING CPY108-REC.
008170     IF CPY108-PARSE-ERROR
008180       DISPLAY '* CALLED PROGRAM MIS115 ABORTED *' UPON SYSOUT
008190       GO TO 9900-DISPLAY-ABORT.
008200     PERFORM
008210           VARYING CPY108-STMT-IX FROM +1 BY +1
008220           UNTIL CPY108-STMT-IX GREATER +5
008230       IF CPY108-RET-STATEMENT (CPY108-STMT-IX) NOT EQUAL SPACE
008240         MOVE CPY108-RET-STATEMENT (CPY108-STMT-IX) TO
008250               WS-DP-STATEMENT
008260         DISPLAY WS-DISPLAY-PARM UPON SYSOUT
008270         MOVE SPACE TO WS-DP-CAPTION
008280       END-IF
008290     END-PERFORM.
008300     SET CPY108-VAL-IX TO +1.
008310     SEARCH CPY108-RET-VALUES-ENTRY
008320       AT END
008330         DISPLAY '* MISSING PARM KEYWORD "DATE      " *'
008340               UPON SYSOUT
008350         GO TO 9900-DISPLAY-ABORT
008360       WHEN CPY108-RET-KEYWORD (CPY108-VAL-IX) EQUAL 'DATE      '
008370         PERFORM 8110-MOVE-EDIT-PARM-DATE
008380         IF INVALID-DATE
008390           DISPLAY '* INVALID PARM DATE "' CPY108-RET-VALUE
008400                 (CPY108-VAL-IX) '" *' UPON SYSOUT
008410           GO TO 9900-DISPLAY-ABORT
008420         ELSE
008430           IF NOT-A-FRIDAY
008440             DISPLAY '* PARM DATE "' CPY108-RET-VALUE
008450                 (CPY108-VAL-IX) '" IS NOT A FRIDAY *'
008460                 UPON SYSOUT
008470             GO TO 9900-DISPLAY-ABORT
008480           END-IF
008490         END-IF
008500         MOVE SPACE TO CPY108-RET-KEYWORD (CPY108-VAL-IX)
008510     END-SEARCH.
008520     PERFORM
008530           VARYING CPY108-VAL-IX FROM +1 BY +1
008540           UNTIL CPY108-VAL-IX GREATER +5
008550       IF CPY108-RET-KEYWORD (CPY108-VAL-IX) NOT EQUAL SPACE
008560         MOVE 'Y' TO WS-RETURN-04-FLAG
008570         DISPLAY 'INFO ONLY: EXTRANEOUS PARM KEYWORD "'
008580               CPY108-RET-KEYWORD (CPY108-VAL-IX) '".'
008590               UPON SYSOUT
008600       END-IF
008610     END-PERFORM.
008620*
008630   8110-MOVE-EDIT-PARM-DATE.
008640     MOVE CPY108-RET-VALUE (CPY108-VAL-IX) TO WS-PARM-DATE.
008650     IF (CPY108-RET-VALUE (CPY108-VAL-IX) NOT EQUAL WS-PARM-DATE)
008660*                * NON-BLANK CHARACTERS AFTER FIRST EIGHT *
008670         OR (WS-PARM-DATE-DASH-1 NOT EQUAL '-')
008680         OR (WS-PARM-DATE-DASH-2 NOT EQUAL '-')
008690       MOVE 'I' TO WS-DATE-EDIT-FLAG
008700     ELSE
008710       MOVE WS-PARM-DATE-YY TO WS-EDIT-DATE-YY-X
008720       MOVE WS-PARM-DATE-MM TO WS-EDIT-DATE-MM-X
008730             WS-HD1-BILL-MM
008740       MOVE WS-PARM-DATE-DD TO WS-EDIT-DATE-DD-X
008750             WS-HD1-BILL-DD
008760       PERFORM 8350-EDIT-BILLING-DATE
008770       MOVE WS-EDIT-DATE-YEAR TO WS-HD1-BILL-YYYY.
008780*
008790   8200-INIT-FILES.
008800     OPEN OUTPUT O545-FILE O16T-FILE PRINTER1-FILE
008810         INPUT I16T-FILE I645P-FILE I550-FILE.
008820     IF NOT WS-I550-SUCCESSFUL-OPEN
008830       DISPLAY '* VSAM ERROR ON I550 OPEN.  FILE STATUS = '
008840             WS-I550-FILE-STATUS '. *' UPON SYSOUT
008850       GO TO 9900-DISPLAY-ABORT.
008860     MOVE 'Y' TO WS-I550-FILE-FLAG.
008870     OPEN INPUT I3DO-LDS-FILE.
008880     IF WS-I3DO-FILE-STATUS NOT EQUAL 0
008890       DISPLAY '*OPEN ERROR ON FILE I3DO-LDS-FILE:'
008900-             WS-I3DO-FILE-STATUS
008910       GO TO 9900-DISPLAY-ABORT.
008920      READ I3DO-LDS-FILE  AT END MOVE 'Y' TO WK-I3DO-EOF-FILE.
008930     IF WS-I3DO-FILE-STATUS NOT EQUAL 0
008940       DISPLAY '*READ ERROR ON FILE I3DO-LDS-FILE:'
008950-             WS-I3DO-FILE-STATUS
008960       GO TO 9900-DISPLAY-ABORT.
008970      MOVE ZERO TO I
008980      MOVE SPACES TO I3DO-LDS-DATA
008990      MOVE 1 TO J
009000      PERFORM UNTIL (WK-I3DO-EOF-FILE = 'Y' ) OR
009010            ( J > WS-MAX-LENGTH )
009020         MOVE I3DO-LDS-REC  TO I3DO-LDS-DATA(J:WS-RECORD-LENGTH)
009030         READ I3DO-LDS-FILE  AT END MOVE 'Y' TO WK-I3DO-EOF-FILE
009040         END-READ
009050       COMPUTE I = I + 1
009060       COMPUTE J = WS-RECORD-LENGTH * I  + 1
009070      END-PERFORM.
009080      MOVE I3DO-LDS-DATA TO LS-WINDOW.
009090     MOVE 'Y' TO WS-I3DO-OPEN-FLAG.
009100     PERFORM 1100-READ-I16T-FILE.
009110     IF END-I16T-FILE
009120       DISPLAY '* I16T IS AN EMPTY FILE. *' UPON SYSOUT
009130       GO TO 9900-DISPLAY-ABORT.
009140     PERFORM 8320-READ-I645P-FILE.
009150     IF END-I645P-FILE
009160       DISPLAY '* I645P IS AN EMPTY FILE. *' UPON SYSOUT
009170       GO TO 9900-DISPLAY-ABORT.
009180*
009190   8300-ESTABLISH-RUN-TYPE.
009200     PERFORM 8310-EACH-I645P-CARD UNTIL END-I645P-FILE.
009210     IF ABORT-FOR-I645P-ERRORS
009220       DISPLAY '* ERRORS IN I645P CARDS *' UPON SYSOUT
009230       GO TO 9900-DISPLAY-ABORT.
009240     IF WEEKLY-RUN
009250       DISPLAY 'WEEKLY RUN - I16T "M" "Q" & "A" CARDS WILL BE BYPA
009260-            'SSED.' UPON SYSOUT
009270     ELSE
009280       IF YEAR-END-RUN
009290         DISPLAY 'YEAR-END RUN - ALL I16T CARD TYPES WILL BE PROCE
009300-              'SSED.' UPON SYSOUT
009310       ELSE
009320         IF QUARTER-END-RUN
009330           DISPLAY 'QUARTER-END RUN - I16T "A" CARDS WILL BE BYPAS
009340-                'SED.' UPON SYSOUT
009350         ELSE
009360           DISPLAY 'MONTH-END RUN - I16T "Q" & "A" CARDS WILL BE B
009370-                'YPASSED.' UPON SYSOUT.
009380*
009390   8310-EACH-I645P-CARD.
009400     MOVE I645P-SEQ-YEAR-X TO WS-I645P-SEQ-YEAR-X.
009410     MOVE I645P-SEQ-MONTH-X TO WS-I645P-SEQ-MONTH-X.
009420     IF WS-I645P-SEQ NOT GREATER WS-PREV-I645P-SEQ
009430       DISPLAY '* I645P SEQUENCE ERROR "' WS-PREV-I645P-SEQ '" "'
009440             WS-I645P-SEQ '" *' UPON SYSOUT
009450       GO TO 9900-DISPLAY-ABORT.
009460     IF I645P-SEQ-YEAR-X NOT NUMERIC
009470       PERFORM 8340-REJECT-I645P-CARD
009480       DISPLAY '   NON-NUMERIC SEQUENCE YEAR "' I645P-SEQ-YEAR-X
009490             '"' UPON SYSOUT.
009500     IF I645P-SEQ-MONTH-X NOT NUMERIC
009510       PERFORM 8340-REJECT-I645P-CARD
009520       DISPLAY '   NON-NUMERIC SEQUENCE MONTH "' I645P-SEQ-MONTH-X
009530             '"' UPON SYSOUT
009540     ELSE
009550       IF (I645P-SEQ-MONTH LESS 01)
009560           OR (I645P-SEQ-MONTH GREATER 12)
009570         PERFORM 8340-REJECT-I645P-CARD
009580         DISPLAY '   INVALID SEQUENCE MONTH "' I645P-SEQ-MONTH-X
009590              '"' UPON SYSOUT.
009600     IF (I645P-BILL-DATE-SLASH-1 NOT EQUAL '/' AND '-' AND SPACE)
009610         OR (I645P-BILL-DATE-SLASH-2 NOT EQUAL
009620               I645P-BILL-DATE-SLASH-1)
009630       MOVE 'I' TO WS-DATE-EDIT-FLAG
009640     ELSE
009650       MOVE I645P-BILL-DATE-YY TO WS-EDIT-DATE-YY-X
009660       MOVE I645P-BILL-DATE-MM TO WS-EDIT-DATE-MM-X
009670       MOVE I645P-BILL-DATE-DD TO WS-EDIT-DATE-DD-X
009680       PERFORM 8350-EDIT-BILLING-DATE.
009690     IF INVALID-DATE
009700       PERFORM 8340-REJECT-I645P-CARD
009710       DISPLAY '   INVALID LAST BILLING DATE FOR MONTH "'
009720             I645P-BILLING-DATE '"' UPON SYSOUT
009730     ELSE
009740       IF NOT-A-FRIDAY
009750         PERFORM 8340-REJECT-I645P-CARD
009760         DISPLAY '   LAST BILLING DATE FOR MONTH "'
009770               I645P-BILLING-DATE '" IS NOT A FRIDAY' UPON SYSOUT.
009780     IF NOT ERRORS-IN-CARD
009790       PERFORM 8330-REASONABILITY-CHECK THRU 8330-EXIT.
009800     IF ERRORS-IN-CARD
009810       MOVE SPACE TO WS-CARD-ERROR-FLAG
009820       MOVE 'Y' TO WS-I645P-ABORT-FLAG
009830     ELSE
009840       IF WEEKLY-RUN
009850           AND (I645P-BILL-DATE-MM EQUAL WS-PARM-DATE-MM)
009860           AND (I645P-BILL-DATE-DD EQUAL WS-PARM-DATE-DD)
009870           AND (I645P-BILL-DATE-YY EQUAL WS-PARM-DATE-YY)
009880         DISPLAY 'PARM DATE IS ENDING DATE FOR I645P YEAR/MONTH '
009890               I645P-SEQ-YEAR-X '/' I645P-SEQ-MONTH-X '.'
009900               UPON SYSOUT
009910         IF I645P-SEQ-MONTH EQUAL 12
009920           MOVE 'A' TO WS-RUN-TYPE
009930         ELSE
009940           IF I645P-SEQ-MONTH EQUAL 03 OR 06 OR 09
009950             MOVE 'Q' TO WS-RUN-TYPE
009960           ELSE
009970             MOVE 'M' TO WS-RUN-TYPE.
009980     MOVE WS-I645P-SEQ TO WS-PREV-I645P-SEQ.
009990     ADD 1 TO WS-I645P-CARD-COUNT.
010000     PERFORM 8320-READ-I645P-FILE.
010010*
010020   8320-READ-I645P-FILE.
010030     READ I645P-FILE AT END
010040       MOVE 'E' TO WS-I645P-FILE-FLAG.
010050*
010060   8330-REASONABILITY-CHECK.
010070     IF (I645P-BILL-DATE-DD NOT LESS '19')
010080         AND (I645P-SEQ-YEAR EQUAL WS-EDIT-DATE-YEAR)
010090         AND (I645P-SEQ-MONTH EQUAL WS-EDIT-DATE-MM)
010100*                * LAST BILLING DATE OF DELUXE 4-4-5 MONTH IS LATE
010110*                 IN THE SAME CALENDAR MONTH; GOOD *
010120       GO TO 8330-EXIT.
010130*
010140     IF I645P-BILL-DATE-DD NOT GREATER '07'
010150       IF I645P-SEQ-YEAR EQUAL WS-EDIT-DATE-YEAR
010160         ADD I645P-SEQ-MONTH +1 GIVING WS-IDW-2DIG-WORK
010170         IF WS-IDW-2DIG-WORK EQUAL WS-EDIT-DATE-MM
010180*                * LAST BILLING DATE OF DELUXE 4-4-5 MONTH IS IN
010190*                 FIRST WEEK OF NEXT CALENDAR MONTH; GOOD *
010200           GO TO 8330-EXIT
010210         END-IF
010220       ELSE
010230         IF (I645P-SEQ-MONTH EQUAL 12)
010240             AND (WS-EDIT-DATE-MM EQUAL 01)
010250           ADD I645P-SEQ-YEAR +1 GIVING WS-IDW-4DIG-WORK
010260           IF WS-IDW-4DIG-WORK EQUAL WS-EDIT-DATE-YEAR
010270*                * LAST BILLING DATE OF DELUXE DECEMBER IS IN
010280*                 FIRST WEEK OF NEXT CALENDAR YEAR; GOOD *
010290             GO TO 8330-EXIT.
010300*
010310*                * ONLY BAD SITUATIONS HAVE NOT EXITED BY THIS
010320*                 POINT:  ENDING BILLING DATE FOR DELUXE 4-4-5
010330*                 MONTH IS NEITHER LATE IN THE SAME CALENDAR
010340*                 MONTH, NOR IN THE FIRST WEEK OF THE NEXT *
010350     PERFORM 8340-REJECT-I645P-CARD.
010360     DISPLAY '   MONTH-END BILLING DATE "' I645P-BILLING-DATE
010370           '" NOT REASONABLE FOR 4-4-5 MONTH/YEAR '
010380           I645P-SEQ-MONTH-X '/' I645P-SEQ-YEAR-X '.' UPON SYSOUT.
010390*
010400   8330-EXIT.
010410     EXIT.
010420*
010430   8340-REJECT-I645P-CARD.
010440     IF NOT ERRORS-IN-CARD
010450       MOVE 'Y' TO WS-CARD-ERROR-FLAG
010460       DISPLAY 'I645P CARD "' I645P-INFO '" REJECTED FOR:'
010470             UPON SYSOUT.
010480*
010490   8350-EDIT-BILLING-DATE.
010500     IF (WS-EDIT-DATE (3:6) NOT NUMERIC)
010510         OR (WS-EDIT-DATE-MM-X LESS '01')
010520         OR (WS-EDIT-DATE-MM-X GREATER '12')
010530         OR (WS-EDIT-DATE-DD-X LESS '01')
010540         OR (WS-EDIT-DATE-DD-X GREATER '31')
010550         OR ((WS-EDIT-DATE-DD-X EQUAL '31')
010560             AND (WS-EDIT-DATE-MM-X NOT EQUAL '01' AND '03' AND
010570                   '05' AND '07' AND '08' AND '10' AND '12'))
010580         OR (WS-EDIT-DATE-MMDD EQUAL '0230')
010590*                * FEBRUARY 29TH IS DEALT WITH LATER IN THE
010600*                 PARAGRAPH, AFTER DETERMINING IF IT'S LEAP YEAR *
010610       MOVE 'I' TO WS-DATE-EDIT-FLAG
010620     ELSE
010630       MOVE CPY4-CENTURY-DIGITS TO WS-EDIT-DATE-CC
010640*                * GENERALLY C645 OR PARM BILLING DATE WILL BE IN
010650*                 THE SAME CENTURY AS THE RUN DATE.  EXCEPTIONS
010660*                 WILL OCCUR LATE IN A CENTURY (WHEN AN EARLY NEXT
010670*                 CENTURY BILLING DATE MAY BE ENCOUNTERED) OR
010680*                 EARLY IN A CENTURY (WHEN A LATE PREVIOUS CENTURY
010690*                 BILLING DATE MAY BE ENCOUNTERED).  THE FOLLOWING
010700*                 DEALS WITH THOSE SITUATIONS.  *
010710       IF (WS-COMPUTER-YEAR GREATER 89)
010720           AND (WS-EDIT-DATE-YY LESS 10)
010730         ADD 1 TO WS-EDIT-DATE-CC
010740       ELSE
010750         IF (WS-COMPUTER-YEAR LESS 10)
010760             AND (WS-EDIT-DATE-YY GREATER 89)
010770           SUBTRACT 1 FROM WS-EDIT-DATE-CC
010780         END-IF
010790       END-IF
010800*                * DETERMINE IF LEAP-YEAR:  IF 2-DIGIT YEAR IS 00,
010810*                 DIVIDE CENTURY DIGITS BY 4, OTHERWISE DIVIDE
010820*                 2-DIGIT YEAR BY 4.  IF REMAINDER IS 0, IT'S A
010830*                 LEAP-YEAR.  *
010840       IF WS-EDIT-DATE-YY EQUAL 00
010850         MOVE WS-EDIT-DATE-CC TO WS-IDW-2DIG-WORK
010860       ELSE
010870         MOVE WS-EDIT-DATE-YY TO WS-IDW-2DIG-WORK
010880       END-IF
010890       DIVIDE WS-IDW-2DIG-WORK BY 4 GIVING WS-IDW-2DIG-WORK
010900             REMAINDER WS-IDW-LEAP-YEAR-FLAG
010910       IF (WS-EDIT-DATE-MMDD EQUAL '0229')
010920           AND (NOT IDW-LEAP-YEAR)
010930         MOVE 'I' TO WS-DATE-EDIT-FLAG
010940       ELSE
010950         MOVE SPACE TO WS-DATE-EDIT-FLAG
010960         PERFORM 8360-VERIFY-FRIDAY-DATE.
010970*
010980   8360-VERIFY-FRIDAY-DATE.
010990*                * THIS ROUTINE IS VALID FROM 01-01-1901 THRU
011000*                 02-28-2100, SINCE EVERY 4TH YEAR IN THAT PERIOD
011010*                 IS A LEAP YEAR.  IT WOULD FAIL AFTER 02-28-2100,
011020*                 SINCE 2100 BREAKS THIS PATTERN AND IS NOT A LEAP
011030*                 YEAR.  *
011040*                * ROUTINE COUNTS THE DAYS SINCE 12-31-1900,
011050*                 DISCARDING SOME MULTIPLES OF 7 IN THE PROCESS;
011060*                 E.G., THERE ARE 365 DAYS IN A COMMON (NON LEAP)
011070*                 YEAR, WHICH IS (52 X 7) + 1, AND MAY BE ADDED IN
011080*                 AS 1.  THEN REMAINING MULTIPLES OF 7 ARE
011090*                 DISCARDED (DIVIDE BY 7 WITH REMAINDED) TO GIVE
011100*                 THE DIFFERENCE IN DAY OF THE WEEK BETWEEN
011110*                 12-31-1900 AND THE DATE BEING EVALUATED; IF THE
011120*                 DATE IS FRIDAY, THE REMAINDER IS 4, SINCE
011130*                 12-31-1900 WAS A MONDAY.  *
011140     SUBTRACT 1901 FROM WS-EDIT-DATE-YEAR GIVING WS-IDW-4DIG-WORK.
011150*                * WHOLE YEARS SINCE 12-31-1900, SINCE 365 DAY
011160*                 YEARS MAY BE COUNTED AS 1 DAY EACH.  *
011170     MULTIPLY 1.25 BY WS-IDW-4DIG-WORK.
011180*                * ACCOUNTS FOR FEBRUARY 29TH THRU LAST YEAR (AN
011190*                 ADDITIONAL DAY FOR EACH 366 DAY YEAR).  *
011200     IF IDW-LEAP-YEAR
011210         AND (WS-EDIT-DATE-MM NOT LESS 03)
011220       ADD 1 TO WS-IDW-4DIG-WORK.
011230*                * ACCOUNTS FOR FEBRUARY 29TH THIS YEAR *
011240     ADD WS-DAYS-BEFORE (WS-EDIT-DATE-MM) TO WS-IDW-4DIG-WORK.
011250*                * ACCOUNTS FOR DAYS (OTHER THAN FEBRUARY 29TH)
011260*                 BEFORE THIS MONTH IN THIS YEAR *
011270     ADD WS-EDIT-DATE-DD TO WS-IDW-4DIG-WORK.
011280*                * ACCOUNTS FOR DAYS IN CURRENT MONTH SO FAR.
011290*                 TOTAL NOW IS DAYS SINCE 12-31-1900, MINUS A LOT
011300*                 OF SEVENS.  *
011310     DIVIDE WS-IDW-4DIG-WORK BY 7 GIVING WS-IDW-4DIG-WORK
011320           REMAINDER WS-IDW-WEEK-DAY-FLAG.
011330*                * ELIMINATING THE REMAINING MULTIPLES OF SEVEN
011340*                 GIVES THE DAY OF THE WEEK IN RELATION TO THAT
011350*                 OF 12-31-1900, I.E. DAYS SINCE MONDAY.  *
011360     IF NOT IDW-FRIDAY
011370       MOVE 'D' TO WS-DATE-EDIT-FLAG.
011380*
011390   8400-INIT-SKELETON-545-REC.
011400     MOVE +766 TO O545-LENGTH-REST-OF-REC.
011410     MOVE SPACE TO O545-REST-OF-REC.
011420     MOVE 'H' TO O545-WIP-PROD-CATEGORY.
011430     MOVE '00001' TO O545-WIP-KEY (32:5) O545-DESCRIPTIVE-QTY.
011440*                * LINES DEALING WITH BYTES 37 THRU 43 OF WIP KEY
011450*                 GENERATE A MEANINGLESS CONSTANT FOR THE RUN,
011460*                 BASED ON MONTH, DAY, AND TIME.  IT CANNOT BE
011470*                 DUPLICATED IN ANY RUN FOR A FULL TWELVE MONTHS,
011480*                 PREVENTING THE POSSIBILITY OF DUPLICATE WIP KEYS
011490*                 ENTERING THE BILLING SYSTEM.  *
011500     ADD WS-COMPUTER-MONTH +1 GIVING WS-ALPHA-SUBSCRIPT.
011510     MOVE WS-ALPHA-TABLE-BYTE (WS-ALPHA-SUBSCRIPT) TO
011520           O545-WIP-KEY (37:1).
011530     ADD WS-COMPUTER-DAY +1 GIVING WS-ALPHA-SUBSCRIPT.
011540     MOVE WS-ALPHA-TABLE-BYTE (WS-ALPHA-SUBSCRIPT) TO
011550           O545-WIP-KEY (38:1).
011560     ADD WS-COMPUTER-HOURS +1 GIVING WS-ALPHA-SUBSCRIPT.
011570     MOVE WS-ALPHA-TABLE-BYTE (WS-ALPHA-SUBSCRIPT) TO
011580           O545-WIP-KEY (39:1).
011590     DIVIDE WS-COMPUTER-HUNDREDTH-SECONDS BY 10 GIVING
011600           WS-INIT-LAST-1 REMAINDER WS-INIT-HOLD-HUNDERDTHS.
011610     MOVE WS-COMPUTER-MINUTES TO WS-INIT-FIRST-2.
011620     DIVIDE WS-INIT-3-DIGIT-NBR BY 36 GIVING
011630           WS-ALPHA-SUBSCRIPT REMAINDER WS-INIT-REMAINDER.
011640     ADD +1 TO WS-ALPHA-SUBSCRIPT WS-INIT-REMAINDER.
011650     MOVE WS-ALPHA-TABLE-BYTE (WS-ALPHA-SUBSCRIPT) TO
011660           O545-WIP-KEY (40:1).
011670     MOVE WS-ALPHA-TABLE-BYTE (WS-INIT-REMAINDER) TO
011680           O545-WIP-KEY (41:1).
011690     MOVE WS-INIT-HOLD-HUNDERDTHS TO WS-INIT-LAST-1.
011700     MOVE WS-COMPUTER-SECONDS TO WS-INIT-FIRST-2.
011710     DIVIDE WS-INIT-3-DIGIT-NBR BY 36 GIVING
011720           WS-ALPHA-SUBSCRIPT REMAINDER WS-INIT-REMAINDER.
011730     ADD +1 TO WS-ALPHA-SUBSCRIPT WS-INIT-REMAINDER.
011740     MOVE WS-ALPHA-TABLE-BYTE (WS-ALPHA-SUBSCRIPT) TO
011750           O545-WIP-KEY (42:1).
011760     MOVE WS-ALPHA-TABLE-BYTE (WS-INIT-REMAINDER) TO
011770           O545-WIP-KEY (43:1).
011780     MOVE 01 TO O545-WIP-TRANS-REC-NBR O545-WIP-TRANS-REC-TOTAL.
011790     MOVE +0 TO O545-WVE-RSL-GROSS O545-WVE-RSL-TAX
011800           O545-TAX-ROUNDER O545-WAIVED-UPCHARGE O545-UNITS
011810           O545-GROSS O545-REPORT-BASE O545-REPORT-DELIVERY
011820           O545-TAX O545-UPCHARGE O545-NORMAL-BASE
011830           O545-NORMAL-DELIVERY O545-ADJUSTED-BASE
011840           O545-GAS-ADJUSTMENTS O545-CAPS-APPLIED
011850           O545-CAPS-TRIGGERED O545-PRE-UPCHARGE-TAX
011860           O545-UNIT-GROSS O545-UNIT-REPORT-BASE
011870           O545-UNIT-REPORT-DELIVERY O545-UNIT-TAX
011880           O545-UNIT-UPCHARGE O545-UNIT-NORMAL-BASE
011890           O545-UNIT-NORMAL-DELIVERY O545-UNIT-ADJUSTED-BASE
011900           O545-STATE-TAX-RATE O545-COUNTY-TAX-RATE
011910           O545-CITY-TAX-RATE O545-TRANSIT-TAX-RATE
011920           O545-MISC-TAX-RATE O545-JURISDICTION-TAX-RATE.
011930     MOVE 'Y' TO O545-RECORD-ORIGIN-FLAG.
011940     MOVE '20' TO O545-PRODUCT-SALE-TYPE.
011950     MOVE +1 TO O545-IMPRINT-QUANTITY.
011960     MOVE 'R' TO O545-REORDER-FLAG O545-MAIN-PR-REORDER-FLAG.
011970     STRING WS-SYSOUT-RUN-MONTH WS-SYSOUT-RUN-DAY
011980           WS-SYSOUT-RUN-YEAR DELIMITED BY SIZE INTO
011990           O545-ORDER-ENTRY-DATE.
012000     MOVE O545-ORDER-ENTRY-DATE TO O545-DATE-SHIPPED.
012010     STRING WS-PARM-DATE-MM WS-PARM-DATE-DD WS-PARM-DATE-YY
012020           DELIMITED BY SIZE INTO O545-BILLING-DATE.
012030     MOVE '1' TO O545-PRICE-CODE.
012040     MOVE 'A' TO O545-UPDATE-CODE.
012050     MOVE O545-REC TO WS-SKELETON-545-REC.
012060*
012070   9000-CLOSE.
012080     PERFORM 9100-CLOSE-I3DO.
012090     PERFORM 9200-CLOSE-I550-FILE.
012100     IF NOT WS-I550-SUCCESSFUL
012110       MOVE 'Y' TO WS-RETURN-04-FLAG
012120       DISPLAY 'VSAM ERROR ON I550 CLOSE.  FILE STATUS = '
012130             WS-I550-FILE-STATUS '.' UPON SYSOUT.
012140     IF WS-LINE-COUNT LESS +90
012150*                * REPORT ISN'T TOTALLY EMPTY *
012160       MOVE WS-END-OF-REPORT TO PRINTER1-RECORD
012170       PERFORM 1700-WRITE-PRINTER1-REC.
012180     CLOSE PRINTER1-FILE O545-FILE O16T-FILE I16T-FILE
012190           I645P-FILE.
012200*
012210     DISPLAY '-------  ' WS-PROGRAM-NUMBER ' FILE COUNTS  -------'
012220           UPON SYSOUT.
012230     MOVE WS-I645P-CARD-COUNT TO WS-CD-RECORD-COUNT.
012240     DISPLAY WS-CD-FILE-LINE UPON SYSOUT.
012250     MOVE 'I16T:  ' TO WS-CD-FILE-NAME.
012260     MOVE WS-I16T-CARD-COUNT TO WS-CD-RECORD-COUNT.
012270     MOVE WS-I16T-REJECT-COUNT TO WS-CD-REJECT-COUNT.
012280     MOVE WS-I16T-BYPASS-COUNT TO WS-CD-BYPASS-COUNT.
012290     DISPLAY WS-CD-I16T-LINE UPON SYSOUT.
012300     MOVE 'O545:  ' TO WS-CD-FILE-NAME.
012310     MOVE WS-O545-RECORD-COUNT TO WS-CD-RECORD-COUNT.
012320     MOVE 'RECORDS WRITTEN' TO WS-CD-I-O-TEXT.
012330     DISPLAY WS-CD-FILE-LINE UPON SYSOUT.
012340*
012350     IF RETURN-08
012360       MOVE 08 TO RETURN-CODE WS-RETURN-CODE
012370     ELSE
012380       IF RETURN-04
012390         MOVE 04 TO RETURN-CODE WS-RETURN-CODE.
012400     DISPLAY WS-RETURN-CODE-LINE UPON SYSOUT.
012410*
012420   9100-CLOSE-I3DO.
012430     MOVE 'N' TO WS-I3DO-OPEN-FLAG.
012440      CLOSE I3DO-LDS-FILE.
012450*
012460   9200-CLOSE-I550-FILE.
012470     MOVE 'N' TO WS-I550-FILE-FLAG.
012480     CLOSE I550-FILE.
012490*
012500   9900-DISPLAY-ABORT.
012510     DISPLAY '* ' WS-PROGRAM-NUMBER ' * RUN ABORTED *'
012520           UPON SYSOUT.
012530     IF OPENED-I3DO
012540       PERFORM 9100-CLOSE-I3DO.
012550     IF OPENED-I550-FILE
012560       PERFORM 9200-CLOSE-I550-FILE.
012570     MOVE 16 TO RETURN-CODE WS-RETURN-CODE.
012580     DISPLAY WS-RETURN-CODE-LINE UPON SYSOUT.
012590     STOP RUN.
012600*
012610*
