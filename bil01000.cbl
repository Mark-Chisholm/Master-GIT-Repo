000010  CBL SQL('QUALIFIER CICS BINDFILE')
000020 IDENTIFICATION DIVISION.
DLXMIG* DLXMIG Changes start here
DLXMIG*  PROGRAM-ID. BIL01000.
DLXMIG   PROGRAM-ID. "bil01000".
DLXMIG* DLXMIG Changes end here
000070*PROGRAM-NAME. UPDATE STANDARD DISCOUNT (1030) FILE.
000080   AUTHOR. JACK MASON.
000090   INSTALLATION. DFS, INC.
000100   DATE-WRITTEN. 07-16-97.
000110   DATE-COMPILED.
000120*+***************************************************************       
000130*+ REVIEWED BY CGA/ARC RENOVATOR RENOALL V5.14 ON 1998-2-6 9:16         
000140*+***************************************************************       
000150******************************************************************
000160*
000170*RUN INSTRUCTIONS                                         11-15-99
000180*
000190*I1030  = 1030 INPUT FROM PREVIOUS BIL010     VARIABLE LRECL=13618
000200*         STANDARD DISCOUNT FILE  -  VSAM
000210*
000220*I1078T = C1078 INPUT FROM DATA ENTRY                  LRECL=80
000230*         STANDARD DISCOUNT UPDATE
000240*
000250*O1030  = 1030 OUTPUT TO FILE                 VARIABLE LRECL=13618
000260*         STANDARD DISCOUNT FILE  -  SEQUENTIAL
000270*
000280*PRINTER1 = UPDATE PROOF, 132 CHAR/LINE                LRECL=133
000290*
000300*PRINTER2 = FILE LISTING, 132 CHAR/LINE                LRECL=133
000310*
000320*SYSOUT = REVISION INFO, FILE COUNTS, ERROR MESSAGES; ANY PAPER
000330*
000340******************************************************************
000350*
000360*RETURN CODES                                             07-16-97
000370*
000380*  16 = RUN ABORTED.  SEE SYSOUT.
000390*         - BOTH INPUT FILES EMPTY
000400*         - I1078T SEQUENCE ERROR
000410*         - O1030 PRODUCT TABLE OVERFLOW
000420*         - I1030 VSAM ERROR (EXCEPT ON CLOSE)
000430*
000440*  08 = ERRORS.  RUN CONTINUED.  SEE SYSOUT.
000450*         - ONE OR MORE I1078T CARDS REJECTED FOR ERRORS
000460*
000470*  04 = INFO ONLY.  RUN CONTINUED.  SEE SYSOUT.
000480*         - EMPTY I1030 FILE (FIRST UPDATE)
000490*         - EMPTY I1078T FILE (1030 NOT UPDATED - LISTED ONLY)
000500*         - I1030 VSAM ERROR ON CLOSE
000510*
000520******************************************************************
000530*
000540*NARRATIVE                                                11-15-99
000550*
000560*PURPOSE:
000570*  TO UPDATE AND LIST THE STANDARD DISCOUNT (1030) FILE.
000580*
000590*SOURCE OF INFORMATION:
000600*  THE 1030 FILE (VSAM).
000610*  THE C1078 CARDS IN RECORD KEY, ENTRY/PRODUCT KEY ORDER;
000620*    SORT FIELDS=(1,26,A).
000630*
000640*PROGRAM FLOW:
000650*  IN INITIALIZATION, THE PROGRAM OPENS FILES AND READS THE FIRST
000660*    RECORD OF EACH INPUT.
000670*  PROCESSING IS DRIVEN BY THE C1078 CARDS.  UNMATCHED I1030
000680*    RECORDS ARE PASSED THRU TO OUTPUT WITHOUT CHANGE.  UNMATCHED
000690*    C1078 "ADD" TRANSACTIONS CREATE NEW O1030 RECORDS.  MATCHED
000700*    C1078 TRANSACTIONS ADD, CHANGE, OR DELETE ENTRIES ON THE 1030
000710*    RECORD, OR EVEN DELETE THE ENTIRE RECORD.
000720*  THIS PROGRAM EDITS EACH C1078 CARD BEFORE APPLYING IT TO THE
000730*    1030 RECORD.  IT PRINTS A PROOF OF ALL 1030 ENTRIES ADDED,
000740*    CHANGED, OR DELETED.  IT ALSO PRINTS A LISTING OF ALL O1030
000750*    RECORDS (NEW, CHANGED, OR UNCHANGED).
000760*  WHEN BOTH INPUT FILES ARE AT END, THE PROGRAM CLOSES FILES,
000770*    DISPLAYS RUN TOTALS, AND ENDS.
000780*
000790******************************************************************
000800*
000810*REVISION HISTORY
000820*
000830*07-16-97  PROGRAM WRITTEN.
000840*          PROJECT #903194.  CM #191591.            JACK MASON  01
000850*
000860*08/31/98  MODIFIED FOR DELUXE RENAISSANCE PROJECT
000870*            PROJECT #501789  CHG MGT #192965   CAP GEMINI      02
000880*
000890*11-15-99  MODIFIED FOR EXPANDED 1030 RECORD & KEY.
000900*            PROJECT #903194.  CAPS #9101.          JACK MASON  03
000910*
000920*04-14-03  MODIFIED THE PROGRAM FOR CHANGING AOE MATCH FLAG TO
000930*          ORDER CHANNEL.
000940*          CMN PKG #BILL869   REMEDY #17850     MOHAMED MAHEEN  04
000950*
DLXMIG**----------------------------------------------------------------    
DLXMIG**  DATE      DESCRIPTION                        INITIALS             
DLXMIG** -------  ------------------------------------------------------    
DLXMIG** 092909   UPDATED FOR DLX MIGRATION            COGNIZANT            
DLXMIG** -------  ------------------------------------------------------    
001010******************************************************************
001020 ENVIRONMENT DIVISION.
001030 CONFIGURATION SECTION.
DLXMIG* DLXMIG Changes start here
DLXMIG*  SOURCE-COMPUTER. IBM-PC.
DLXMIG   SOURCE-COMPUTER. UNIX.
DLXMIG* DLXMIG Changes end here
DLXMIG* DLXMIG Changes start here
DLXMIG*  OBJECT-COMPUTER. IBM-370.
DLXMIG   OBJECT-COMPUTER. UNIX.
DLXMIG* DLXMIG Changes end here
001120   SPECIAL-NAMES.
001130     CLASS GENERIC-NUMERIC IS '*', '0' THRU '9'
001140     CLASS GENERIC-ALPHA IS '*', 'A' THRU 'I', 'J' THRU 'R',
001150           'S' THRU 'Z'
001160     CLASS ALPHA IS 'A' THRU 'I', 'J' THRU 'R', 'S' THRU 'Z'
001170     CSP IS NO-ADVANCE
001180     C12 IS C12-LINE-61.
001190*
001200 INPUT-OUTPUT SECTION.
001210   FILE-CONTROL.
001220     SELECT I1030-FILE    ASSIGN I1030
001230                            ORGANIZATION IS INDEXED
001240                            ACCESS MODE IS SEQUENTIAL
001250                            RECORD KEY IS I1030-KEY
001260                            FILE STATUS IS WS-I1030-FILE-STATUS.
001270     SELECT I1078T-FILE  
DLXMIG             ASSIGN RSD-I1078T.
001290     SELECT O1030-FILE 
001300             ASSIGN RSD-O1030.
001310     SELECT PRINTER1-FILE
001320           ASSIGN PRINTER1
DLXMIG                 ORGANIZATION IS LINE SEQUENTIAL.    
001340     SELECT PRINTER2-FILE 
001350           ASSIGN PRINTER2
DLXMIG                 ORGANIZATION IS LINE SEQUENTIAL.
001370*
001380 DATA DIVISION.
001390 FILE SECTION.
001400*
001410 FD  I1030-FILE
001420*
001430 COPY T1030I.
001440*
001450 FD  I1078T-FILE
001460     RECORDING MODE IS F
001470     BLOCK CONTAINS 0 RECORDS
001480     RECORD CONTAINS 80 CHARACTERS
001490     LABEL RECORDS STANDARD.
001500   01  I1078T-REC.
001510     03  I1078T-INFO.
001520       05  I1078T-SEQUENCE.
001530         07  I1078T-RECORD-KEY                   PIC X(4).
001540         07  I1078T-ENTRY-KEY.
001550           09  I1078T-PRODUCT-CODE.
001560             11  I1078T-PROD-EXP-CODE            PIC X(2).
001570             11  I1078T-PROD-NUMERIC-PART        PIC X(10).
001580             11  I1078T-PROD-SUFFIX              PIC X.
001590           09  I1078T-DESCRIPTIVE-QTY            PIC X(5).
001600           09  I1078T-REORDER-FLAG               PIC X.
001610               88  I1078T-VALID-REORDER-FLAG VALUES 'N', 'R', '1'.
001620           09  I1078T-UNITS                      PIC X(2).
001630           09  I1078T-ORDER-CHANNEL              PIC X.
001640       05  I1078T-TRANSACTION-CODE               PIC X.
001650       05  I1078T-ADJUST-INFO.
001660         07  I1078T-STEP-1.
001670           09  I1078T-S1-BASIS                   PIC X.
001680               88  I1078T-S1-DELETE-STEP           VALUE '^'.
001690               88  I1078T-S1-VALID-BASIS           VALUES
001700                   'B', 'D', 'P', 'V' THRU 'Z', '0' THRU '9'.
001710           09  I1078T-S1-RATE-X.
001720             11  I1078T-S1-RATE   DISPLAY        PIC S9(3)V9(2).
001730             11  I1078T-S1-RATE-U REDEFINES I1078T-S1-RATE
001740                                  DISPLAY        PIC 9(3)V9(2).
001750         07  I1078T-STEP-2.
001760           09  I1078T-S2-BASIS                   PIC X.
001770               88  I1078T-S2-DELETE-STEP           VALUE '^'.
001780               88  I1078T-S2-VALID-BASIS           VALUES
001790                   'B', 'D', 'P', 'V' THRU 'Z', '0' THRU '9'.
001800           09  I1078T-S2-RATE-X.
001810             11  I1078T-S2-RATE   DISPLAY        PIC S9(3)V9(2).
001820             11  I1078T-S2-RATE-U REDEFINES I1078T-S2-RATE
001830                                  DISPLAY        PIC 9(3)V9(2).
001840         07  I1078T-STEP-3.
001850           09  I1078T-S3-BASIS                   PIC X.
001860               88  I1078T-S3-DELETE-STEP           VALUE '^'.
001870               88  I1078T-S3-VALID-BASIS           VALUES
001880                   'B', 'D', 'P', 'V' THRU 'Z', '0' THRU '9'.
001890           09  I1078T-S3-RATE-X.
001900             11  I1078T-S3-RATE   DISPLAY        PIC S9(3)V9(2).
001910             11  I1078T-S3-RATE-U REDEFINES I1078T-S3-RATE
001920                                  DISPLAY        PIC 9(3)V9(2).
001930     03  FILLER                                  PIC X(35).
001940*
001950 FD  O1030-FILE
DLXMIG*     RECORDING MODE IS V 
DLXMIG     RECORDING MODE IS F 
001980*
001990 COPY T1030O.
002000*
002010 FD  PRINTER1-FILE
002020     RECORDING MODE IS F
002030     BLOCK CONTAINS 0 RECORDS
002040     RECORD CONTAINS 132 CHARACTERS
002050     LABEL RECORDS OMITTED.
002060   01  PRINTER1-REC.
002070     03  FILLER                                  PIC X(13).
002080     03  PR1-RECORD-KEY                          PIC X(4).
002090     03  FILLER                                  PIC X(6).
002100     03  PR1-PRODUCT-CODE                        PIC X(13).
002110     03  FILLER                                  PIC X(4).
002120     03  PR1-DESCRIPTIVE-QTY                     PIC X(5).
002130     03  FILLER                                  PIC X(8).
002140     03  PR1-REORDER-FLAG                        PIC X.
002150     03  FILLER                                  PIC X(7).
002160     03  PR1-UNITS                               PIC X(2).
002170     03  FILLER                                  PIC X(11).
002180     03  PR1-ORDER-CHANNEL                       PIC X.
002190     03  FILLER                                  PIC X(7).
002200     03  PR1-STEP-NUMBER          DISPLAY        PIC 9.
002210     03  FILLER                                  PIC X(7).
002220     03  PR1-STEP-OLD-ADJUST.
002230       05  PR1-STEP-OLD-BASIS                    PIC X.
002240       05  FILLER                                PIC X(2).
002250       05  PR1-STEP-OLD-DLR-SIGN                 PIC X.
002260       05  PR1-STEP-OLD-RATE                     PIC ++++.99.
002270       05  PR1-STEP-OLD-PCT-SIGN                 PIC X.
002280       05  FILLER                                PIC X(3).
002290     03  FILLER                                  PIC X(5).
002300     03  PR1-STEP-NEW-ADJUST.
002310       05  PR1-STEP-NEW-BASIS                    PIC X.
002320       05  FILLER                                PIC X(2).
002330       05  PR1-STEP-NEW-DLR-SIGN                 PIC X.
002340       05  PR1-STEP-NEW-RATE                     PIC ++++.99.
002350       05  PR1-STEP-NEW-PCT-SIGN                 PIC X.
002360       05  FILLER                                PIC X(3).
002370     03  FILLER                                  PIC X(7).
002380*
002390 FD  PRINTER2-FILE
002400     RECORDING MODE IS F
002410     BLOCK CONTAINS 0 RECORDS
002420     RECORD CONTAINS 132 CHARACTERS
002430     LABEL RECORDS OMITTED.
002440   01  PRINTER2-REC.
002450     03  FILLER                                  PIC X(30).
002460     03  PR2-PRODUCT-CODE                        PIC X(13).
002470     03  FILLER                                  PIC X(4).
002480     03  PR2-DESCRIPTIVE-QTY                     PIC X(5).
002490     03  FILLER                                  PIC X(8).
002500     03  PR2-REORDER-FLAG                        PIC X.
002510     03  FILLER                                  PIC X(6).
002520     03  PR2-UNITS                               PIC X(2).
002530     03  FILLER                                  PIC X(10).
002540     03  PR2-ORDER-CHANNEL                       PIC X.
002550     03  FILLER                                  PIC X(8).
002560     03  PR2-STEP-1-ADJUST.
002570       05  PR2-STEP-1-BASIS                      PIC X.
002580       05  FILLER                                PIC X(2).
002590       05  PR2-STEP-1-DLR-SIGN                   PIC X.
002600       05  PR2-STEP-1-RATE                       PIC ++++.99.
002610       05  PR2-STEP-1-PCT-SIGN                   PIC X.
002620     03  FILLER                                  PIC X(4).
002630     03  PR2-STEP-2-ADJUST.
002640       05  PR2-STEP-2-BASIS                      PIC X.
002650       05  FILLER                                PIC X(2).
002660       05  PR2-STEP-2-DLR-SIGN                   PIC X.
002670       05  PR2-STEP-2-RATE                       PIC ++++.99.
002680       05  PR2-STEP-2-PCT-SIGN                   PIC X.
002690     03  FILLER                                  PIC X(4).
002700     03  PR2-STEP-3-ADJUST.
002710       05  PR2-STEP-3-BASIS                      PIC X.
002720       05  FILLER                                PIC X(2).
002730       05  PR2-STEP-3-DLR-SIGN                   PIC X.
002740       05  PR2-STEP-3-RATE                       PIC ++++.99.
002750       05  PR2-STEP-3-PCT-SIGN                   PIC X.
002760*
002770 WORKING-STORAGE SECTION.
DLXMIG* DLXMIG Changes start here
DLXMIG COPY TDLXMIGE.
DLXMIG     EXEC SQL INCLUDE SQLCA END-EXEC.
DLXMIG* DLXMIG Changes end here
002820*
002830 01  WS-FLAGS-AND-SUCH                           VALUE SPACES.
002840   03  WS-I1030-FILE-STATUS       PIC X(2).
002850     88  WS-I1030-SUCCESSFUL        VALUE '00'.
002860     88  WS-I1030-END-OF-FILE       VALUE '10'.
002870     88  WS-I1030-SUCCESSFUL-OPEN   VALUES '00', '97'.
002880   03  WS-I1030-FILE-FLAG         PIC X.
002890     88  OPENED-I1030-FILE          VALUE 'Y'.
002900   03  WS-I1078T-FILE-FLAG        PIC X.
002910     88  END-I1078T-FILE            VALUE 'E'.
002920   03  WS-RETURN-08-FLAG          PIC X.
002930     88  RETURN-08                  VALUE 'Y'.
002940   03  WS-RETURN-04-FLAG          PIC X.
002950     88  RETURN-04                  VALUE 'Y'.
002960   03  WS-CARD-ERROR-FLAG         PIC X.
002970     88  ERRORS-IN-CARD             VALUE 'Y'.
002980   03  WS-EXPENSE-CODE-FLAG       PIC X.
002990     88  VALID-EXPENSE-CODE         VALUE 'Y'.
003000   03  WS-RECORD-UPDATE-FLAG      PIC X.
003010     88  RECORD-UPDATED             VALUE 'Y'.
003020*
003030 01  WS-SEQUENCES                                VALUE LOW-VALUES.
003040   03  WS-I1078T-SEQUENCE.
003050     05  WS-I1078T-RECORD-KEY     PIC X(4).
003060     05  WS-I1078T-ENTRY-KEY      PIC X(22).
003070   03  WS-HOLD-RECORD-KEY         PIC X(4).
003080*
003090 01  WS-MISC-NUMERICS.
003100   03  WS-PR1-LINE-COUNT          PIC S9(3)      COMP   VALUE +90.
003110   03  WS-PR2-LINE-COUNT          PIC S9(3)      COMP   VALUE +90.
003120   03  WS-PR1-LINE-SPACER         PIC S9         COMP    VALUE +2.
003130   03  WS-PR2-LINE-SPACER         PIC S9         COMP    VALUE +2.
003140   03  WS-I1078T-CARD-COUNT       PIC S9(5)      COMP-3  VALUE +0.
003150   03  WS-I1078T-REJECT-COUNT     PIC S9(3)      COMP-3  VALUE +0.
003160   03  WS-I1030-RECORD-COUNT      PIC S9(3)      COMP-3  VALUE +0.
003170   03  WS-O1030-RECORD-COUNT      PIC S9(3)      COMP-3  VALUE +0.
003180   03  WS-ADJ-TBL-ENTRY-COUNT     PIC S9(3)      COMP-3  VALUE +0.
003190*
003200 COPY CPY109.
003210*
003220 01  WS-ENTRY.
003230   03  WS-ENT-PRODUCT-KEY.
003240     05  WS-ENT-PRODUCT-CODE      PIC X(13).
003250     05  WS-ENT-DESCRIPTIVE-QTY   PIC X(5).
003260     05  WS-ENT-REORDER-FLAG      PIC X.
003270     05  WS-ENT-UNITS             PIC X(2).
003280     05  WS-ENT-ORDER-CHANNEL     PIC X.
003290   03  WS-ENT-ADJUST-INFO.
003300     05  WS-ENT-STEP-1.
003310       07  WS-ENT-S1-BASIS        PIC X.
003320       07  WS-ENT-S1-RATE-X.
003330         09  WS-ENT-S1-RATE-N     PIC S9(3)V9(2) COMP-3.
003340     05  WS-ENT-STEP-2.
003350       07  WS-ENT-S2-BASIS        PIC X.
003360       07  WS-ENT-S2-RATE-X.
003370         09  WS-ENT-S2-RATE-N     PIC S9(3)V9(2) COMP-3.
003380     05  WS-ENT-STEP-3.
003390       07  WS-ENT-S3-BASIS        PIC X.
003400       07  WS-ENT-S3-RATE-X.
003410         09  WS-ENT-S3-RATE-N     PIC S9(3)V9(2) COMP-3.
003420*
003430 01  WS-OLD-ADJUSTMENTS.
003440   03  WS-OLD-S1-BASIS            PIC X.
003450   03  WS-OLD-S1-RATE-N           PIC S9(3)V9(2) COMP-3.
003460   03  WS-OLD-S2-BASIS            PIC X.
003470   03  WS-OLD-S2-RATE-N           PIC S9(3)V9(2) COMP-3.
003480   03  WS-OLD-S3-BASIS            PIC X.
003490   03  WS-OLD-S3-RATE-N           PIC S9(3)V9(2) COMP-3.
003500*
003510 01  WS-HEADINGS-ETC.
003520   03  WS-PR1-HEADING-1.
003530     05  FILLER                   PIC X          VALUE '-'.
003540     05  WS-PR1-HD1-PROGRAM-NBR   PIC X(8)       VALUE SPACE.
003550     05  FILLER                   PIC X(36)      VALUE '-'.
003560     05  FILLER                   PIC X(68)      VALUE
003570                     'STANDARD DISCOUNT (1030) FILE UPDATE PROOF'.
003580     05  FILLER                   PIC X(9)       VALUE 'RUN DATE'.
003590     05  WS-PR1-HD1-RUN-DATE                   VALUE '00/00/0000'.
003600       07  WS-PR1-HD1-RUN-MONTH   PIC X(2).
003610       07  FILLER                 PIC X.
003620       07  WS-PR1-HD1-RUN-DAY     PIC X(2).
003630       07  FILLER                 PIC X.
003640       07  WS-PR1-HD1-RUN-YEAR    PIC X(4).
003650   03  WS-PR1-HEADING-2.
003660     05  FILLER                   PIC X(50)      VALUE
003670             '            RECORD     PRODUCT CODE   DESCR QTY   '.
003680     05  FILLER                   PIC X(55)      VALUE
003690        'REORDER   UNITS   ORD CHNL    STEP#    OLD BASIS & RATE'.
003700     05  FILLER                   PIC X(27)      VALUE
003710                                    '    NEW BASIS & RATE       '.
003720   03  WS-PR2-HEADING-1.
003730     05  FILLER                   PIC X          VALUE '-'.
003740     05  WS-PR2-HD1-PROGRAM-NBR   PIC X(8)       VALUE SPACE.
003750     05  FILLER                   PIC X(38)      VALUE '-'.
003760     05  FILLER                   PIC X(66)      VALUE
003770                          'STANDARD DISCOUNT (1030) FILE LISTING'.
003780     05  FILLER                   PIC X(9)       VALUE 'RUN DATE'.
003790     05  WS-PR2-HD1-RUN-DATE      PIC X(10)    VALUE '00/00/0000'.
003800   03  WS-PR2-HEADING-2A.
003810     05  FILLER                   PIC X(11)   VALUE 'RECORD KEY '.
003820     05  WS-PR2-HD2A-RECORD-KEY   PIC X(4)       VALUE SPACE.
003830     05  FILLER                   PIC X          VALUE SPACE.
003840     05  WS-PR2-HD2A-CONTINUED    PIC X(11)      VALUE SPACE.
003850     05  FILLER                   PIC X(3)       VALUE SPACE.
003860     05  FILLER                   PIC X(51)     VALUE 'ENTRY KEY'.
003870     05  FILLER                   PIC X(5)       VALUE SPACE.
003880     05  FILLER                   PIC X(14)      VALUE 'STEP 1'.
003890     05  FILLER                   PIC X(2)       VALUE SPACE.
003900     05  FILLER                   PIC X(16)      VALUE 'STEP 2'.
003910     05  FILLER                   PIC X(14)      VALUE 'STEP 3'.
003920   03  WS-PR2-HEADING-2B.
003930     05  FILLER                   PIC X(30)      VALUE SPACE.
003940     05  FILLER                   PIC X(51)      VALUE ALL '_'.
003950     05  FILLER                   PIC X(5)       VALUE SPACE.
003960     05  FILLER                   PIC X(13)      VALUE ALL '_'.
003970     05  FILLER                   PIC X(3)       VALUE SPACE.
003980     05  FILLER                   PIC X(13)      VALUE ALL '_'.
003990     05  FILLER                   PIC X(3)       VALUE SPACE.
004000     05  FILLER                   PIC X(13)      VALUE ALL '_'.
004010     05  FILLER                   PIC X          VALUE SPACE.
004020   03  WS-PR2-HEADING-3.
004030     05  FILLER                  PIC X(15) VALUE '  LAST UPDATED'.
004040     05  WS-PR2-HD3-UPDT-MONTH    PIC X(2)       VALUE SPACE.
004050     05  FILLER                   PIC X          VALUE '/'.
004060     05  WS-PR2-HD3-UPDT-DAY      PIC X(2)       VALUE SPACE.
004070     05  FILLER                   PIC X          VALUE '/'.
004080     05  WS-PR2-HD3-UPDT-YEAR     PIC X(4)       VALUE SPACE.
004090     05  FILLER                   PIC X(5)       VALUE SPACE.
004100     05  FILLER                   PIC X(56)      VALUE
004110            'PRODUCT CODE   DESCR QTY   REORDER  UNITS  ORD CHNL'.
004120     05  FILLER                   PIC X(16) VALUE 'BASIS    RATE'.
004130     05  FILLER                   PIC X(16) VALUE 'BASIS    RATE'.
004140     05  FILLER                   PIC X(14) VALUE 'BASIS    RATE'.
004150   03  WS-PR2-HEADING-4.
004160     05  FILLER                   PIC X(14) VALUE '  ENTRY COUNT'.
004170     05  WS-PR2-HD4-ENTRY-COUNT   PIC ZZ9.
004180     05  FILLER                   PIC X(115)     VALUE SPACE.
004190   03  WS-END-OF-REPORT.
004200     05  FILLER                   PIC X(20)      VALUE ALL '-'.
004210     05  FILLER                 PIC X(15) VALUE ' END OF REPORT '.
004220     05  FILLER                   PIC X(20)      VALUE ALL '-'.
004230*
004240 01  WS-ADJUST-TABLE.
004250   03  WS-ADJ-TBL-ENTRY           OCCURS 0 TO 400 TIMES
004260                                  DEPENDING ON
004270                                        WS-ADJ-TBL-ENTRY-COUNT
004280                                  INDEXED BY IX-WS-ADJ-TBL.
004290     05  WS-ADJ-TBL-ENTRY-KEY     PIC X(22).
004300     05  WS-ADJ-TBL-ADJUST-INFO   PIC X(12).
004310*
004320 01  WS-CLOSING-DISPLAYS.
004330   03  WS-CD-I1078T-LINE.
004340     05  FILLER                   PIC X(8)       VALUE 'I1078T: '.
004350     05  WS-CD-I1078T-CARD-COUNT  PIC ZZ,ZZ9.
004360     05  FILLER                   PIC X(18)   VALUE ' CARDS READ'.
004370     05  WS-CD-I1078T-REJECT-CT   PIC ZZ9.
004380     05  FILLER                   PIC X(9)      VALUE ' REJECTED'.
004390   03  WS-CD-1030-LINE.
004400     05  WS-CD-1030-I-OR-O        PIC X          VALUE 'I'.
004410     05  FILLER                   PIC X(10)      VALUE '1030:'.
004420     05  WS-CD-1030-RECORD-COUNT  PIC ZZ9.
004430     05  FILLER                   PIC X(9)       VALUE ' RECORDS'.
004440     05  WS-CD-1030-VERB          PIC X(7)       VALUE 'READ'.
004450   03  WS-RETURN-CODE-LINE.
004460     05  FILLER                   PIC X(14) VALUE 'RETURN CODE ='.
004470     05  WS-RETURN-CODE           PIC 9(2)       DISPLAY VALUE 00.
004480     05  FILLER                   PIC X          VALUE '.'.
004490*
004500 COPY CPY004.
004510*
004520 01  WS-PROGRAM-NUMBER            PIC X(8)       VALUE 'BIL01004'.
004530 01  WS-REVISION-DATE             PIC X(8)       VALUE '04-14-03'.
004540*
004550 PROCEDURE DIVISION.
004560*
DLXMIG 0000-SQL-DB-CONNECT.
DLXMIG     MOVE 'DLXVSAM' TO DBNAME.
DLXMIG COPY CPYMIGUD.
004600   0000-MAIN-LINE.
004610     PERFORM 8000-INITIALIZE.
004620     PERFORM 1000-PROCESS UNTIL END-I1078T-FILE.
004630     PERFORM 9000-CLOSE.
004640     STOP RUN.
004650*
004660   1000-PROCESS.
004670     MOVE WS-I1078T-RECORD-KEY TO WS-HOLD-RECORD-KEY.
004680     PERFORM 1100-PASS-THRU-I1030
004690         WITH TEST BEFORE
004700         UNTIL WS-I1030-END-OF-FILE
004710             OR (I1030-KEY NOT LESS WS-HOLD-RECORD-KEY).
004720     PERFORM 1800-EDIT-I1078T-REC-LVL-INFO.
004730     IF ERRORS-IN-CARD
004740       PERFORM 3400-READ-I1078T-FILE
004750     ELSE
004760       IF I1078T-TRANSACTION-CODE EQUAL 'X'
004770         PERFORM 2000-DELETE-RECORD
004780       ELSE
004790         PERFORM 3000-CREATE-OR-UPDATE-REC.
004800*
004810   1100-PASS-THRU-I1030.
004820     MOVE I1030-KEY TO O1030-KEY.
004830     MOVE I1030-DATE-OF-LAST-UPDATE TO O1030-DATE-OF-LAST-UPDATE.
004840     MOVE I1030-ENTRY-COUNT TO O1030-ENTRY-COUNT.
004850     MOVE I1030-ADJUSTMENT-TABLE TO O1030-ADJUSTMENT-TABLE.
004860     PERFORM 1300-WRITE-O1030-REC.
004870     PERFORM 1200-READ-I1030-FILE.
004880*
004890   1200-READ-I1030-FILE.
004900     READ I1030-FILE.
004910     IF WS-I1030-SUCCESSFUL
004920       ADD 1 TO WS-I1030-RECORD-COUNT
004930     ELSE
004940       IF NOT WS-I1030-END-OF-FILE
004950         DISPLAY '* VSAM ERROR ON I1030 READ.  FILE STATUS = '
004960               WS-I1030-FILE-STATUS '. *' UPON SYSOUT
004970         GO TO 9900-DISPLAY-ABORT.
004980*
004990   1300-WRITE-O1030-REC.
005000     PERFORM 1400-LIST-O1030-REC.
005010     WRITE O1030-REC.
005020     ADD 1 TO WS-O1030-RECORD-COUNT.
005030*
005040   1400-LIST-O1030-REC.
005050     MOVE O1030-KEY TO WS-PR2-HD2A-RECORD-KEY.
005060     MOVE SPACE TO WS-PR2-HD2A-CONTINUED.
005070     MOVE O1030-MONTH-OF-LAST-UPDATE TO WS-PR2-HD3-UPDT-MONTH.
005080     MOVE O1030-DAY-OF-LAST-UPDATE TO WS-PR2-HD3-UPDT-DAY.
005090     MOVE O1030-YEAR-OF-LAST-UPDATE TO WS-PR2-HD3-UPDT-YEAR.
005100     MOVE O1030-ENTRY-COUNT TO WS-PR2-HD4-ENTRY-COUNT.
005110     PERFORM 1700-PRINTER2-HEADINGS.
005120     PERFORM 1500-PRINTER2-ENTRY
005130           VARYING O1030-IX-ENTRY FROM +1 BY +1
005140           UNTIL O1030-IX-ENTRY GREATER O1030-ENTRY-COUNT.
005150*
005160   1500-PRINTER2-ENTRY.
005170     IF WS-PR2-LINE-COUNT GREATER +59
005180       MOVE '(CONTINUED)' TO WS-PR2-HD2A-CONTINUED
005190       PERFORM 1700-PRINTER2-HEADINGS.
005200     MOVE SPACE TO PRINTER2-REC.
005210     MOVE O1030-ADJUSTMENT-ENTRY (O1030-IX-ENTRY) TO WS-ENTRY.
005220     MOVE WS-ENT-PRODUCT-CODE TO PR2-PRODUCT-CODE.
005230     MOVE WS-ENT-DESCRIPTIVE-QTY TO PR2-DESCRIPTIVE-QTY.
005240     MOVE WS-ENT-REORDER-FLAG TO PR2-REORDER-FLAG.
005250     MOVE WS-ENT-UNITS TO PR2-UNITS.
005260     MOVE WS-ENT-ORDER-CHANNEL TO PR2-ORDER-CHANNEL.
005270     IF WS-ENT-S1-BASIS EQUAL SPACE
005280       MOVE '(NONE)' TO PR2-STEP-1-ADJUST (1:6)
005290     ELSE
005300       MOVE WS-ENT-S1-BASIS TO PR2-STEP-1-BASIS
005310       MOVE WS-ENT-S1-RATE-N TO PR2-STEP-1-RATE
005320       IF WS-ENT-S1-BASIS IS NUMERIC
005330         MOVE '$' TO PR2-STEP-1-DLR-SIGN
005340       ELSE
005350         MOVE '%' TO PR2-STEP-1-PCT-SIGN.
005360     IF WS-ENT-S2-BASIS EQUAL SPACE
005370       MOVE '(NONE)' TO PR2-STEP-2-ADJUST (1:6)
005380     ELSE
005390       MOVE WS-ENT-S2-BASIS TO PR2-STEP-2-BASIS
005400       MOVE WS-ENT-S2-RATE-N TO PR2-STEP-2-RATE
005410       IF WS-ENT-S2-BASIS IS NUMERIC
005420         MOVE '$' TO PR2-STEP-2-DLR-SIGN
005430       ELSE
005440         MOVE '%' TO PR2-STEP-2-PCT-SIGN.
005450     IF WS-ENT-S3-BASIS EQUAL SPACE
005460       MOVE '(NONE)' TO PR2-STEP-3-ADJUST (1:6)
005470     ELSE
005480       MOVE WS-ENT-S3-BASIS TO PR2-STEP-3-BASIS
005490       MOVE WS-ENT-S3-RATE-N TO PR2-STEP-3-RATE
005500       IF WS-ENT-S3-BASIS IS NUMERIC
005510         MOVE '$' TO PR2-STEP-3-DLR-SIGN
005520       ELSE
005530         MOVE '%' TO PR2-STEP-3-PCT-SIGN.
005540     PERFORM 1600-WRITE-PRINTER2-REC.
005550*
005560   1600-WRITE-PRINTER2-REC.
005570     WRITE PRINTER2-REC AFTER WS-PR2-LINE-SPACER.
005580     ADD WS-PR2-LINE-SPACER TO WS-PR2-LINE-COUNT.
005590*
005600   1700-PRINTER2-HEADINGS.
005610     WRITE PRINTER2-REC FROM WS-PR2-HEADING-1 AFTER PAGE.
005620     MOVE +1 TO WS-PR2-LINE-COUNT.
005630     MOVE +2 TO WS-PR2-LINE-SPACER.
005640     MOVE WS-PR2-HEADING-2A TO PRINTER2-REC.
005650     PERFORM 1600-WRITE-PRINTER2-REC.
DLXMIG*     WRITE PRINTER2-REC FROM WS-PR2-HEADING-2B AFTER NO-ADVANCE.
DLXMIG     WRITE PRINTER2-REC FROM WS-PR2-HEADING-2B.
005680     MOVE +1 TO WS-PR2-LINE-SPACER.
005690     MOVE WS-PR2-HEADING-3 TO PRINTER2-REC.
005700     PERFORM 1600-WRITE-PRINTER2-REC.
005710     MOVE WS-PR2-HEADING-4 TO PRINTER2-REC.
005720     PERFORM 1600-WRITE-PRINTER2-REC.
005730*
005740   1800-EDIT-I1078T-REC-LVL-INFO.
005750     IF (I1078T-TRANSACTION-CODE EQUAL 'X')
005760         AND (WS-I1078T-ENTRY-KEY NOT EQUAL SPACE)
005770       PERFORM 3500-REJECT-I1078T-CARD
005780       DISPLAY '   NON-BLANK ENTRY/PRODUCT KEY "'
005790             WS-I1078T-ENTRY-KEY '" ON ENTIRE RECORD DELETE'
005800             UPON SYSOUT
005810     ELSE
005820       IF WS-I1030-END-OF-FILE
005830           OR (I1030-KEY NOT EQUAL WS-I1078T-RECORD-KEY)
005840         IF I1078T-TRANSACTION-CODE EQUAL 'X'
005850           PERFORM 3500-REJECT-I1078T-CARD
005860           DISPLAY '   ENTIRE RECORD DELETE FOR A KEY "'
005870                 WS-I1078T-RECORD-KEY '" NOT ON THE I1030 FILE'
005880                 UPON SYSOUT
005890         ELSE
005900           IF SPACE EQUAL WS-I1078T-RECORD-KEY (1:1) OR
005910                 WS-I1078T-RECORD-KEY (2:1) OR
005920                 WS-I1078T-RECORD-KEY (3:1) OR
005930                 WS-I1078T-RECORD-KEY (4:1)
005940             PERFORM 3500-REJECT-I1078T-CARD
005950             DISPLAY '   RECORD KEY "' WS-I1078T-RECORD-KEY
005960                   '" CONTAINS ONE OR MORE SPACES' UPON SYSOUT.
005970*
005980   2000-DELETE-RECORD.
005990     MOVE I1030-ENTRY-COUNT TO WS-ADJ-TBL-ENTRY-COUNT.
006000     MOVE I1030-ADJUSTMENT-TABLE TO WS-ADJUST-TABLE.
006010     SET IX-WS-ADJ-TBL TO +1.
006020     PERFORM 2100-DELETE-ENTRY
006030         UNTIL IX-WS-ADJ-TBL GREATER WS-ADJ-TBL-ENTRY-COUNT.
006040     PERFORM 1200-READ-I1030-FILE.
006050     PERFORM 3400-READ-I1078T-FILE.
006060*
006070   2100-DELETE-ENTRY.
006080     PERFORM 3100-GET-ENT-FROM-WS-TBL.
006090     IF (WS-PR1-LINE-COUNT GREATER +58)
006100         OR ((WS-PR1-LINE-COUNT EQUAL +58)
006110             AND (WS-ENT-S2-BASIS NOT EQUAL SPACE))
006120         OR ((WS-PR1-LINE-COUNT EQUAL +57)
006130             AND (WS-ENT-S3-BASIS NOT EQUAL SPACE))
006140       PERFORM 2200-PRINTER1-HEADINGS
006150     ELSE
006160       MOVE +2 TO WS-PR1-LINE-SPACER.
006170     MOVE SPACE TO PRINTER1-REC.
006180     MOVE WS-HOLD-RECORD-KEY TO PR1-RECORD-KEY.
006190     MOVE WS-ENT-PRODUCT-CODE TO PR1-PRODUCT-CODE.
006200     MOVE WS-ENT-DESCRIPTIVE-QTY TO PR1-DESCRIPTIVE-QTY.
006210     MOVE WS-ENT-REORDER-FLAG TO PR1-REORDER-FLAG.
006220     MOVE WS-ENT-UNITS TO PR1-UNITS.
006230     MOVE WS-ENT-ORDER-CHANNEL TO PR1-ORDER-CHANNEL.
006240     MOVE 1 TO PR1-STEP-NUMBER.
006250     MOVE '(ENTRY DELETED)' TO PR1-STEP-NEW-ADJUST.
006260     IF WS-ENT-S1-BASIS EQUAL SPACE
006270       MOVE '(NONE)' TO PR1-STEP-OLD-ADJUST (1:6)
006280     ELSE
006290       MOVE WS-ENT-S1-BASIS TO PR1-STEP-OLD-BASIS
006300       MOVE WS-ENT-S1-RATE-N TO PR1-STEP-OLD-RATE
006310       IF WS-ENT-S1-BASIS IS NUMERIC
006320         MOVE '$' TO PR1-STEP-OLD-DLR-SIGN
006330       ELSE
006340         MOVE '%' TO PR1-STEP-OLD-PCT-SIGN
006350       END-IF
006360       IF WS-ENT-S2-BASIS NOT EQUAL SPACE
006370         PERFORM 2300-WRITE-PRINTER1-REC
006380         MOVE +1 TO WS-PR1-LINE-SPACER
006390         MOVE SPACE TO PRINTER1-REC
006400         MOVE 2 TO PR1-STEP-NUMBER
006410         MOVE WS-ENT-S2-BASIS TO PR1-STEP-OLD-BASIS
006420         MOVE WS-ENT-S2-RATE-N TO PR1-STEP-OLD-RATE
006430         IF WS-ENT-S2-BASIS IS NUMERIC
006440           MOVE '$' TO PR1-STEP-OLD-DLR-SIGN
006450         ELSE
006460           MOVE '%' TO PR1-STEP-OLD-PCT-SIGN
006470         END-IF
006480         IF WS-ENT-S3-BASIS NOT EQUAL SPACE
006490           PERFORM 2300-WRITE-PRINTER1-REC
006500           MOVE SPACE TO PRINTER1-REC
006510           MOVE 3 TO PR1-STEP-NUMBER
006520           MOVE WS-ENT-S3-BASIS TO PR1-STEP-OLD-BASIS
006530           MOVE WS-ENT-S3-RATE-N TO PR1-STEP-OLD-RATE
006540           IF WS-ENT-S3-BASIS IS NUMERIC
006550             MOVE '$' TO PR1-STEP-OLD-DLR-SIGN
006560           ELSE
006570             MOVE '%' TO PR1-STEP-OLD-PCT-SIGN.
006580     PERFORM 2300-WRITE-PRINTER1-REC.
006590*
006600   2200-PRINTER1-HEADINGS.
006610     WRITE PRINTER1-REC FROM WS-PR1-HEADING-1 AFTER PAGE.
006620     MOVE +2 TO WS-PR1-LINE-SPACER.
006630     MOVE +1 TO WS-PR1-LINE-COUNT.
006640     MOVE WS-PR1-HEADING-2 TO PRINTER1-REC.
006650     PERFORM 2300-WRITE-PRINTER1-REC.
006660*
006670   2300-WRITE-PRINTER1-REC.
006680     WRITE PRINTER1-REC AFTER WS-PR1-LINE-SPACER.
006690     ADD WS-PR1-LINE-SPACER TO WS-PR1-LINE-COUNT.
006700*
006710   3000-CREATE-OR-UPDATE-REC.
006720     MOVE SPACE TO WS-RECORD-UPDATE-FLAG.
006730     MOVE WS-HOLD-RECORD-KEY TO O1030-KEY.
006740     MOVE +0 TO O1030-ENTRY-COUNT.
006750     IF WS-I1030-END-OF-FILE
006760         OR (I1030-KEY NOT EQUAL WS-HOLD-RECORD-KEY)
006770       MOVE SPACE TO O1030-DATE-OF-LAST-UPDATE
006780       MOVE +0 TO WS-ADJ-TBL-ENTRY-COUNT
006790     ELSE
006800       MOVE I1030-DATE-OF-LAST-UPDATE TO O1030-DATE-OF-LAST-UPDATE
006810       MOVE I1030-ENTRY-COUNT TO WS-ADJ-TBL-ENTRY-COUNT
006820       MOVE I1030-ADJUSTMENT-TABLE TO WS-ADJUST-TABLE
006830       PERFORM 1200-READ-I1030-FILE.
006840     SET IX-WS-ADJ-TBL TO +1.
006850     PERFORM 3300-EACH-I1078T-FOR-KEY
006860         WITH TEST AFTER
006870         UNTIL END-I1078T-FILE
006880             OR (WS-I1078T-RECORD-KEY NOT EQUAL
006890                   WS-HOLD-RECORD-KEY).
006900     PERFORM
006910         WITH TEST BEFORE
006920         UNTIL IX-WS-ADJ-TBL GREATER WS-ADJ-TBL-ENTRY-COUNT
006930       PERFORM 3100-GET-ENT-FROM-WS-TBL
006940       PERFORM 3200-LOAD-ENT-TO-O1030
006950     END-PERFORM.
006960     IF O1030-ENTRY-COUNT GREATER +0
006970       IF RECORD-UPDATED
006980         MOVE CPY4-FOUR-DIGIT-YEAR TO O1030-YEAR-OF-LAST-UPDATE
006990         MOVE WS-COMPUTER-MONTH TO O1030-MONTH-OF-LAST-UPDATE
007000         MOVE WS-COMPUTER-DAY TO O1030-DAY-OF-LAST-UPDATE
007010       END-IF
007020       PERFORM 1300-WRITE-O1030-REC.
007030*
007040   3100-GET-ENT-FROM-WS-TBL.
007050     MOVE WS-ADJ-TBL-ENTRY (IX-WS-ADJ-TBL) TO WS-ENTRY.
007060     SET IX-WS-ADJ-TBL UP BY 1.
007070*
007080   3200-LOAD-ENT-TO-O1030.
007090     IF O1030-ENTRY-COUNT NOT LESS +400
007100       DISPLAY '* O1030 PRODUCT/ENTRY TABLE OVERFLOWED FOR KEY "'
007110             WS-HOLD-RECORD-KEY '" *' UPON SYSOUT
007120       GO TO 9900-DISPLAY-ABORT.
007130     ADD 1 TO O1030-ENTRY-COUNT.
007140     MOVE WS-ENTRY TO O1030-ADJUSTMENT-ENTRY (O1030-ENTRY-COUNT).
007150*
007160   3300-EACH-I1078T-FOR-KEY.
007170     PERFORM
007180         WITH TEST BEFORE
007190         UNTIL (IX-WS-ADJ-TBL GREATER WS-ADJ-TBL-ENTRY-COUNT)
007200             OR (WS-ADJ-TBL-ENTRY-KEY (IX-WS-ADJ-TBL) NOT LESS
007210                   I1078T-ENTRY-KEY)
007220       PERFORM 3100-GET-ENT-FROM-WS-TBL
007230       PERFORM 3200-LOAD-ENT-TO-O1030
007240     END-PERFORM.
007250     IF I1078T-TRANSACTION-CODE EQUAL 'A' OR 'C' OR 'D'
007260       IF (IX-WS-ADJ-TBL GREATER WS-ADJ-TBL-ENTRY-COUNT)
007270           OR (WS-ADJ-TBL-ENTRY-KEY (IX-WS-ADJ-TBL) NOT EQUAL
007280                 I1078T-ENTRY-KEY)
007290         IF I1078T-TRANSACTION-CODE EQUAL 'A'
007300           PERFORM 4000-I1078T-ADD-CARD
007310         ELSE
007320           PERFORM 3500-REJECT-I1078T-CARD
007330           IF I1078T-TRANSACTION-CODE EQUAL 'C'
007340             DISPLAY '   CHANGE FOR PRODUCT/ENTRY "'
007350                   I1078T-ENTRY-KEY '" NOT ON THIS 1030 RECORD'
007360                   UPON SYSOUT
007370           ELSE
007380             DISPLAY '   DELETE FOR PRODUCT/ENTRY "'
007390                   I1078T-ENTRY-KEY '" NOT ON THIS 1030 RECORD'
007400                   UPON SYSOUT
007410       ELSE
007420         IF I1078T-TRANSACTION-CODE EQUAL 'A'
007430           PERFORM 3500-REJECT-I1078T-CARD
007440           DISPLAY '   ADD FOR PRODUCT/ENTRY "' I1078T-ENTRY-KEY
007450                 '" ALREADY ON THIS 1030 RECORD'
007460                 UPON SYSOUT
007470         ELSE
007480           IF I1078T-TRANSACTION-CODE EQUAL 'C'
007490             PERFORM 5000-I1078T-CHANGE-CARD
007500           ELSE
007510             PERFORM 2100-DELETE-ENTRY
007520             MOVE 'Y' TO WS-RECORD-UPDATE-FLAG
007530     ELSE
007540       PERFORM 3500-REJECT-I1078T-CARD
007550       IF I1078T-TRANSACTION-CODE EQUAL 'X'
007560         DISPLAY '   TRANSACTION TO DELETE ENTIRE RECORD AFTER OTH
007570-              'ER CARDS TO UPDATE RECORD "' WS-HOLD-RECORD-KEY
007580               '"' UPON SYSOUT
007590       ELSE
007600         DISPLAY '   INVALID TRANSACTION CODE "'
007610               I1078T-TRANSACTION-CODE '"' UPON SYSOUT.
007620     PERFORM 3400-READ-I1078T-FILE.
007630*
007640   3400-READ-I1078T-FILE.
007650     IF ERRORS-IN-CARD
007660       MOVE SPACE TO WS-CARD-ERROR-FLAG
007670       MOVE 'Y' TO WS-RETURN-08-FLAG
007680       ADD 1 TO WS-I1078T-REJECT-COUNT.
007690     READ I1078T-FILE AT END
007700       MOVE 'E' TO WS-I1078T-FILE-FLAG.
007710     IF NOT END-I1078T-FILE
007720       IF I1078T-SEQUENCE GREATER WS-I1078T-SEQUENCE
007730         MOVE I1078T-SEQUENCE TO WS-I1078T-SEQUENCE
007740         ADD 1 TO WS-I1078T-CARD-COUNT
007750       ELSE
007760         DISPLAY '* I1078T SEQUENCE ERROR "' WS-I1078T-SEQUENCE
007770               '" "' I1078T-SEQUENCE '" *' UPON SYSOUT
007780         GO TO 9900-DISPLAY-ABORT.
007790*
007800   3500-REJECT-I1078T-CARD.
007810     IF NOT ERRORS-IN-CARD
007820       MOVE 'Y' TO WS-CARD-ERROR-FLAG
007830       DISPLAY 'I1078T RECORD "' I1078T-INFO '" REJECTED FOR:'
007840             UPON SYSOUT.
007850*
007860   4000-I1078T-ADD-CARD.
007870     MOVE I1078T-ENTRY-KEY TO WS-ENT-PRODUCT-KEY.
007880     MOVE SPACE TO WS-ENT-ADJUST-INFO.
007890     IF I1078T-ENTRY-KEY NOT EQUAL SPACE
007900       PERFORM 4100-EDIT-ADD-ENTRY-KEY.
007910     IF I1078T-STEP-1 NOT EQUAL SPACE
007920       PERFORM 4200-EDIT-ADD-STEP-1.
007930     IF I1078T-STEP-2 NOT EQUAL SPACE
007940       PERFORM 4300-EDIT-ADD-STEP-2.
007950     IF I1078T-STEP-3 NOT EQUAL SPACE
007960       PERFORM 4400-EDIT-ADD-STEP-3.
007970     IF NOT ERRORS-IN-CARD
007980       PERFORM 4500-CHECK-AGAINST-BLANK-ENTRY
007990       IF NOT ERRORS-IN-CARD
008000         PERFORM 4600-ADD-ENTRY
008010         MOVE 'Y' TO WS-RECORD-UPDATE-FLAG.
008020*
008030   4100-EDIT-ADD-ENTRY-KEY.
008040     IF I1078T-PRODUCT-CODE NOT EQUAL SPACE
008050       SEARCH ALL CPY109-TBL2-EXPENSE-CODE
008060         AT END
008070           MOVE SPACE TO WS-EXPENSE-CODE-FLAG
008080         WHEN CPY109-TBL2-EXPENSE-CODE (CPY109-IX-TBL2) EQUAL
008090               I1078T-PROD-EXP-CODE
008100           MOVE 'Y' TO WS-EXPENSE-CODE-FLAG
008110       END-SEARCH
008120       IF (NOT VALID-EXPENSE-CODE)
008130           OR (I1078T-PROD-NUMERIC-PART NOT GENERIC-NUMERIC)
008140           OR (I1078T-PROD-SUFFIX NOT GENERIC-ALPHA)
008150         PERFORM 3500-REJECT-I1078T-CARD
008160         DISPLAY '   INVALID PRODUCT CODE "' I1078T-PRODUCT-CODE
008170               '"' UPON SYSOUT.
008180     IF I1078T-DESCRIPTIVE-QTY NOT EQUAL SPACE
008190       IF I1078T-DESCRIPTIVE-QTY NOT NUMERIC
008200         PERFORM 3500-REJECT-I1078T-CARD
008210         DISPLAY '   NON-NUMERIC DESCRIPTIVE QUANTITY "'
008220               I1078T-DESCRIPTIVE-QTY '"' UPON SYSOUT.
008230     IF I1078T-REORDER-FLAG NOT EQUAL SPACE
008240       IF NOT I1078T-VALID-REORDER-FLAG
008250         PERFORM 3500-REJECT-I1078T-CARD
008260         DISPLAY '   INVALID REORDER FLAG "' I1078T-REORDER-FLAG
008270               '"' UPON SYSOUT.
008280     IF I1078T-UNITS NOT EQUAL SPACE
008290       IF I1078T-UNITS NOT NUMERIC
008300         PERFORM 3500-REJECT-I1078T-CARD
008310         DISPLAY '   NON-NUMERIC MATCH UNITS "' I1078T-UNITS '"'
008320               UPON SYSOUT.
008330     IF I1078T-ORDER-CHANNEL NOT EQUAL SPACE
008340       IF I1078T-ORDER-CHANNEL NOT NUMERIC
008350         IF I1078T-ORDER-CHANNEL NOT ALPHA
008360           PERFORM 3500-REJECT-I1078T-CARD
008370           DISPLAY '   INVALID ORDER CHANNEL "'
008380                 I1078T-ORDER-CHANNEL '"' UPON SYSOUT.
008390*
008400   4200-EDIT-ADD-STEP-1.
008410     IF I1078T-S1-BASIS NOT EQUAL SPACE
008420       IF NOT I1078T-S1-VALID-BASIS
008430         PERFORM 3500-REJECT-I1078T-CARD
008440         DISPLAY '   INVALID STEP 1 BASIS "' I1078T-S1-BASIS
008450               '"' UPON SYSOUT
008460       ELSE
008470         MOVE I1078T-S1-BASIS TO WS-ENT-S1-BASIS
008480         IF I1078T-S1-RATE-X EQUAL SPACE
008490           PERFORM 3500-REJECT-I1078T-CARD
008500           DISPLAY '   STEP 1 BASIS "' I1078T-S1-BASIS
008510                 '" PRESENT WITHOUT STEP 1 RATE' UPON SYSOUT.
008520     IF I1078T-S1-RATE-X NOT EQUAL SPACE
008530       IF I1078T-S1-RATE-U IS NUMERIC
008540         MOVE I1078T-S1-RATE-U TO WS-ENT-S1-RATE-N
008550       ELSE
008560         IF I1078T-S1-RATE IS NUMERIC
008570           MOVE I1078T-S1-RATE TO WS-ENT-S1-RATE-N
008580         ELSE
008590           PERFORM 3500-REJECT-I1078T-CARD
008600           DISPLAY '   NON-NUMERIC STEP 1 RATE "'
008610                 I1078T-S1-RATE-X '"' UPON SYSOUT
008620         END-IF
008630       END-IF
008640       IF I1078T-S1-BASIS EQUAL SPACE
008650           PERFORM 3500-REJECT-I1078T-CARD
008660           DISPLAY '   STEP 1 RATE "' I1078T-S1-RATE-X
008670                 '" PRESENT WITHOUT STEP 1 BASIS' UPON SYSOUT.
008680*
008690   4300-EDIT-ADD-STEP-2.
008700     IF I1078T-S2-BASIS NOT EQUAL SPACE
008710       IF NOT I1078T-S2-VALID-BASIS
008720         PERFORM 3500-REJECT-I1078T-CARD
008730         DISPLAY '   INVALID STEP 2 BASIS "' I1078T-S2-BASIS
008740               '"' UPON SYSOUT
008750       ELSE
008760         MOVE I1078T-S2-BASIS TO WS-ENT-S2-BASIS
008770         IF I1078T-S1-BASIS EQUAL SPACE
008780           PERFORM 3500-REJECT-I1078T-CARD
008790           DISPLAY '   STEP 2 BASIS "' I1078T-S2-BASIS
008800                 '" PRESENT WITHOUT STEP 1 BASIS' UPON SYSOUT
008810         END-IF
008820         IF I1078T-S2-RATE-X EQUAL SPACE
008830           PERFORM 3500-REJECT-I1078T-CARD
008840           DISPLAY '   STEP 2 BASIS "' I1078T-S2-BASIS
008850                 '" PRESENT WITHOUT STEP 2 RATE' UPON SYSOUT.
008860     IF I1078T-S2-RATE-X NOT EQUAL SPACE
008870       IF I1078T-S2-RATE-U IS NUMERIC
008880         MOVE I1078T-S2-RATE-U TO WS-ENT-S2-RATE-N
008890       ELSE
008900         IF I1078T-S2-RATE IS NUMERIC
008910           MOVE I1078T-S2-RATE TO WS-ENT-S2-RATE-N
008920         ELSE
008930           PERFORM 3500-REJECT-I1078T-CARD
008940           DISPLAY '   NON-NUMERIC STEP 2 RATE "'
008950                 I1078T-S2-RATE-X '"' UPON SYSOUT
008960         END-IF
008970       END-IF
008980       IF I1078T-S2-BASIS EQUAL SPACE
008990           PERFORM 3500-REJECT-I1078T-CARD
009000           DISPLAY '   STEP 2 RATE "' I1078T-S2-RATE-X
009010                 '" PRESENT WITHOUT STEP 2 BASIS' UPON SYSOUT.
009020*
009030   4400-EDIT-ADD-STEP-3.
009040     IF I1078T-S3-BASIS NOT EQUAL SPACE
009050       IF NOT I1078T-S3-VALID-BASIS
009060         PERFORM 3500-REJECT-I1078T-CARD
009070         DISPLAY '   INVALID STEP 3 BASIS "' I1078T-S3-BASIS
009080               '"' UPON SYSOUT
009090       ELSE
009100         MOVE I1078T-S3-BASIS TO WS-ENT-S3-BASIS
009110         IF I1078T-S2-BASIS EQUAL SPACE
009120           PERFORM 3500-REJECT-I1078T-CARD
009130           DISPLAY '   STEP 3 BASIS "' I1078T-S3-BASIS
009140                 '" PRESENT WITHOUT STEP 2 BASIS' UPON SYSOUT
009150         END-IF
009160         IF I1078T-S3-RATE-X EQUAL SPACE
009170           PERFORM 3500-REJECT-I1078T-CARD
009180           DISPLAY '   STEP 3 BASIS "' I1078T-S3-BASIS
009190                 '" PRESENT WITHOUT STEP 3 RATE' UPON SYSOUT.
009200     IF I1078T-S3-RATE-X NOT EQUAL SPACE
009210       IF I1078T-S3-RATE-U IS NUMERIC
009220         MOVE I1078T-S3-RATE-U TO WS-ENT-S3-RATE-N
009230       ELSE
009240         IF I1078T-S3-RATE IS NUMERIC
009250           MOVE I1078T-S3-RATE TO WS-ENT-S3-RATE-N
009260         ELSE
009270           PERFORM 3500-REJECT-I1078T-CARD
009280           DISPLAY '   NON-NUMERIC STEP 3 RATE "'
009290                 I1078T-S3-RATE-X '"' UPON SYSOUT
009300         END-IF
009310       END-IF
009320       IF I1078T-S3-BASIS EQUAL SPACE
009330           PERFORM 3500-REJECT-I1078T-CARD
009340           DISPLAY '   STEP 3 RATE "' I1078T-S3-RATE-X
009350                 '" PRESENT WITHOUT STEP 3 BASIS' UPON SYSOUT.
009360*
009370   4500-CHECK-AGAINST-BLANK-ENTRY.
009380     IF WS-ENTRY EQUAL SPACES
009390       PERFORM 3500-REJECT-I1078T-CARD
009400       DISPLAY '  RESULTS IN BLANK (NO KEY, NO STEPS) ENTRY'
009410             UPON SYSOUT.
009420*
009430   4600-ADD-ENTRY.
009440     IF (WS-PR1-LINE-COUNT GREATER +58)
009450         OR ((WS-PR1-LINE-COUNT EQUAL +58)
009460             AND (WS-ENT-S2-BASIS NOT EQUAL SPACE))
009470         OR ((WS-PR1-LINE-COUNT EQUAL +57)
009480             AND (WS-ENT-S3-BASIS NOT EQUAL SPACE))
009490       PERFORM 2200-PRINTER1-HEADINGS
009500     ELSE
009510       MOVE +2 TO WS-PR1-LINE-SPACER.
009520     MOVE SPACE TO PRINTER1-REC.
009530     MOVE WS-HOLD-RECORD-KEY TO PR1-RECORD-KEY.
009540     MOVE WS-ENT-PRODUCT-CODE TO PR1-PRODUCT-CODE.
009550     MOVE WS-ENT-DESCRIPTIVE-QTY TO PR1-DESCRIPTIVE-QTY.
009560     MOVE WS-ENT-REORDER-FLAG TO PR1-REORDER-FLAG.
009570     MOVE WS-ENT-UNITS TO PR1-UNITS.
009580     MOVE WS-ENT-ORDER-CHANNEL TO PR1-ORDER-CHANNEL.
009590     MOVE 1 TO PR1-STEP-NUMBER.
009600     MOVE '(ENTRY ADDED)' TO PR1-STEP-OLD-ADJUST (1:13).
009610     IF WS-ENT-S1-BASIS EQUAL SPACE
009620       MOVE '(NONE)' TO PR1-STEP-NEW-ADJUST (1:6)
009630     ELSE
009640       MOVE WS-ENT-S1-BASIS TO PR1-STEP-NEW-BASIS
009650       MOVE WS-ENT-S1-RATE-N TO PR1-STEP-NEW-RATE
009660       IF WS-ENT-S1-BASIS IS NUMERIC
009670         MOVE '$' TO PR1-STEP-NEW-DLR-SIGN
009680       ELSE
009690         MOVE '%' TO PR1-STEP-NEW-PCT-SIGN
009700       END-IF
009710       IF WS-ENT-S2-BASIS NOT EQUAL SPACE
009720         PERFORM 2300-WRITE-PRINTER1-REC
009730         MOVE +1 TO WS-PR1-LINE-SPACER
009740         MOVE SPACE TO PRINTER1-REC
009750         MOVE 2 TO PR1-STEP-NUMBER
009760         MOVE WS-ENT-S2-BASIS TO PR1-STEP-NEW-BASIS
009770         MOVE WS-ENT-S2-RATE-N TO PR1-STEP-NEW-RATE
009780         IF WS-ENT-S2-BASIS IS NUMERIC
009790           MOVE '$' TO PR1-STEP-NEW-DLR-SIGN
009800         ELSE
009810           MOVE '%' TO PR1-STEP-NEW-PCT-SIGN
009820         END-IF
009830         IF WS-ENT-S3-BASIS NOT EQUAL SPACE
009840           PERFORM 2300-WRITE-PRINTER1-REC
009850           MOVE SPACE TO PRINTER1-REC
009860           MOVE 3 TO PR1-STEP-NUMBER
009870           MOVE WS-ENT-S3-BASIS TO PR1-STEP-NEW-BASIS
009880           MOVE WS-ENT-S3-RATE-N TO PR1-STEP-NEW-RATE
009890           IF WS-ENT-S3-BASIS IS NUMERIC
009900             MOVE '$' TO PR1-STEP-NEW-DLR-SIGN
009910           ELSE
009920             MOVE '%' TO PR1-STEP-NEW-PCT-SIGN.
009930     PERFORM 2300-WRITE-PRINTER1-REC.
009940     PERFORM 3200-LOAD-ENT-TO-O1030.
009950*
009960   5000-I1078T-CHANGE-CARD.
009970     PERFORM 3100-GET-ENT-FROM-WS-TBL.
009980     MOVE WS-ENT-ADJUST-INFO TO WS-OLD-ADJUSTMENTS.
009990     IF I1078T-STEP-1 NOT EQUAL SPACE
010000       IF I1078T-S1-DELETE-STEP
010010         PERFORM 5100-EDIT-RESET-STEP-1
010020       ELSE
010030         PERFORM 5200-EDIT-CHANGE-STEP-1.
010040     IF I1078T-STEP-2 NOT EQUAL SPACE
010050       IF I1078T-S2-DELETE-STEP
010060         PERFORM 5300-EDIT-RESET-STEP-2
010070       ELSE
010080         PERFORM 5400-EDIT-CHANGE-STEP-2.
010090     IF I1078T-STEP-3 NOT EQUAL SPACE
010100       IF I1078T-S3-DELETE-STEP
010110         MOVE SPACE TO WS-ENT-STEP-3
010120       ELSE
010130         PERFORM 5600-EDIT-CHANGE-STEP-3.
010140     IF NOT ERRORS-IN-CARD
010150       PERFORM 4500-CHECK-AGAINST-BLANK-ENTRY
010160       IF NOT ERRORS-IN-CARD
010170         IF WS-ENT-ADJUST-INFO EQUAL WS-OLD-ADJUSTMENTS
010180           PERFORM 3500-REJECT-I1078T-CARD
010190           DISPLAY '   DOES NOT RESULT IN ANY CHANGES TO ENTRY'
010200                 UPON SYSOUT.
010210     IF ERRORS-IN-CARD
010220       SET IX-WS-ADJ-TBL DOWN BY 1
010230     ELSE
010240       PERFORM 5700-CHANGE-ENTRY
010250       MOVE 'Y' TO WS-RECORD-UPDATE-FLAG.
010260*
010270   5100-EDIT-RESET-STEP-1.
010280     MOVE SPACE TO WS-ENT-STEP-1.
010290     IF (WS-ENT-S2-BASIS NOT EQUAL SPACE)
010300         AND (NOT I1078T-S2-DELETE-STEP)
010310       PERFORM 3500-REJECT-I1078T-CARD
010320       DISPLAY '   DELETING STEP 1 WHILE KEEPING NON-BLANK STEP 2'
010330             UPON SYSOUT.
010340*
010350   5200-EDIT-CHANGE-STEP-1.
010360     IF I1078T-S1-BASIS NOT EQUAL SPACE
010370       IF NOT I1078T-S1-VALID-BASIS
010380         PERFORM 3500-REJECT-I1078T-CARD
010390         DISPLAY '   INVALID STEP 1 BASIS "' I1078T-S1-BASIS
010400               '"' UPON SYSOUT
010410       ELSE
010420         MOVE I1078T-S1-BASIS TO WS-ENT-S1-BASIS
010430         IF SPACE EQUAL I1078T-S1-RATE-X AND WS-ENT-S1-RATE-X
010440           PERFORM 3500-REJECT-I1078T-CARD
010450           DISPLAY '   STEP 1 BASIS "' I1078T-S1-BASIS
010460                 '" PRESENT WITHOUT STEP 1 RATE' UPON SYSOUT.
010470     IF I1078T-S1-RATE-X NOT EQUAL SPACE
010480       IF I1078T-S1-RATE-U IS NUMERIC
010490         MOVE I1078T-S1-RATE-U TO WS-ENT-S1-RATE-N
010500       ELSE
010510         IF I1078T-S1-RATE IS NUMERIC
010520           MOVE I1078T-S1-RATE TO WS-ENT-S1-RATE-N
010530         ELSE
010540           PERFORM 3500-REJECT-I1078T-CARD
010550           DISPLAY '   NON-NUMERIC STEP 1 RATE "'
010560                 I1078T-S1-RATE-X '"' UPON SYSOUT
010570         END-IF
010580       END-IF
010590       IF SPACE EQUAL WS-ENT-S1-BASIS AND I1078T-S1-BASIS
010600           PERFORM 3500-REJECT-I1078T-CARD
010610           DISPLAY '   STEP 1 RATE "' I1078T-S1-RATE-X
010620                 '" PRESENT WITHOUT STEP 1 BASIS' UPON SYSOUT.
010630*
010640   5300-EDIT-RESET-STEP-2.
010650     MOVE SPACE TO WS-ENT-STEP-2.
010660     IF (WS-ENT-S3-BASIS NOT EQUAL SPACE)
010670         AND (NOT I1078T-S3-DELETE-STEP)
010680       PERFORM 3500-REJECT-I1078T-CARD
010690       DISPLAY '   DELETING STEP 2 WHILE KEEPING NON-BLANK STEP 3'
010700             UPON SYSOUT.
010710*
010720   5400-EDIT-CHANGE-STEP-2.
010730     IF I1078T-S2-BASIS NOT EQUAL SPACE
010740       IF NOT I1078T-S2-VALID-BASIS
010750         PERFORM 3500-REJECT-I1078T-CARD
010760         DISPLAY '   INVALID STEP 2 BASIS "' I1078T-S2-BASIS
010770               '"' UPON SYSOUT
010780       ELSE
010790         MOVE I1078T-S2-BASIS TO WS-ENT-S2-BASIS
010800         IF I1078T-S1-DELETE-STEP
010810             OR (SPACE EQUAL WS-ENT-S1-BASIS AND I1078T-S1-BASIS)
010820           PERFORM 3500-REJECT-I1078T-CARD
010830           DISPLAY '   STEP 2 BASIS "' I1078T-S2-BASIS
010840                 '" PRESENT WITHOUT STEP 1 BASIS' UPON SYSOUT
010850         END-IF
010860         IF SPACE EQUAL WS-ENT-S2-RATE-X AND I1078T-S2-RATE-X
010870           PERFORM 3500-REJECT-I1078T-CARD
010880           DISPLAY '   STEP 2 BASIS "' I1078T-S2-BASIS
010890                 '" PRESENT WITHOUT STEP 2 RATE' UPON SYSOUT.
010900     IF I1078T-S2-RATE-X NOT EQUAL SPACE
010910       IF I1078T-S2-RATE-U IS NUMERIC
010920         MOVE I1078T-S2-RATE-U TO WS-ENT-S2-RATE-N
010930       ELSE
010940         IF I1078T-S2-RATE IS NUMERIC
010950           MOVE I1078T-S2-RATE TO WS-ENT-S2-RATE-N
010960         ELSE
010970           PERFORM 3500-REJECT-I1078T-CARD
010980           DISPLAY '   NON-NUMERIC STEP 2 RATE "'
010990                 I1078T-S2-RATE-X '"' UPON SYSOUT
011000         END-IF
011010       END-IF
011020       IF SPACE EQUAL WS-ENT-S2-BASIS AND I1078T-S2-BASIS
011030           PERFORM 3500-REJECT-I1078T-CARD
011040           DISPLAY '   STEP 2 RATE "' I1078T-S2-RATE-X
011050                 '" PRESENT WITHOUT STEP 2 BASIS' UPON SYSOUT.
011060*
011070   5600-EDIT-CHANGE-STEP-3.
011080     IF I1078T-S3-BASIS NOT EQUAL SPACE
011090       IF NOT I1078T-S3-VALID-BASIS
011100         PERFORM 3500-REJECT-I1078T-CARD
011110         DISPLAY '   INVALID STEP 3 BASIS "' I1078T-S3-BASIS
011120               '"' UPON SYSOUT
011130       ELSE
011140         MOVE I1078T-S3-BASIS TO WS-ENT-S3-BASIS
011150         IF I1078T-S2-DELETE-STEP
011160             OR (SPACE EQUAL WS-ENT-S2-BASIS AND I1078T-S2-BASIS)
011170           PERFORM 3500-REJECT-I1078T-CARD
011180           DISPLAY '   STEP 3 BASIS "' I1078T-S3-BASIS
011190                 '" PRESENT WITHOUT STEP 2 BASIS' UPON SYSOUT
011200         END-IF
011210         IF SPACE EQUAL WS-ENT-S3-RATE-X AND I1078T-S3-RATE-X
011220           PERFORM 3500-REJECT-I1078T-CARD
011230           DISPLAY '   STEP 3 BASIS "' I1078T-S3-BASIS
011240                 '" PRESENT WITHOUT STEP 3 RATE' UPON SYSOUT.
011250     IF I1078T-S3-RATE-X NOT EQUAL SPACE
011260       IF I1078T-S3-RATE-U IS NUMERIC
011270         MOVE I1078T-S3-RATE-U TO WS-ENT-S3-RATE-N
011280       ELSE
011290         IF I1078T-S3-RATE IS NUMERIC
011300           MOVE I1078T-S3-RATE TO WS-ENT-S3-RATE-N
011310         ELSE
011320           PERFORM 3500-REJECT-I1078T-CARD
011330           DISPLAY '   NON-NUMERIC STEP 3 RATE "'
011340                 I1078T-S3-RATE-X '"' UPON SYSOUT
011350         END-IF
011360       END-IF
011370       IF SPACE EQUAL WS-ENT-S3-BASIS AND I1078T-S3-BASIS
011380           PERFORM 3500-REJECT-I1078T-CARD
011390           DISPLAY '   STEP 3 RATE "' I1078T-S3-RATE-X
011400                 '" PRESENT WITHOUT STEP 3 BASIS' UPON SYSOUT.
011410*
011420   5700-CHANGE-ENTRY.
011430     IF (WS-PR1-LINE-COUNT GREATER +58)
011440         OR ((WS-PR1-LINE-COUNT EQUAL +58)
011450             AND (SPACE NOT EQUAL WS-ENT-S2-BASIS OR
011460                   WS-OLD-S2-BASIS))
011470         OR ((WS-PR1-LINE-COUNT EQUAL +57)
011480             AND (SPACE NOT EQUAL WS-ENT-S3-BASIS OR
011490                   WS-OLD-S3-BASIS))
011500       PERFORM 2200-PRINTER1-HEADINGS
011510     ELSE
011520       MOVE +2 TO WS-PR1-LINE-SPACER.
011530     MOVE SPACE TO PRINTER1-REC.
011540     MOVE WS-HOLD-RECORD-KEY TO PR1-RECORD-KEY.
011550     MOVE WS-ENT-PRODUCT-CODE TO PR1-PRODUCT-CODE.
011560     MOVE WS-ENT-DESCRIPTIVE-QTY TO PR1-DESCRIPTIVE-QTY.
011570     MOVE WS-ENT-REORDER-FLAG TO PR1-REORDER-FLAG.
011580     MOVE WS-ENT-UNITS TO PR1-UNITS.
011590     MOVE WS-ENT-ORDER-CHANNEL TO PR1-ORDER-CHANNEL.
011600     MOVE 1 TO PR1-STEP-NUMBER.
011610     IF WS-OLD-S1-BASIS EQUAL SPACE
011620       MOVE '(NONE)' TO PR1-STEP-OLD-ADJUST (1:6)
011630     ELSE
011640       MOVE WS-OLD-S1-BASIS TO PR1-STEP-OLD-BASIS
011650       MOVE WS-OLD-S1-RATE-N TO PR1-STEP-OLD-RATE
011660       IF WS-OLD-S1-BASIS IS NUMERIC
011670         MOVE '$' TO PR1-STEP-OLD-DLR-SIGN
011680       ELSE
011690         MOVE '%' TO PR1-STEP-OLD-PCT-SIGN.
011700     IF WS-ENT-S1-BASIS EQUAL SPACE
011710       MOVE '(NONE)' TO PR1-STEP-NEW-ADJUST (1:6)
011720     ELSE
011730       MOVE WS-ENT-S1-BASIS TO PR1-STEP-NEW-BASIS
011740       MOVE WS-ENT-S1-RATE-N TO PR1-STEP-NEW-RATE
011750       IF WS-ENT-S1-BASIS IS NUMERIC
011760         MOVE '$' TO PR1-STEP-NEW-DLR-SIGN
011770       ELSE
011780         MOVE '%' TO PR1-STEP-NEW-PCT-SIGN.
011790     IF SPACE NOT EQUAL WS-ENT-S2-BASIS OR WS-OLD-S2-BASIS
011800       PERFORM 2300-WRITE-PRINTER1-REC
011810       MOVE +1 TO WS-PR1-LINE-SPACER
011820       MOVE SPACE TO PRINTER1-REC
011830       MOVE 2 TO PR1-STEP-NUMBER
011840       IF WS-OLD-S2-BASIS EQUAL SPACE
011850         MOVE '(NONE)' TO PR1-STEP-OLD-ADJUST (1:6)
011860       ELSE
011870         MOVE WS-OLD-S2-BASIS TO PR1-STEP-OLD-BASIS
011880         MOVE WS-OLD-S2-RATE-N TO PR1-STEP-OLD-RATE
011890         IF WS-OLD-S2-BASIS IS NUMERIC
011900           MOVE '$' TO PR1-STEP-OLD-DLR-SIGN
011910         ELSE
011920           MOVE '%' TO PR1-STEP-OLD-PCT-SIGN
011930         END-IF
011940       END-IF
011950       IF WS-ENT-S2-BASIS EQUAL SPACE
011960         MOVE '(NONE)' TO PR1-STEP-NEW-ADJUST (1:6)
011970       ELSE
011980         MOVE WS-ENT-S2-BASIS TO PR1-STEP-NEW-BASIS
011990         MOVE WS-ENT-S2-RATE-N TO PR1-STEP-NEW-RATE
012000         IF WS-ENT-S2-BASIS IS NUMERIC
012010           MOVE '$' TO PR1-STEP-NEW-DLR-SIGN
012020         ELSE
012030           MOVE '%' TO PR1-STEP-NEW-PCT-SIGN
012040         END-IF
012050       END-IF
012060       IF SPACE NOT EQUAL WS-ENT-S3-BASIS OR WS-OLD-S3-BASIS
012070         PERFORM 2300-WRITE-PRINTER1-REC
012080         MOVE SPACE TO PRINTER1-REC
012090         MOVE 3 TO PR1-STEP-NUMBER
012100         IF WS-OLD-S3-BASIS EQUAL SPACE
012110           MOVE '(NONE)' TO PR1-STEP-OLD-ADJUST (1:6)
012120         ELSE
012130           MOVE WS-OLD-S3-BASIS TO PR1-STEP-OLD-BASIS
012140           MOVE WS-OLD-S3-RATE-N TO PR1-STEP-OLD-RATE
012150           IF WS-OLD-S3-BASIS IS NUMERIC
012160             MOVE '$' TO PR1-STEP-OLD-DLR-SIGN
012170           ELSE
012180             MOVE '%' TO PR1-STEP-OLD-PCT-SIGN
012190           END-IF
012200         END-IF
012210         IF WS-ENT-S3-BASIS EQUAL SPACE
012220           MOVE '(NONE)' TO PR1-STEP-NEW-ADJUST (1:6)
012230         ELSE
012240           MOVE WS-ENT-S3-BASIS TO PR1-STEP-NEW-BASIS
012250           MOVE WS-ENT-S3-RATE-N TO PR1-STEP-NEW-RATE
012260           IF WS-ENT-S3-BASIS IS NUMERIC
012270             MOVE '$' TO PR1-STEP-NEW-DLR-SIGN
012280           ELSE
012290             MOVE '%' TO PR1-STEP-NEW-PCT-SIGN.
012300     PERFORM 2300-WRITE-PRINTER1-REC.
012310     PERFORM 3200-LOAD-ENT-TO-O1030.
012320*
012330   8000-INITIALIZE.
012340*
012350 COPY CPY005.
012360*
012370     OPEN OUTPUT O1030-FILE PRINTER1-FILE PRINTER2-FILE
012380           INPUT I1078T-FILE I1030-FILE.
012390     IF NOT WS-I1030-SUCCESSFUL-OPEN
012400       DISPLAY '* VSAM ERROR ON I1030 OPEN.  FILE STATUS = '
012410             WS-I1030-FILE-STATUS '. *' UPON SYSOUT
012420       GO TO 9900-DISPLAY-ABORT.
012430     MOVE 'Y' TO WS-I1030-FILE-FLAG.
012440     PERFORM 1200-READ-I1030-FILE.
012450     PERFORM 3400-READ-I1078T-FILE.
012460     IF WS-I1030-END-OF-FILE
012470       IF END-I1078T-FILE
012480         DISPLAY '* BOTH INPUT FILES ARE EMPTY *' UPON SYSOUT
012490         GO TO 9900-DISPLAY-ABORT
012500       ELSE
012510         MOVE 'Y' TO WS-RETURN-04-FLAG
012520         DISPLAY 'I1030 IS AN EMPTY FILE.  BUILDING A NEW FILE.'
012530               UPON SYSOUT
012540     ELSE
012550       IF END-I1078T-FILE
012560         MOVE 'Y' TO WS-RETURN-04-FLAG
012570         DISPLAY 'I1078T IS AN EMPTY FILE.  LISTING CURRENT FILE.'
012580               UPON SYSOUT.
012590     MOVE WS-PROGRAM-NUMBER TO WS-PR1-HD1-PROGRAM-NBR
012600           WS-PR2-HD1-PROGRAM-NBR.
012610     MOVE WS-COMPUTER-MONTH TO WS-PR1-HD1-RUN-MONTH.
012620     MOVE WS-COMPUTER-DAY TO WS-PR1-HD1-RUN-DAY.
012630     MOVE CPY4-FOUR-DIGIT-YEAR TO WS-PR1-HD1-RUN-YEAR.
012640     MOVE WS-PR1-HD1-RUN-DATE TO WS-PR2-HD1-RUN-DATE.
012650*
012660   9000-CLOSE.
012670     PERFORM 1100-PASS-THRU-I1030 UNTIL WS-I1030-END-OF-FILE.
012680     IF WS-PR1-LINE-COUNT NOT EQUAL +90
012690*                * PRINTER1 WAS WRITTEN TO *
DLXMIG*       WRITE PRINTER1-REC FROM WS-END-OF-REPORT AFTER C12-LINE-61 
DLXMIG       WRITE PRINTER1-REC FROM WS-END-OF-REPORT AFTER 12 LINES.
012720     IF WS-PR2-LINE-COUNT NOT EQUAL +90
012730*                * PRINTER2 WAS WRITTEN TO *
DLXMIG*       WRITE PRINTER2-REC FROM WS-END-OF-REPORT AFTER C12-LINE-61 
DLXMIG       WRITE PRINTER2-REC FROM WS-END-OF-REPORT AFTER 12 LINES.
012760     CLOSE PRINTER1-FILE PRINTER2-FILE O1030-FILE I1078T-FILE.
012770     PERFORM 9100-CLOSE-I1030-FILE.
012780     IF NOT WS-I1030-SUCCESSFUL
012790       DISPLAY 'VSAM ERROR ON I1030 CLOSE.  FILE STATUS = '
012800             WS-I1030-FILE-STATUS '.' UPON SYSOUT
012810       MOVE 'Y' TO WS-RETURN-04-FLAG.
012820     MOVE WS-I1078T-CARD-COUNT TO WS-CD-I1078T-CARD-COUNT.
012830     MOVE WS-I1078T-REJECT-COUNT TO WS-CD-I1078T-REJECT-CT.
012840     MOVE WS-I1030-RECORD-COUNT TO WS-CD-1030-RECORD-COUNT.
012850     DISPLAY '-------  ' WS-PROGRAM-NUMBER ' FILE COUNTS  -------'
012860           UPON SYSOUT.
012870     DISPLAY WS-CD-I1078T-LINE UPON SYSOUT.
012880     DISPLAY WS-CD-1030-LINE UPON SYSOUT.
012890     MOVE 'O' TO WS-CD-1030-I-OR-O.
012900     MOVE WS-O1030-RECORD-COUNT TO WS-CD-1030-RECORD-COUNT.
012910     MOVE 'WRITTEN' TO WS-CD-1030-VERB.
012920     DISPLAY WS-CD-1030-LINE UPON SYSOUT.
012930     IF RETURN-08
012940       MOVE 08 TO RETURN-CODE WS-RETURN-CODE
012950     ELSE
012960       IF RETURN-04
012970         MOVE 04 TO RETURN-CODE WS-RETURN-CODE.
012980     DISPLAY WS-RETURN-CODE-LINE UPON SYSOUT.
012990*
013000   9100-CLOSE-I1030-FILE.
013010     MOVE 'N' TO WS-I1030-FILE-FLAG.
013020     CLOSE I1030-FILE.
013030*
013040   9900-DISPLAY-ABORT.
013050     DISPLAY '* ' WS-PROGRAM-NUMBER ' * RUN ABORTED *'
013060           UPON SYSOUT.
013070     MOVE 16 TO RETURN-CODE WS-RETURN-CODE.
013080     DISPLAY WS-RETURN-CODE-LINE UPON SYSOUT.
013090     IF OPENED-I1030-FILE
013100       PERFORM 9100-CLOSE-I1030-FILE.
013110     STOP RUN.
013120*
013130*
