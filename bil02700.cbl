000010  CBL SQL('QUALIFIER CICS BINDFILE')
000020 IDENTIFICATION DIVISION.
DLXMIG* DLXMIG Changes start here
DLXMIG*  PROGRAM-ID. BIL02700.
DLXMIG   PROGRAM-ID. "bil02700".
DLXMIG* DLXMIG Changes end here
000070*PROGRAM-NAME. COMPARE BILLING/SALES EOP (MSRP) INFO.
000080   AUTHOR. JACK MASON.
000090   INSTALLATION. DELUXE FINANCIAL SERVICES, INC.
000100   DATE-WRITTEN. 05-17-99.
000110   DATE-COMPILED.
000120******************************************************************
000130*RUN INSTRUCTIONS             === PASS 1 ===              10/18/99
000140*
000150*I471   = 471 INPUT FROM SLS055                        LRECL=700
000160*         SEQUENTIAL CUSTOMER INFORMATION FILE
000170*
000180*I1636  = 1636 INPUT FROM BIL502              VARIABLE LRECL=21766
000190*         BC/BRANCH/BRANCH MASTER FILE
000200*
000210*I1909P = C1909 INPUT FROM PARMLIB                     LRECL=80
000220*         BC/BR THAT DO NOT WANT AN EOP (MSRP) CATALOG CODE BUT DO
000230*         WANT TO KEEP EOP (MSRP) UPCHARGING
000240*
000250*WORK1  = WORK FILE OUTPUT TO PASS 2 VIA SORT          LRECL=74
000260*
000270*WORK2  = WORK FILE OUTPUT TO PASS 3 VIA SORT          LRECL=34
000280*
000290*SYSOUT = REVISION INFO, ERROR MESSAGES; ANY PAPER
000300*
000310*PARM = 'PASS=1,NON-BILL=(INCLUDE/OMIT)'
000320*                         INCLUDE OR OMIT 471 RECORDS FOR
000330*                           NON-CUSTOMER BANKS (NON-NUMERIC BANK
000340*                           SYSTEM) & NON-BILLED BRANCHES (BRANCH
000350*                           NUMBER NON-NUMERIC BUT NOT SPACE)
000360*
000370******************************************************************
000380*
000390*RETURN CODES                 === PASS 1 ===              05-17-99
000400*
000410*  16 = RUN ABORTED.  SEE SYSOUT.
000420*         - INVALID PARM DATA
000430*         - EMPTY FILE; I471 OR I1636
000440*         - I1909P SEQUENCE ERROR
000450*         - I1636 VSAM ERROR (EXCEPT ON CLOSE)
000460*
000470*  08 = ERROR.  RUN CONTINUED.  SEE SYSOUT.
000480*         - INVALID C1909 CARD(S) REJECTED
000490*
000500*  04 = INFO ONLY.  RUN CONTINUED.  SEE SYSOUT.
000510*         - EXTRANEOUS PARM DATA
000520*         - EMPTY I1909P FILE
000530*         - UNMATCHED C1909 CARD(S)
000540*         - I1636 VSAM ERROR ON CLOSE
000550*
000560******************************************************************
000570*
000580*NARRATIVE                    === PASS 1 ===              10/18/99
000590*
000600*PURPOSE:
000610*  TO COMPARE BILLING AND SALES EOP (MSRP) CATALOG & VERSION INFO.
000620*  PASS 1 GATHERS DATA FROM THE 471, 1636 AND C1909 FILES.
000630*
000640*SOURCE OF INFORMATION:
000650*  THE 471 FILE IN ANY ORDER.
000660*  THE 1636 FILE (VSAM).
000670*  THE C1909 CARDS IN BC/BRANCH ORDER
000680*    SORT FIELDS=(1,13,A).
000690*
000700*PROGRAM FLOW:
000710*  PASS 1 INITIALIZES BY OPENING FILES AND CHECKING AGAINST EMPTY
000720*    INPUTS.
000730*  THERE ARE TWO DISTINCT PROCESSES IN PASS 1.
000740*  IN ONE, THE PROGRAM EXTRACTS A WORK FILE (WORK1) FROM THE 1636
000750*    WHILE COMPARING ITS SEQUENCE WITH THE C1909 CARDS.  ONE BYTE
000760*    OF THE WORK1 FILE IS A FLAG INDICATING THAT THERE WAS A MATCH
000770*    WITH THE CARDS.
000780*  IN THE OTHER, THE PROGRAM EXTRACTS A WORK FILE (WORK2) FROM THE
000790*    471 FILE.
000800*  AT THE END OF ALL INPUT FILES, THE PASS CLOSES FILES AND ENDS.
000810*
000820******************************************************************
000830*
000840*RUN INSTRUCTIONS             === PASS 2 ===              05-17-99
000850*
000860*WORK1  = WORK FILE INPUT FROM PASS 1 VIA SORT         LRECL=74
000870*
000880*I554   = 554 INPUT FROM BIL508               VARIABLE LRECL=27459
000890*         UPCHARGE FILE
000900*
000910*WORK3  = WORK FILE OUTPUT TO PASS 3 VIA SORT          LRECL=76
000920*
000930*SYSOUT = REVISION INFO, ERROR MESSAGES; ANY PAPER
000940*
000950*PARM = 'PASS=2'
000960*
000970******************************************************************
000980*
000990*RETURN CODES                 === PASS 2 ===              05-17-99
001000*
001010*  16 = RUN ABORTED.  SEE SYSOUT.
001020*         - INVALID PARM DATA
001030*         - EMPTY FILE; WORK1 OR I554
001040*         - WORK1 SEQUENCE ERROR
001050*         - I554 VSAM ERROR (EXCEPT ON CLOSE)
001060*
001070*  08 = ERROR.  RUN CONTINUED.  SEE SYSOUT.
001080*         - MISSING 554 RECORD FOR KEY POINTED TO BY 1636 (WORK1)
001090*
001100*  04 = INFO ONLY.  RUN CONTINUED.  SEE SYSOUT.
001110*         - EXTRANEOUS PARM DATA
001120*         - I554 VSAM ERROR ON CLOSE
001130*
001140******************************************************************
001150*
001160*NARRATIVE                    === PASS 2 ===              10/18/99
001170*
001180*PURPOSE:
001190*  TO COMPARE BILLING AND SALES EOP (MSRP) CATALOG & VERSION INFO.
001200*  PASS 2 APPENDS 554 EOP (MSRP) VERSION CODES TO 1636 DATA.
001210*
001220*SOURCE OF INFORMATION:
001230*  THE WORK1 FILE IN 554 KEY ORDER
001240*    SORT FIELDS=(1,46,A).
001250*  THE 554 FILE (VSAM).
001260*
001270*PROGRAM FLOW:
001280*  IN INITIALIZATION, PASS 2 OPENS FILES AND CHECKS AGAINST WORK1
001290*    BEING EMPTY.
001300*  IN PROCESSING, PASS 2 COMPARES I554 & WORK1 SEQUENCES, AND
001310*    COMBINES DATA FROM BOTH RECORDS TO CREATE WORK3 RECORDS.  IF
001320*    A WORK1 RECORD HAS A 554 KEY (NON-BLANK) THAT'S NOT ON THE
001330*    I554 FILE, THE PASS ISSUES AN ERROR MESSAGE (THE 554 AND PASS
001340*    1'S 1636 FILE ARE NOT FROM THE SAME WEEK).
001350*  AT THE END OF THE WORK1 FILE, PASS 2 CLOSES FILES AND ENDS.
001360*
001370******************************************************************
001380*
001390*RUN INSTRUCTIONS             === PASS 3 ===              03-13-00
001400*
001410*WORK2  = WORK FILE INPUT FROM PASS 1 VIA SORT         LRECL=34
001420*
001430*WORK3  = WORK FILE INPUT FROM PASS 2 VIA SORT         LRECL=76
001440*
001450*I1788P = C1788 INPUT FROM PARMLIB                     LRECL=80
001460*         COMPARATIVE RECAP CATALOG CARDS
001470*
001480*I1908P = C1908 INPUT FROM PARMLIB                     LRECL=80
001490*         BILLING CONVERSION TO SALES EOP (MSRP) CODES
001500*
001510*I1920P = C1920 INPUT FROM PARMLIB                     LRECL=80
001520*         EXCLUDED SALES BANK NUMBERS/SYSTEMS FOR BIL027
001530*
001540*WORK4  = WORK FILE OUTPUT TO PASS 4 VIA SORT          LRECL=89
001550*
001560*SYSOUT = REVISION INFO, ERROR MESSAGES; ANY PAPER
001570*
001580*PARM = 'PASS=3'
001590*
001600******************************************************************
001610*
001620*RETURN CODES                 === PASS 3 ===              03-13-00
001630*
001640*  16 = RUN ABORTED.  SEE SYSOUT.
001650*         - INVALID PARM DATA
001660*         - EMPTY FILE; WORK2 OR WORK3
001670*         - NO VALID I1788P CARDS (EMPTY FILE OR ALL REJECTED)
001680*         - NO VALID I1908P CARDS (EMPTY FILE OR ALL REJECTED)
001690*         - SEQUENCE ERROR WORK2, WORK3, I1908P OR I1920P
001700*         - VERSION/CATALOG TABLE OVERFLOW
001710*         - C1920 TABLE OVERFLOW
001720*
001730*  08 = ERROR.  RUN CONTINUED.  SEE SYSOUT.
001740*         - INVALID C1788 CARD(S) REJECTED
001750*         - INVALID C1908 CARD(S) REJECTED
001760*         - INVALID C1920 CARD(S) REJECTED
001770*
001780*  04 = INFO ONLY.  RUN CONTINUED.  SEE SYSOUT.
001790*         - EXTRANEOUS PARM DATA
001800*         - EMPTY I1920P FILE
001810*         - REDUNDANT C1920 CARD(S) DROPPED
001820*         - C1920 TABLE OVER 80% FULL
001830*         - VERSION/CATALOG TABLE OVER 80% FULL
001840*
001850******************************************************************
001860*
001870*NARRATIVE                    === PASS 3 ===              03-13-00
001880*
001890*PURPOSE:
001900*  TO COMPARE BILLING AND SALES EOP (MSRP) CATALOG & VERSION INFO.
001910*  PASS CHECKS 171 DATA FOR INTERNAL ERRORS, AND COMPARES 171 DATA
001920*    WITH 1636/554 DATA TO CHECK FOR MISMATCH ERRORS.  IT WRITES
001930*    WORK RECORDS FOR ANY ERRORS FOUND.
001940*
001950*SOURCE OF INFORMATION:
001960*  THE WORK2 FILE IN CUSTOMER-ID ORDER
001970*    SORT FIELDS=(1,9,A).
001980*  THE WORK3 FILE IN CUSTOMER-ID ORDER
001990*    SORT FIELDS=(1,9,A).
002000*  THE C1788 CARDS IN ANY ORDER.
002010*  THE C1908 CARDS IN EOP (MSRP) VERSION, CATALOG CODE ORDER
002020*    SORT FIELDS=(1,2,A,4,4,A).
002030*  THE C1920 CARDS IN SALES BANK NUMBER ORDER
002040*    SORT FIELDS=(1,10,A).
002050*
002060*PROGRAM FLOW:
002070*  IN INITIALIZATION THIS PASS OPENS FILES, AND CHECKS AGAINST
002080*    EMPTY INPUT FILES.  NEXT IT LOADS A TABLE OF C1920 CARDS.
002090*    THEN IT USES THE I1908P CARDS TO LOAD THE VERSION/CATALOG
002100*    TABLE.  AND LAST IT USES THE I1788 CARDS TO ADD A CODE TO
002110*    THAT TABLE INDICATING WHICH VERSION NUMBERS ARE SUPPORTED BY
002120*    CATALOGS WITH PRICES PRINTED IN THEM.
002130*  PASS 3'S PROCESSING CONSISTS OF CHECKING THE WORK2 AND WORK3
002140*    FILES, AND CREATING WORK4 RECORDS FOR ANY REPORTABLE ERRORS:
002150*      *  MISMATCH BETWEEN 171 & 554 EOP (MSRP) VERSIONS
002160*      *  554 EOP (MSRP) VERSION WITHOUT 171 EOP (MSRP) VERSION
002170*      *  171 EOP (MSRP) VERSION WITHOUT 554 EOP (MSRP) VERSION
002180*      *  MISMATCH BETWEEN 171 CATALOG AND 171 VERSION
002190*  CERTAIN RECORDS ARE EXEMPT FROM THESE TESTS:
002200*      *  SALES KEYS WITH SHELL (45, 50, 55) OFFICE TYPES, OR
002210*           MATCH A C1920 CARD; AND ANY BILLING RECORDS WITH THE
002220*           SAME CUSTOMER-ID.
002230*      *  BILLING RECORDS FLAGGED AS HAVING MATCHED A C1909 CARD,
002240*           AND ANY SALES RECORDS WITH THE SAME CUSTOMER-ID.
002250*  WHEN BOTH WORK2 AND WORK3 ARE AT END, PASS 3 CLOSES FILES AND
002260*    ENDS.
002270*
002280******************************************************************
002290*
002300*RUN INSTRUCTIONS             === PASS 4 ===              05-17-99
002310*
002320*WORK4  = WORK FILE INPUT FROM PASS 4 VIA SORT         LRECL=89
002330*
002340*PRINTER1 = REPORTS (4 OF THEM) 132 CHARACTERS/LINE   (LRECL=133)
002350*
002360*SYSOUT = REVISION INFO, ERROR MESSAGES; ANY PAPER
002370*
002380*PARM = 'PASS=4'
002390*
002400******************************************************************
002410*
002420*RETURN CODES                 === PASS 4 ===              05-17-99
002430*
002440*  16 = RUN ABORTED.  SEE SYSOUT.
002450*         - INVALID PARM DATA
002460*         - WORK4 SEQUENCE ERROR
002470*
002480*  04 = INFO ONLY.  RUN CONTINUED.  SEE SYSOUT.
002490*         - EXTRANEOUS PARM DATA
002500*
002510******************************************************************
002520*
002530*NARRATIVE                    === PASS 4 ===              10/18/99
002540*
002550*PURPOSE:
002560*  TO COMPARE BILLING AND SALES EOP (MSRP) CATALOG & VERSION INFO.
002570*  PASS 4 FORMATS AND PRINTS THE REPORTS.
002580*
002590*SOURCE OF INFORMATION:
002600*  THE WORK4 FILE REPORT NUMBER, BC/BR/BR, 171 KEY ORDER
002610*    SORT FIELDS=(1,35,A).
002620*
002630*PROGRAM FLOW:
002640*  IN INITIALIZATION, PASS 4 OPENS FILES, READS THE FIRST WORK4
002650*    RECORD, AND INSTALLS THE PROGRAM NUMBER & RUN DATE IN A
002660*    HEADING.
002670*  A PROCESS IS PERFORMED FOUR TIMES, ONCE FOR EACH REPORT NUMBER.
002680*    THE PASS RESETS THE PAGE NUMBER AND INSTALLS THE TITLE OF THE
002690*    REPORT IN A HEADING.  IT CHECKS WORK4 TO SEE IF THERE ARE
002700*    RECORDS FOR THAT REPORT.  IF SO, IT PRINTS THEM.  OTHERWISE
002710*    IT PRINTS A LINE INDICATING "NONE".
002720*  AFTER PRINTING THE FOUR REPORTS, THE PASS CLOSES FILES, AND
002730*    ENDS.
002740*
002750******************************************************************
002760*
002770*REVISION HISTORY
002780*
002790*05-17-99  PROGRAM WRITTEN.  PROJECT #903194.  CAPS #9095.
002800*                                                   JACK MASON  01
002810*
002820*10/18/99  ADDED THE CHECK FOR 471 DLX CATALOG TOTAL GREATER THAN
002830*          ZERO FOR THE RECORD TO BE PROCESSED AND CHANGED HEADING
002840*          MSRP VERBAGE TO EOP VERBAGE.
002850*          PROJECT #903194 CHGMN#BILL250
002860*                               #BILL243    CAPS#9100 PAUL BUSH 02
002870*
002880*03-13-00  ADDED I1920P FILE, TABLE AND LOGIC TO PASS 3.  THE NEW
002890*            CARDS EXCLUDE SELECTED SALES RECORDS (AND MATCHING
002900*            BILLING RECORDS) FROM ERROR TESTING/REPORTING.
002910*          PROJECT #903194.  CAPS #15360.           JACK MASON  03
002920*
002930*02-26-01  IN PASS-2 RUN INSTRUCTIONS THE LRECL FOR I554 IS
002940*          CHANGED FROM 27154 TO 27459 TO ACCOMODATE NEW EXPENSE
002950*            CODE. RECOMPILED
002960*          PROJECT #903194.  CAPS #21988          SYED HUSSAIN  04
002970*
002980*11-11-02  MODIFIED THE PROGRAM FOR EXPANDING THE I1908 TABLE
002990*          SIZE FROM 120 TO 500 ENTRIES
003000*          CM #BILL843.  REMEDY #ACM7003       BOGGARAPU PRASAD 05
003010*
DLXMIG**----------------------------------------------------------------    
DLXMIG**  DATE      DESCRIPTION                        INITIALS             
DLXMIG** -------  ------------------------------------------------------    
DLXMIG** 101409   UPDATED FOR DLX MIGRATION            COGNIZANT            
DLXMIG** -------  ------------------------------------------------------    
003070******************************************************************
003080*
003090 ENVIRONMENT DIVISION.
003100 CONFIGURATION SECTION.
DLXMIG* DLXMIG Changes start here
DLXMIG*  SOURCE-COMPUTER. IBM-370.
DLXMIG   SOURCE-COMPUTER. UNIX.
DLXMIG* DLXMIG Changes end here
DLXMIG* DLXMIG Changes start here
DLXMIG*  OBJECT-COMPUTER. IBM-370.
DLXMIG   OBJECT-COMPUTER. UNIX.
DLXMIG* DLXMIG Changes end here
003190   SPECIAL-NAMES.
003200     CLASS LETTERS-AND-NUMERALS IS 'A' THRU 'I', 'J' THRU 'R',
003210           'S' THRU 'Z', '0' THRU '9'.
003220*
003230 INPUT-OUTPUT SECTION.
003240   FILE-CONTROL.
DLXMIG     SELECT I471-FILE     ASSIGN RSD-I471.
003260     SELECT I1636-FILE    ASSIGN I1636
003270                            ORGANIZATION IS INDEXED
003280                            ACCESS MODE IS SEQUENTIAL
003290                            RECORD KEY IS I1636-KEY
003300                            FILE STATUS IS WS-I1636-FILE-STATUS.
DLXMIG     SELECT I1909P-FILE   ASSIGN RSD-I1909P.
DLXMIG     SELECT WORK1-FILE    ASSIGN RSD-WORK1.
DLXMIG     SELECT WORK2-FILE    ASSIGN RSD-WORK2.
003340     SELECT I554-FILE     ASSIGN I554
003350                            ORGANIZATION IS INDEXED
003360                            ACCESS MODE IS SEQUENTIAL
003370                            RECORD KEY IS I554-KEY
003380                            FILE STATUS IS WS-I554-FILE-STATUS.
DLXMIG     SELECT WORK3-FILE    ASSIGN RSD-WORK3.
DLXMIG     SELECT I1788P-FILE   ASSIGN RSD-I1788P.
DLXMIG     SELECT I1908P-FILE   ASSIGN RSD-I1908P.
DLXMIG     SELECT I1920P-FILE   ASSIGN RSD-I1920P.
DLXMIG     SELECT WORK4-FILE    ASSIGN RSD-WORK4.
003440     SELECT PRINTER1-FILE 
DLXMIG             ASSIGN PRINTER1
DLXMIG             ORGANIZATION IS LINE SEQUENTIAL.
003470*
003480 DATA DIVISION.
003490 FILE SECTION.
003500*
003510 FD  I471-FILE
003520*
003530 COPY T0471I.
003540*
003550 FD  I1636-FILE
003560*
003570 COPY T1636I.
003580*
003590 FD  I1909P-FILE
003600     RECORDING MODE IS F
003610     BLOCK CONTAINS 0 RECORDS
003620     RECORD CONTAINS 80 CHARACTERS
003630     LABEL RECORDS STANDARD.
003640   01  I1909P-RECORD.
003650     03  I1909P-SEQ.
003660       05  I1909P-BC                            PIC X(8).
003670       05  I1909P-PRI-BRANCH                    PIC X(5).
003680     03  FILLER                                 PIC X(67).
003690*
003700 FD  WORK1-FILE
003710     RECORDING MODE IS F
003720     BLOCK CONTAINS 0 RECORDS
003730     RECORD CONTAINS 74 CHARACTERS
003740     LABEL RECORDS STANDARD.
003750   01  WK1-RECORD.
003760     03  WK1-554-KEY                             PIC X(46).
003770     03  WK1-CUST-ID                             PIC X(9).
003780     03  WK1-BC-BR-BR                            PIC X(18).
003790     03  WK1-C1909-EXCL-FLAG                     PIC X.
003800*
003810 FD  WORK2-FILE
003820     RECORDING MODE IS F
003830     BLOCK CONTAINS 0 RECORDS
003840     RECORD CONTAINS 34 CHARACTERS
003850     LABEL RECORDS STANDARD.
003860   01  WK2-RECORD.
003870     03  WK2-CUST-ID                             PIC X(9).
003880     03  WK2-171-KEY.
003890       05  WK2-171-KEY-GROUP                     PIC X(4).
003900       05  WK2-171-KEY-BANK-SYS                  PIC X(7).
003910       05  WK2-171-KEY-OFFICE-TYPE               PIC X(2).
003920         88  WK2-171-SHELL-RECORD         VALUES '45', '50', '55'.
003930       05  WK2-171-KEY-BRANCH                    PIC X(3).
003940     03  WK2-171-CATALOG                         PIC X(4).
003950     03  WK2-171-VERSION                         PIC X(2).
003960     03  WK2-171-DLX-CAT-TOTAL                   PIC S9(5) COMP-3.
003970*
003980 FD  I554-FILE
003990*
004000 COPY T0554I.
004010*
004020 FD  WORK3-FILE
004030     RECORDING MODE IS F
004040     BLOCK CONTAINS 0 RECORDS
004050     RECORD CONTAINS 76 CHARACTERS
004060     LABEL RECORDS STANDARD.
004070   01  WK3-RECORD.
004080     03  WK3-CUST-ID                             PIC X(9).
004090     03  WK3-BC-BR-BR                            PIC X(18).
004100     03  WK3-554-KEY                             PIC X(46).
004110     03  WK3-554-VERSION                         PIC X(2).
004120     03  WK3-C1909-EXCL-FLAG                     PIC X.
004130       88  WK3-EXCLUDED-BY-C1909                   VALUE 'Y'.
004140*
004150 FD  I1788P-FILE
004160     RECORDING MODE IS F
004170     BLOCK CONTAINS 0 RECORDS
004180     RECORD CONTAINS 80 CHARACTERS
004190     LABEL RECORDS STANDARD.
004200   01  I1788P-RECORD.
004210     03  I1788P-INFO.
004220       05  I1788P-CATALOG                        PIC X(4).
004230       05  FILLER                                PIC X.
004240       05  I1788P-VERSION                        PIC X(2).
004250     03  FILLER                                  PIC X(73).
004260*
004270 FD  I1908P-FILE
004280     RECORDING MODE IS F
004290     BLOCK CONTAINS 0 RECORDS
004300     RECORD CONTAINS 80 CHARACTERS
004310     LABEL RECORDS STANDARD.
004320   01  I1908P-RECORD.
004330     03  I1908P-INFO.
004340       05  I1908P-VERSION                        PIC X(2).
004350       05  FILLER                                PIC X.
004360       05  I1908P-CATALOG                        PIC X(4).
004370     03  FILLER                                  PIC X(73).
004380*
004390 FD  I1920P-FILE
004400     RECORDING MODE IS F
004410     BLOCK CONTAINS 0 RECORDS
004420     RECORD CONTAINS 80 CHARACTERS
004430     LABEL RECORDS STANDARD.
004440   01  I1920P-RECORD.
004450     03  I1920P-BANK-NUMBER.
004460       05  I1920P-BANK-SYSTEM.
004470         07  I1920P-BK-SYS-1ST-TWO.
004480*                * TWO NUMERALS OR TWO LETTERS *
004490           09  I1920P-BK-SYS-CHAR-1              PIC X.
004500           09  I1920P-BK-SYS-CHAR-2              PIC X.
004510         07  I1920P-BK-SYS-REST                  PIC X(5).
004520*                * MUST BE NUMERIC *
004530       05  I1920P-BANK-BRANCH                    PIC X(3).
004540*                * BLANK, OR THREE NON-BLANK NUMERALS & LETTERS *
004550     03  FILLER                                  PIC X(70).
004560*
004570 FD  WORK4-FILE
004580     RECORDING MODE IS F
004590     BLOCK CONTAINS 0 RECORDS
004600     RECORD CONTAINS 89 CHARACTERS
004610     LABEL RECORDS STANDARD.
004620   01  WK4-RECORD.
004630     03  WK4-REPORT-NBR                          PIC X.
004640     03  WK4-PRINT-DATA.
004650       05  WK4-BC-BR-BR                          PIC X(18).
004660       05  WK4-171-KEY                           PIC X(16).
004670       05  WK4-554-KEY.
004680         07  WK4-554-KEY-LEFT                    PIC X(26).
004690         07  WK4-554-KEY-RIGHT                   PIC X(20).
004700       05  WK4-171-CATALOG                       PIC X(4).
004710       05  WK4-171-VERSION                       PIC X(2).
004720       05  WK4-554-VERSION                       PIC X(2).
004730     66  WK4-SEQ RENAMES WK4-REPORT-NBR THRU WK4-171-KEY.
004740*
004750 FD  PRINTER1-FILE
004760     RECORDING MODE IS F
004770     BLOCK CONTAINS 0 RECORDS
004780     RECORD CONTAINS 132 CHARACTERS
004790     LABEL RECORDS OMITTED.
004800   01  PRINTER1-RECORD.
004810     03  PR1-BC-BR-BR                          PIC X(8)BX(5)BX(5).
004820     03  FILLER                                  PIC X(3).
004830     03  PR1-554-KEY-LEFT       PIC XBX(5)BXXBXXBXXBX(8)BXXBXXBXX.
004840     03  FILLER                                  PIC X.
004850     03  PR1-554-KEY-RIGHT                PIC X(8)BX(5)BX(5)BX(2).
004860     03  FILLER                                  PIC X(3).
004870     03  PR1-171-KEY                      PIC X(4)BX(7)BX(2)BX(3).
004880     03  FILLER                                  PIC X(5).
004890     03  PR1-171-CATALOG                         PIC X(4).
004900     03  FILLER                                  PIC X(6).
004910     03  PR1-171-VERSION                         PIC X(2).
004920     03  FILLER                                  PIC X(7).
004930     03  PR1-554-VERSION                         PIC X(2).
004940     03  FILLER                                  PIC X(3).
004950*
004960 WORKING-STORAGE SECTION.
DLXMIG* DLXMIG Changes start here
DLXMIG COPY TDLXMIGE.
DLXMIG     EXEC SQL INCLUDE SQLCA END-EXEC.
DLXMIG* DLXMIG Changes end here
005010*
005020 01  WS-FLAGS-AND-SUCH                           VALUE SPACES.
005030   03  WS-I1636-FILE-STATUS       PIC X(2).
005040     88  WS-I1636-SUCCESSFUL        VALUE '00'.
005050     88  WS-I1636-END-OF-FILE       VALUE '10'.
005060     88  WS-I1636-SUCCESSFUL-OPEN   VALUES '00', '97'.
005070   03  WS-I554-FILE-STATUS        PIC X(2).
005080     88  WS-I554-SUCCESSFUL         VALUE '00'.
005090     88  WS-I554-END-OF-FILE        VALUE '10'.
005100     88  WS-I554-SUCCESSFUL-OPEN    VALUES '00', '97'.
005110   03  WS-I554-FILE-FLAG          PIC X.
005120     88  OPENED-I554-FILE           VALUE 'Y'.
005130   03  WS-I1636-FILE-FLAG         PIC X.
005140     88  OPENED-I1636-FILE          VALUE 'Y'.
005150   03  WS-I471-FILE-FLAG          PIC X.
005160     88  END-I471-FILE              VALUE 'E'.
005170   03  WS-I1788P-FILE-FLAG        PIC X.
005180     88  END-I1788P-FILE            VALUE 'E'.
005190   03  WS-I1908P-FILE-FLAG        PIC X.
005200     88  END-I1908P-FILE            VALUE 'E'.
005210   03  WS-I1909P-FILE-FLAG        PIC X.
005220     88  END-I1909P-FILE            VALUE 'E'.
005230   03  WS-I1920P-FILE-FLAG        PIC X.
005240     88  END-I1920P-FILE            VALUE 'E'.
005250   03  WS-WORK1-FILE-FLAG         PIC X.
005260     88  END-WORK1-FILE             VALUE 'E'.
005270   03  WS-WORK2-FILE-FLAG         PIC X.
005280     88  END-WORK2-FILE             VALUE 'E'.
005290   03  WS-WORK3-FILE-FLAG         PIC X.
005300     88  END-WORK3-FILE             VALUE 'E'.
005310   03  WS-WORK4-FILE-FLAG         PIC X.
005320     88  END-WORK4-FILE             VALUE 'E'.
005330   03  WS-PASS                    PIC X.
005340   03  WS-P1-PARM-NON-BILL-INSTR  PIC X.
005350     88  INCLUDE-I471-NON-BILLS     VALUE 'I'.
005360     88  OMIT-I471-NON-BILLS        VALUE 'O'.
005370   03  WS-P1-C1909-EXCL-FLAG      PIC X.
005380   03  WS-1636-ACCESS-FLAGS.
005390     05  WS-1636-ACCESS-HC-FLAG   PIC X.
005400     05  WS-1636-ACCESS-MBC-FLAG  PIC X.
005410     05  WS-1636-ACCESS-BC-FLAG   PIC X.
005420     05  WS-1636-ACCESS-TYPE-FLAG PIC X.
005430   03  WS-P2-554-VERSION          PIC X(2).
005440   03  WS-P3-I1788P-LOAD-FLAG     PIC X.
005450     88  LOADED-I1788P-DATA         VALUE 'Y'.
005460   03  WS-REPORT-NBR              PIC X.
005470   03  WS-RETURN-08-FLAG          PIC X.
005480     88  RETURN-08                  VALUE 'Y'.
005490   03  WS-RETURN-04-FLAG          PIC X.
005500     88  RETURN-04                  VALUE 'Y'.
005510*
005520 01  WS-SEQUENCES                                VALUE LOW-VALUES.
005530   03  WS-I1909P-SEQ              PIC X(13).
005540   03  WS-I1909P-MAX-SEQ.
005550     05  FILLER                   PIC X(8).
005560     05  WS-I1909P-MAX-BRANCH     PIC X(5).
005570   03  WS-WK1-554-KEY             PIC X(46).
005580   03  WS-HOLD-WK1-554-KEY        PIC X(46).
005590   03  WS-I1908P-SEQ.
005600     05  WS-I1908P-VERSION        PIC X(2).
005610     05  WS-I1908P-CATALOG        PIC X(4).
005620   03  WS-PREV-I1908P-SEQ         PIC X(6).
005630   03  WS-I1920P-BANK-NUMBER.
005640     05  WS-I1920P-BANK-SYSTEM    PIC X(7).
005650     05  WS-I1920P-BANK-BRANCH    PIC X(3).
005660   03  WS-WK2-CUST-ID             PIC X(9).
005670   03  WS-WK3-CUST-ID             PIC X(9).
005680   03  WS-WK4-SEQ                 PIC X(35).
005690   03  WS-HOLD-WK4-BC             PIC X(8).
005700   03  WS-HOLD-WK4-BK-SYS         PIC X(8).
005710*
005720 01  WS-PRINT-CONTROLS.
005730   03  WS-LINE-COUNT              PIC S9(3)      COMP VALUE +090.
005740   03  WS-LINE-SPACER             PIC S9         COMP VALUE +2.
005750   03  WS-PAGE-NUMBER             PIC S9(5)      COMP-3 VALUE +0.
005760*
005770 01  WS-HEADINGS-ETC.
005780   03  WS-HEADING-1.
005790     05  FILLER                   PIC X          VALUE '-'.
005800     05  WS-HD1-PROGRAM-NUMBER    PIC X(8)       VALUE SPACE.
005810     05  FILLER                   PIC X(18)      VALUE '-'.
005820     05  WS-HD1-REPORT-TITLE      PIC X(42)      VALUE
005830                     '    MISMATCHED 171 & 554 EOP VERSIONS     '.
005840     05  FILLER                   PIC X(17)      VALUE SPACE.
005850     05  FILLER                   PIC X(9)      VALUE 'RUN DATE '.
005860     05  FILLER                                VALUE '00-00-0000'.
005870       07  WS-HD1-RUN-MONTH       PIC X(2).
005880       07  FILLER                 PIC X.
005890       07  WS-HD1-RUN-DAY         PIC X(2).
005900       07  FILLER                 PIC X.
005910       07  WS-HD1-RUN-YEAR        PIC X(4).
005920     05  FILLER                   PIC X(18)      VALUE SPACE.
005930     05  FILLER                   PIC X(4)       VALUE 'PAGE'.
005940     05  WS-HD1-PAGE-NUMBER       PIC ZZZZ9.
005950   03  WS-HEADING-2.
005960     05  FILLER                   PIC X(23)      VALUE '1636 KEY'.
005970     05  FILLER                   PIC X(61)      VALUE '554 KEY'.
005980     05  FILLER                   PIC X(19)      VALUE '171 KEY'.
005990     05  FILLER   PIC X(29) VALUE '     171      171      554   '.
006000   03  WS-HEADING-3.
006010     05  FILLER                   PIC X(20)      VALUE ALL '-'.
006020     05  FILLER                   PIC X(3)       VALUE SPACE.
006030     05  FILLER                   PIC X(58)      VALUE ALL '-'.
006040     05  FILLER                   PIC X(3)       VALUE SPACE.
006050     05  FILLER                   PIC X(19)      VALUE ALL '-'.
006060     05  FILLER   PIC X(29) VALUE '   CATALOG  VERSION  VERSION '.
006070   03  WS-REPORT-2-TITLE          PIC X(42)      VALUE
006080                     '  554 EOP VERSION WITHOUT 171 EOP VERSION '.
006090   03  WS-REPORT-3-TITLE          PIC X(42)      VALUE
006100                     '  171 EOP VERSION WITHOUT 554 EOP VERSION '.
006110   03  WS-REPORT-4-TITLE          PIC X(42)      VALUE
006120                     'MISMATCHED 171 CATALOG CODE & EOP VERSION '.
006130*
006140 01  WS-PASS-3-WORK.
006150   03  WS-P3-FILE-FLAG            PIC X.
006160     88  P3-WORK2-ONLY              VALUE '2'.
006170     88  P3-WORK3-ONLY              VALUE '3'.
006180     88  BOTH-P3-FILES              VALUE SPACE.
006190   03  WS-P3-C1920-MATCH-FLAG     PIC X.
006200     88  P3-C1920-MATCHED           VALUE 'Y'.
006210   03  WS-P3-WK2-INFO.
006220     05  WS-P3-WK2-VERSION        PIC X(2).
006230     05  WS-P3-WK2-CATALOG        PIC X(4).
006240   03  WS-P3-WK3-VERSION          PIC X(2).
006250*
006260 01  WS-VERSION-CATALOG-TABLE.
006270   03  WS-VER-CAT-ENTRY-COUNT     PIC S9(3)      COMP.
006280   03  WS-VER-CAT-ENTRY           OCCURS 0 TO 500 TIMES
006290                                  DEPENDING ON
006300                                    WS-VER-CAT-ENTRY-COUNT
006310                                  ASCENDING KEY IS
006320                                    WS-VER-CAT-KEY
006330                                  INDEXED BY IX-WS-VER-CAT.
006340     05  WS-VER-CAT-KEY.
006350       07  WS-VER-CAT-VERSION     PIC X(2).
006360       07  WS-VER-CAT-CATALOG     PIC X(4).
006370     05  WS-VER-CAT-PRICED-FLAG   PIC X.
006380       88  VERSION-W-PRICED-CATALOG VALUE 'Y'.
006390*
006400 01  WS-C1920-TABLE.
006410   03  WS-C1920-ENTRY             OCCURS 250 TIMES
006420                                  INDEXED BY IX-WS-C1920-OUT
006430                                             IX-WS-C1920-IN.
006440     05  WS-C1920-BANK-SYSTEM     PIC X(7).
006450     05  WS-C1920-BANK-BRANCH     PIC X(3).
006460*
006470 01  WS-RETURN-CODE-LINE.
006480   03  FILLER                    PIC X(14) VALUE 'RETURN CODE = '.
006490   03  WS-RETURN-CODE             PIC 9(2)       DISPLAY VALUE 00.
006500   03  FILLER                     PIC X          VALUE '.'.
006510*
006520 COPY CPY108.
006530*
006540 01  WS-DISPLAY-PARM.
006550   03  WS-DP-CAPTION             PIC X(14) VALUE 'PARM OPTIONS: '.
006560   03  FILLER                     PIC X          VALUE '"'.
006570   03  WS-DP-STATEMENT            PIC X(20)      VALUE SPACE.
006580   03  FILLER                     PIC X          VALUE '"'.
006590*
006600 COPY CPY004.
006610*
006620 01  WS-PROGRAM-NUMBER            PIC X(8)       VALUE 'BIL02705'.
006630 01  WS-REVISION-DATE             PIC X(8)       VALUE '11-11-02'.
006640*
DLXMIG* DLXMIG Changes start here
DLXMIG*LINKAGE SECTION.
DLXMIG* DLXMIG Changes end here
006680*
006690 01  LS-PARM-INFO.
006700   03  LS-PARM-LENGTH             PIC S9(4)      COMP.
006710   03  LS-PARM-DATA               PIC X(100).
006720*
DLXMIG* DLXMIG Changes start here
DLXMIG LINKAGE SECTION.
DLXMIG COPY AIXLNCPY.
DLXMIG* DLXMIG Changes end here
DLXMIG* DLXMIG Changes start here
DLXMIG*PROCEDURE DIVISION USING LS-PARM-INFO.
DLXMIG PROCEDURE DIVISION USING BY VALUE PARMCNT
DLXMIG           BY REFERENCE OS-PARM.
DLXMIG COPY AIXPRCPY.
DLXMIG* DLXMIG Changes end here
006830*
DLXMIG 0000-SQL-DB-CONNECT.
DLXMIG     MOVE 'DLXVSAM' TO DBNAME.
DLXMIG COPY CPYMIGUD.
006870   0000-MAIN-LINE.
006880     PERFORM 8000-COMMON-INIT.
006890     IF WS-PASS LESS '3'
006900       IF WS-PASS LESS '2'
006910         PERFORM 1000-PASS-1
006920       ELSE
006930         PERFORM 2000-PASS-2
006940     ELSE
006950       IF WS-PASS LESS '4'
006960         PERFORM 3000-PASS-3
006970       ELSE
006980         PERFORM 4000-PASS-4.
006990     PERFORM 9000-COMMON-CLOSE.
007000     STOP RUN.
007010*
007020*
007030   1000-PASS-1.
007040     PERFORM 1800-PASS-1-INIT.
007050     PERFORM 1100-EACH-I1909P-CARD
007060         WITH TEST BEFORE
007070         UNTIL END-I1909P-FILE.
007080     PERFORM 1300-EACH-I1636-REC
007090         WITH TEST BEFORE
007100         UNTIL WS-I1636-END-OF-FILE.
007110     PERFORM 1500-EACH-I471-REC
007120         WITH TEST AFTER
007130         UNTIL END-I471-FILE.
007140     PERFORM 1900-PASS-1-CLOSE.
007150*
007160   1100-EACH-I1909P-CARD.
007170     IF (I1909P-BC IS NUMERIC)
007180         AND ((I1909P-PRI-BRANCH IS NUMERIC)
007190             OR (I1909P-PRI-BRANCH EQUAL SPACE))
007200       PERFORM 1300-EACH-I1636-REC
007210           WITH TEST BEFORE
007220           UNTIL WS-I1636-END-OF-FILE
007230               OR (I1636-KEY (1:13) NOT LESS WS-I1909P-SEQ)
007240       IF (NOT WS-I1636-END-OF-FILE)
007250           AND (I1636-KEY (1:13) NOT GREATER WS-I1909P-MAX-SEQ)
007260         MOVE 'Y' TO WS-P1-C1909-EXCL-FLAG
007270         PERFORM 1300-EACH-I1636-REC
007280             WITH TEST AFTER
007290             UNTIL WS-I1636-END-OF-FILE
007300                 OR (I1636-KEY (1:13) GREATER WS-I1909P-MAX-SEQ)
007310         MOVE SPACE TO WS-P1-C1909-EXCL-FLAG
007320       ELSE
007330         MOVE 'Y' TO WS-RETURN-04-FLAG
007340         DISPLAY 'NO I1636 MATCH FOR I1909P CARD "'
007350               WS-I1909P-SEQ '"' UPON SYSOUT
007360     ELSE
007370       MOVE 'Y' TO WS-RETURN-08-FLAG
007380       DISPLAY 'I1909P CARD "' WS-I1909P-SEQ '" REJECTED FOR:'
007390             UPON SYSOUT
007400       IF I1909P-BC NOT NUMERIC
007410         DISPLAY '   NON-NUMERIC BC "' I1909P-BC '"' UPON SYSOUT
007420       END-IF
007430       IF (I1909P-PRI-BRANCH NOT NUMERIC)
007440           AND (I1909P-PRI-BRANCH NOT EQUAL SPACE)
007450         DISPLAY '   NON-NUMERIC NON-BLANK PRIMARY BRANCH "'
007460               I1909P-PRI-BRANCH '"' UPON SYSOUT.
007470     PERFORM 1200-READ-I1909P-FILE.
007480*
007490   1200-READ-I1909P-FILE.
007500     READ I1909P-FILE
007510       AT END
007520         MOVE 'E' TO WS-I1909P-FILE-FLAG
007530       NOT AT END
007540         IF I1909P-SEQ GREATER WS-I1909P-MAX-SEQ
007550           MOVE I1909P-SEQ TO WS-I1909P-SEQ WS-I1909P-MAX-SEQ
007560           IF WS-I1909P-MAX-BRANCH EQUAL SPACE
007570             MOVE HIGH-VALUES TO WS-I1909P-MAX-BRANCH
007580           END-IF
007590         ELSE
007600           IF I1909P-SEQ NOT GREATER WS-I1909P-SEQ
007610             DISPLAY '* I1909P SEQUENCE ERROR "' WS-I1909P-SEQ
007620                   '" "' I1909P-SEQ '" *' UPON SYSOUT
007630           ELSE
007640             DISPLAY '* I1909P SEQUENCE OVERLAP "' WS-I1909P-SEQ
007650                   '" "' I1909P-SEQ '" *' UPON SYSOUT
007660           END-IF
007670           GO TO 9900-DISPLAY-ABORT
007680         END-IF
007690     END-READ.
007700*
007710   1300-EACH-I1636-REC.
007720     MOVE SPACE TO WK1-554-KEY.
007730     IF (NOT I1636-SUMMARY-ONLY-BRANCH (1))
007740         AND (NOT I1636-NO-UPCHARGE-REC (1))
007750       MOVE I1636-UPCHARGE-ACCESS (1) TO WS-1636-ACCESS-FLAGS
007760       MOVE 'B' TO WK1-554-KEY (1:1)
007770       IF WS-1636-ACCESS-HC-FLAG GREATER '0'
007780         MOVE I1636-HOLDING-COMPANY (1) TO WK1-554-KEY (2:5)
007790         IF WS-1636-ACCESS-HC-FLAG GREATER '1'
007800           MOVE I1636-HOLD-CO-LEVEL-1-DIV (1) TO WK1-554-KEY (7:2)
007810           IF WS-1636-ACCESS-HC-FLAG GREATER '2'
007820             MOVE I1636-HOLD-CO-LEVEL-2-DIV (1)
007830                   TO WK1-554-KEY (9:2)
007840             IF WS-1636-ACCESS-HC-FLAG GREATER '3'
007850               MOVE I1636-HOLD-CO-LEVEL-3-DIV (1)
007860                     TO WK1-554-KEY (11:2)
007870             END-IF
007880           END-IF
007890         END-IF
007900       END-IF
007910       IF WS-1636-ACCESS-MBC-FLAG GREATER '0'
007920         MOVE I1636-FI-MASTER-BC (1) TO WK1-554-KEY (13:8)
007930         IF WS-1636-ACCESS-MBC-FLAG GREATER '1'
007940           MOVE I1636-FI-LEVEL-1-DIV (1) TO WK1-554-KEY (21:2)
007950           IF WS-1636-ACCESS-MBC-FLAG GREATER '2'
007960             MOVE I1636-FI-LEVEL-1-DIV (1) TO WK1-554-KEY (23:2)
007970             IF WS-1636-ACCESS-MBC-FLAG GREATER '3'
007980               MOVE I1636-FI-LEVEL-1-DIV (1) TO WK1-554-KEY (25:2)
007990             END-IF
008000           END-IF
008010         END-IF
008020       END-IF
008030       IF WS-1636-ACCESS-BC-FLAG GREATER '0'
008040         MOVE I1636-BC TO WK1-554-KEY (27:8)
008050         IF WS-1636-ACCESS-BC-FLAG GREATER '1'
008060           MOVE I1636-PRIMARY-BRANCH TO WK1-554-KEY (35:5)
008070           IF WS-1636-ACCESS-BC-FLAG GREATER '2'
008080             MOVE I1636-SECONDARY-BRANCH TO WK1-554-KEY (40:5)
008090           END-IF
008100         END-IF
008110       END-IF
008120       IF WS-1636-ACCESS-TYPE-FLAG GREATER '0'
008130         MOVE I1636-BILLING-TYPE (1) TO WK1-554-KEY (45:2).
008140*
008150     MOVE I1636-CUSTOMER-ID TO WK1-CUST-ID.
008160     MOVE I1636-KEY TO WK1-BC-BR-BR.
008170     MOVE WS-P1-C1909-EXCL-FLAG TO WK1-C1909-EXCL-FLAG.
008180     WRITE WK1-RECORD.
008190     PERFORM 1400-READ-I1636-FILE.
008200*
008210   1400-READ-I1636-FILE.
008220     READ I1636-FILE NEXT.
008230     IF (NOT WS-I1636-SUCCESSFUL)
008240         AND (NOT WS-I1636-END-OF-FILE)
008250       DISPLAY '* VSAM ERROR ON I1636 READ NEXT.  FILE STATUS = '
008260             WS-I1636-FILE-STATUS '. *' UPON SYSOUT
008270       GO TO 9900-DISPLAY-ABORT.
008280*
008290   1500-EACH-I471-REC.
008300     IF INCLUDE-I471-NON-BILLS
008310         OR ((I471-INSTITUTION-NBR IS NUMERIC)
008320             AND ((I471-BRANCH-NBR IS NUMERIC)
008330                 OR (I471-BRANCH-NBR EQUAL SPACES)))
008340       MOVE I471-CUSTOMER-ID-NUMBER TO WK2-CUST-ID
008350       MOVE I471-REC-KEY TO WK2-171-KEY
008360       MOVE I471-DLX-CAT-CODE TO WK2-171-CATALOG
008370       MOVE I471-DLX-CATALOG-VERSION TO WK2-171-VERSION
008380       MOVE I471-DLX-CAT-TOTAL TO WK2-171-DLX-CAT-TOTAL
008390       WRITE WK2-RECORD.
008400     PERFORM 1600-READ-I471-FILE.
008410*
008420   1600-READ-I471-FILE.
008430     READ I471-FILE AT END
008440       MOVE 'E' TO WS-I471-FILE-FLAG.
008450*
008460   1800-PASS-1-INIT.
008470     OPEN OUTPUT WORK2-FILE WORK1-FILE
008480           INPUT I471-FILE I1909P-FILE I1636-FILE.
008490     IF NOT WS-I1636-SUCCESSFUL-OPEN
008500       DISPLAY '* VSAM ERROR ON I1636 OPEN.  FILE STATUS = '
008510             WS-I1636-FILE-STATUS '. *' UPON SYSOUT
008520       GO TO 9900-DISPLAY-ABORT.
008530     MOVE 'Y' TO WS-I1636-FILE-FLAG.
008540     PERFORM 1400-READ-I1636-FILE.
008550     IF WS-I1636-END-OF-FILE
008560       DISPLAY '* I1636 IS AN EMPTY FILE *' UPON SYSOUT
008570       GO TO 9900-DISPLAY-ABORT.
008580     PERFORM 1600-READ-I471-FILE.
008590     IF END-I471-FILE
008600       DISPLAY '* I471 IS AN EMPTY FILE *' UPON SYSOUT
008610       GO TO 9900-DISPLAY-ABORT.
008620     PERFORM 1200-READ-I1909P-FILE.
008630     IF END-I1909P-FILE
008640       DISPLAY 'I1909P IS AN EMPTY FILE.' UPON SYSOUT
008650       MOVE 'Y' TO WS-RETURN-04-FLAG.
008660*
008670   1900-PASS-1-CLOSE.
008680     CLOSE WORK2-FILE WORK1-FILE I1909P-FILE I471-FILE.
008690     PERFORM 1910-CLOSE-I1636-FILE.
008700     IF NOT WS-I1636-SUCCESSFUL
008710       DISPLAY 'VSAM ERROR ON I1636 CLOSE.  FILE STATUS = '
008720             WS-I1636-FILE-STATUS '.' UPON SYSOUT
008730       MOVE 'Y' TO WS-RETURN-04-FLAG.
008740*
008750   1910-CLOSE-I1636-FILE.
008760     MOVE 'N' TO WS-I1636-FILE-FLAG.
008770     CLOSE I1636-FILE.
008780*
008790*
008800   2000-PASS-2.
008810     PERFORM 2800-PASS-2-INIT.
008820     PERFORM 2100-COMPARE-WK1-I554
008830         WITH TEST AFTER
008840         UNTIL END-WORK1-FILE.
008850     PERFORM 2900-PASS-2-CLOSE.
008860*
008870   2100-COMPARE-WK1-I554.
008880     PERFORM 2300-READ-I554-FILE
008890         WITH TEST BEFORE
008900         UNTIL WS-I554-END-OF-FILE
008910             OR (I554-KEY (1:46) NOT LESS WS-WK1-554-KEY)
008920     IF WS-I554-END-OF-FILE
008930         OR (I554-KEY (1:46) GREATER WS-WK1-554-KEY)
008940       MOVE SPACE TO WS-P2-554-VERSION
008950       IF WS-WK1-554-KEY NOT EQUAL SPACE
008960         DISPLAY 'MISSING I554 RECORD FOR KEY "'
008970               WS-WK1-554-KEY '".' UPON SYSOUT
008980         MOVE 'Y' TO WS-RETURN-08-FLAG
008990       END-IF
009000     ELSE
009010       MOVE I554-MSRP-CATALOG-VERSION TO WS-P2-554-VERSION.
009020*
009030     MOVE WS-WK1-554-KEY TO WS-HOLD-WK1-554-KEY.
009040     PERFORM
009050         WITH TEST AFTER
009060         UNTIL END-WORK1-FILE
009070             OR (WS-WK1-554-KEY GREATER WS-HOLD-WK1-554-KEY)
009080       MOVE WK1-CUST-ID TO WK3-CUST-ID
009090       MOVE WK1-BC-BR-BR TO WK3-BC-BR-BR
009100       MOVE WK1-554-KEY TO WK3-554-KEY
009110       MOVE WS-P2-554-VERSION TO WK3-554-VERSION
009120       MOVE WK1-C1909-EXCL-FLAG TO WK3-C1909-EXCL-FLAG
009130       WRITE WK3-RECORD
009140       PERFORM 2200-READ-WORK1-FILE
009150     END-PERFORM.
009160*
009170   2200-READ-WORK1-FILE.
009180     READ WORK1-FILE
009190       AT END
009200         MOVE 'E' TO WS-WORK1-FILE-FLAG
009210       NOT AT END
009220         IF WK1-554-KEY NOT LESS WS-WK1-554-KEY
009230           MOVE WK1-554-KEY TO WS-WK1-554-KEY
009240         ELSE
009250           DISPLAY '* WORK1 SEQUENCE ERROR "' WS-WK1-554-KEY '"'
009260                 UPON SYSOUT
009270           DISPLAY '           FOLLOWED BY "' WK1-554-KEY '" *'
009280                 UPON SYSOUT
009290           GO TO 9900-DISPLAY-ABORT
009300         END-IF
009310     END-READ.
009320*
009330   2300-READ-I554-FILE.
009340     READ I554-FILE NEXT.
009350     IF (NOT WS-I554-SUCCESSFUL)
009360         AND (NOT WS-I554-END-OF-FILE)
009370       DISPLAY '* VSAM ERROR ON I554 READ NEXT.  FILE STATUS = '
009380             WS-I554-FILE-STATUS '. *' UPON SYSOUT
009390       GO TO 9900-DISPLAY-ABORT.
009400*
009410   2800-PASS-2-INIT.
009420     OPEN OUTPUT WORK3-FILE INPUT WORK1-FILE I554-FILE.
009430     IF NOT WS-I554-SUCCESSFUL-OPEN
009440       DISPLAY '* VSAM ERROR ON I554 OPEN.  FILE STATUS = '
009450             WS-I554-FILE-STATUS '. *' UPON SYSOUT
009460       GO TO 9900-DISPLAY-ABORT.
009470     MOVE 'Y' TO WS-I554-FILE-FLAG.
009480     PERFORM 2300-READ-I554-FILE.
009490     IF WS-I554-END-OF-FILE
009500       DISPLAY '* I554 IS AN EMPTY FILE *' UPON SYSOUT
009510       GO TO 9900-DISPLAY-ABORT.
009520     PERFORM 2200-READ-WORK1-FILE.
009530     IF END-WORK1-FILE
009540       DISPLAY '* WORK1 IS AN EMPTY FILE *' UPON SYSOUT
009550       GO TO 9900-DISPLAY-ABORT.
009560*
009570   2900-PASS-2-CLOSE.
009580     CLOSE WORK3-FILE WORK1-FILE.
009590     PERFORM 2910-CLOSE-I554-FILE.
009600     IF NOT WS-I554-SUCCESSFUL
009610       DISPLAY 'VSAM ERROR ON I554 CLOSE.  FILE STATUS = '
009620             WS-I554-FILE-STATUS '.' UPON SYSOUT
009630       MOVE 'Y' TO WS-RETURN-04-FLAG.
009640*
009650   2910-CLOSE-I554-FILE.
009660     MOVE 'N' TO WS-I554-FILE-FLAG.
009670     CLOSE I554-FILE.
009680*
009690*
009700   3000-PASS-3.
009710     PERFORM 3800-PASS-3-INIT.
009720     PERFORM 3100-EACH-CUST-ID
009730         WITH TEST AFTER
009740         UNTIL END-WORK2-FILE AND END-WORK3-FILE.
009750     PERFORM 3900-PASS-3-CLOSE.
009760*
009770   3100-EACH-CUST-ID.
009780     MOVE SPACE TO WS-PASS-3-WORK.
009790     IF END-WORK3-FILE
009800         OR ((NOT END-WORK2-FILE)
009810             AND (WS-WK2-CUST-ID LESS WS-WK3-CUST-ID))
009820       MOVE '2' TO WS-P3-FILE-FLAG
009830     ELSE
009840       IF END-WORK2-FILE
009850           OR (WS-WK3-CUST-ID LESS WS-WK2-CUST-ID)
009860         MOVE '3' TO WS-P3-FILE-FLAG.
009870     IF (NOT P3-WORK3-ONLY)
009880         AND (IX-WS-C1920-IN GREATER +1)
009890       PERFORM 3600-CHECK-C1920-TABLE.
009900     IF (P3-WORK2-ONLY
009910             OR (NOT WK3-EXCLUDED-BY-C1909))
009920*                * THERE IS NO WORK3 RECORD, OR THE WORK3 RECORD
009930*                  DOES NOT EXEMPT THE BRANCH FROM ERROR CHECKING
009940*                  (BY C1909 CARD) *
009950         AND (P3-WORK3-ONLY
009960             OR ((NOT WK2-171-SHELL-RECORD)
009970                 AND (NOT P3-C1920-MATCHED)
009980                 AND (WK2-171-DLX-CAT-TOTAL GREATER 0)))
009990*                * THERE IS NO WORK2 RECORD, OR THE WORK2 RECORD
010000*                  DOES NOT EXEMPT THE BRANCH FROM ERROR CHECKING
010010*                  (BY SHELL RECORD STATUS, BY C1920 CARD, OR BY
010020*                  HAVING A CATALOG COUNT OF ZERO) *
010030       PERFORM 3400-CHECK-VERSIONS.
010040     IF NOT P3-WORK3-ONLY
010050       PERFORM 3200-READ-WORK2-FILE.
010060     IF NOT P3-WORK2-ONLY
010070       PERFORM 3300-READ-WORK3-FILE.
010080*
010090   3200-READ-WORK2-FILE.
010100     READ WORK2-FILE
010110       AT END
010120         MOVE 'E' TO WS-WORK2-FILE-FLAG
010130       NOT AT END
010140         IF WK2-CUST-ID GREATER WS-WK2-CUST-ID
010150           MOVE WK2-CUST-ID TO WS-WK2-CUST-ID
010160         ELSE
010170           DISPLAY '* WORK2 SEQUENCE ERROR "' WS-WK2-CUST-ID '" "'
010180                 WK2-CUST-ID '" *' UPON SYSOUT
010190           GO TO 9900-DISPLAY-ABORT
010200         END-IF
010210     END-READ.
010220*
010230   3300-READ-WORK3-FILE.
010240     READ WORK3-FILE
010250       AT END
010260         MOVE 'E' TO WS-WORK3-FILE-FLAG
010270       NOT AT END
010280         IF WK3-CUST-ID GREATER WS-WK3-CUST-ID
010290           MOVE WK3-CUST-ID TO WS-WK3-CUST-ID
010300         ELSE
010310           DISPLAY '* WORK3 SEQUENCE ERROR "' WS-WK3-CUST-ID '" "'
010320                 WK3-CUST-ID '" *' UPON SYSOUT
010330           GO TO 9900-DISPLAY-ABORT
010340         END-IF
010350     END-READ.
010360*
010370   3400-CHECK-VERSIONS.
010380     IF NOT P3-WORK3-ONLY
010390       MOVE WK2-171-VERSION TO WS-P3-WK2-VERSION
010400       MOVE WK2-171-CATALOG TO WS-P3-WK2-CATALOG
010410       MOVE SPACE TO WS-REPORT-NBR
010420       IF WS-P3-WK2-CATALOG EQUAL SPACE
010430         IF WS-P3-WK2-VERSION NOT EQUAL SPACE
010440           SET IX-WS-VER-CAT TO +1
010450           SEARCH WS-VER-CAT-ENTRY
010460             AT END
010470               MOVE '4' TO WS-REPORT-NBR
010480             WHEN WS-VER-CAT-VERSION (IX-WS-VER-CAT) EQUAL
010490                   WS-P3-WK2-VERSION
010500               IF VERSION-W-PRICED-CATALOG (IX-WS-VER-CAT)
010510                 MOVE '4' TO WS-REPORT-NBR
010520               END-IF
010530           END-SEARCH
010540         END-IF
010550       ELSE
010560         IF WS-P3-WK2-VERSION NOT EQUAL SPACE
010570           SEARCH ALL WS-VER-CAT-ENTRY
010580             AT END
010590               MOVE '4' TO WS-REPORT-NBR
010600             WHEN WS-VER-CAT-KEY (IX-WS-VER-CAT) EQUAL
010610                   WS-P3-WK2-INFO
010620               CONTINUE
010630           END-SEARCH
010640         ELSE
010650           SET IX-WS-VER-CAT TO +1
010660           SEARCH WS-VER-CAT-ENTRY
010670             AT END
010680               CONTINUE
010690             WHEN WS-VER-CAT-CATALOG (IX-WS-VER-CAT) EQUAL
010700                   WS-P3-WK2-CATALOG
010710               IF VERSION-W-PRICED-CATALOG (IX-WS-VER-CAT)
010720                 MOVE '4' TO WS-REPORT-NBR
010730               END-IF
010740           END-SEARCH
010750         END-IF
010760       END-IF
010770       IF WS-REPORT-NBR EQUAL '4'
010780         PERFORM 3500-BUILD-WORK4-REC.
010790     IF NOT P3-WORK2-ONLY
010800       MOVE WK3-554-VERSION TO WS-P3-WK3-VERSION.
010810     IF WS-P3-WK2-VERSION NOT EQUAL WS-P3-WK3-VERSION
010820       IF WS-P3-WK3-VERSION EQUAL SPACE
010830         MOVE '3' TO WS-REPORT-NBR
010840       ELSE
010850         IF WS-P3-WK2-VERSION EQUAL SPACE
010860           MOVE '2' TO WS-REPORT-NBR
010870         ELSE
010880           MOVE '1' TO WS-REPORT-NBR
010890         END-IF
010900       END-IF
010910       PERFORM 3500-BUILD-WORK4-REC.
010920*
010930   3500-BUILD-WORK4-REC.
010940     MOVE WS-REPORT-NBR TO WK4-REPORT-NBR.
010950     MOVE SPACE TO WK4-PRINT-DATA.
010960     IF NOT P3-WORK2-ONLY
010970       MOVE WK3-BC-BR-BR TO WK4-BC-BR-BR
010980       MOVE WK3-554-KEY TO WK4-554-KEY
010990       MOVE WK3-554-VERSION TO WK4-554-VERSION.
011000     IF NOT P3-WORK3-ONLY
011010       MOVE WK2-171-KEY TO WK4-171-KEY
011020       MOVE WK2-171-CATALOG TO WK4-171-CATALOG
011030       MOVE WK2-171-VERSION TO WK4-171-VERSION.
011040     WRITE WK4-RECORD.
011050*
011060   3600-CHECK-C1920-TABLE.
011070     IF (WK2-171-KEY-BANK-SYS LESS WS-I1920P-BANK-SYSTEM)
011080         OR ((WK2-171-KEY-BANK-SYS EQUAL WS-I1920P-BANK-SYSTEM)
011090             AND (WK2-171-KEY-BRANCH LESS WS-I1920P-BANK-BRANCH))
011100*                * PREVIOUS SEARCH WAS FOR A HIGHER BANK NUMBER,
011110*                 START CURRENT SEARCH AT FIRST RECORD *
011120       SET IX-WS-C1920-OUT TO +1.
011130     MOVE WK2-171-KEY-BANK-SYS TO WS-I1920P-BANK-SYSTEM.
011140     MOVE WK2-171-KEY-BRANCH TO WS-I1920P-BANK-BRANCH.
011150     PERFORM
011160         WITH TEST BEFORE
011170         UNTIL (IX-WS-C1920-OUT NOT LESS IX-WS-C1920-IN)
011180             OR (WS-C1920-BANK-SYSTEM (IX-WS-C1920-OUT) GREATER
011190                   WK2-171-KEY-BANK-SYS)
011200             OR ((WS-C1920-BANK-SYSTEM (IX-WS-C1920-OUT) EQUAL
011210                   WK2-171-KEY-BANK-SYS)
011220                 AND ((WS-C1920-BANK-BRANCH (IX-WS-C1920-OUT)
011230                       EQUAL SPACE)
011240                     OR (WS-C1920-BANK-BRANCH (IX-WS-C1920-OUT)
011250                           NOT LESS WK2-171-KEY-BRANCH)))
011260       SET IX-WS-C1920-OUT UP BY 1
011270     END-PERFORM.
011280     IF (IX-WS-C1920-OUT LESS IX-WS-C1920-IN)
011290         AND (WS-C1920-BANK-SYSTEM (IX-WS-C1920-OUT) EQUAL
011300               WK2-171-KEY-BANK-SYS)
011310         AND (WS-C1920-BANK-BRANCH (IX-WS-C1920-OUT) EQUAL SPACE
011320               OR WK2-171-KEY-BRANCH)
011330       MOVE 'Y' TO WS-P3-C1920-MATCH-FLAG.
011340*
011350   3800-PASS-3-INIT.
011360     OPEN OUTPUT WORK4-FILE
011370           INPUT WORK3-FILE WORK2-FILE I1788P-FILE I1908P-FILE
011380                 I1920P-FILE.
011390     PERFORM 3200-READ-WORK2-FILE.
011400     IF END-WORK2-FILE
011410       DISPLAY '* WORK2 IS AN EMPTY FILE *' UPON SYSOUT
011420       GO TO 9900-DISPLAY-ABORT.
011430     PERFORM 3300-READ-WORK3-FILE.
011440     IF END-WORK3-FILE
011450       DISPLAY '* WORK3 IS AN EMPTY FILE *' UPON SYSOUT
011460       GO TO 9900-DISPLAY-ABORT.
011470     PERFORM 3860-READ-I1788P-FILE.
011480     IF END-I1788P-FILE
011490       DISPLAY '* I1788P IS AN EMPTY FILE *' UPON SYSOUT
011500       GO TO 9900-DISPLAY-ABORT.
011510     PERFORM 3820-READ-I1908P-FILE.
011520     IF END-I1908P-FILE
011530       DISPLAY '* I1908P IS AN EMPTY FILE *' UPON SYSOUT
011540       GO TO 9900-DISPLAY-ABORT.
011550     SET IX-WS-C1920-OUT IX-WS-C1920-IN TO +1.
011560     PERFORM 3890-READ-I1920P-FILE.
011570     IF END-I1920P-FILE
011580       MOVE 'Y' TO WS-RETURN-04-FLAG
011590       DISPLAY 'I1920P IS AN EMPTY FILE.' UPON SYSOUT
011600     ELSE
011610       PERFORM 3880-EACH-I1920P-CARD
011620           WITH TEST AFTER
011630           UNTIL END-I1920P-FILE
011640       IF IX-WS-C1920-IN GREATER +200
011650         MOVE 'Y' TO WS-RETURN-04-FLAG
011660         DISPLAY 'I1920P TABLE OVER 80% FULL.' UPON SYSOUT.
011670     MOVE +0 TO WS-VER-CAT-ENTRY-COUNT.
011680     PERFORM 3810-EACH-I1908P-CARD
011690         WITH TEST AFTER
011700         UNTIL END-I1908P-FILE.
011710     IF WS-VER-CAT-ENTRY-COUNT EQUAL +0
011720       DISPLAY '* NO VALID I1908P CARDS *' UPON SYSOUT
011730       GO TO 9900-DISPLAY-ABORT.
011740     IF WS-VER-CAT-ENTRY-COUNT GREATER +400
011750       DISPLAY 'VERSION/CATALOG (I1908P) TABLE OVER 80% FULL.'
011760             UPON SYSOUT
011770       MOVE 'Y' TO WS-RETURN-04-FLAG.
011780     PERFORM 3850-EACH-I1788P-CARD
011790         WITH TEST AFTER
011800         UNTIL END-I1788P-FILE.
011810     IF NOT LOADED-I1788P-DATA
011820       DISPLAY '* NO VALID I1788P CARDS *' UPON SYSOUT
011830       GO TO 9900-DISPLAY-ABORT.
011840*
011850   3810-EACH-I1908P-CARD.
011860     IF WS-I1908P-SEQ IS LETTERS-AND-NUMERALS
011870       IF WS-VER-CAT-ENTRY-COUNT LESS +500
011880         ADD 1 TO WS-VER-CAT-ENTRY-COUNT
011890         MOVE WS-I1908P-SEQ TO WS-VER-CAT-KEY
011900               (WS-VER-CAT-ENTRY-COUNT)
011910         MOVE SPACE TO WS-VER-CAT-PRICED-FLAG
011920               (WS-VER-CAT-ENTRY-COUNT)
011930       ELSE
011940         DISPLAY '* VERSION/CATALOG (I1908P) TABLE OVERFLOWED *'
011950               UPON SYSOUT
011960         GO TO 9900-DISPLAY-ABORT
011970     ELSE
011980       MOVE 'Y' TO WS-RETURN-08-FLAG
011990       DISPLAY 'I1908P CARD "' I1908P-INFO '" REJECTED FOR:'
012000             UPON SYSOUT
012010       IF WS-I1908P-VERSION NOT LETTERS-AND-NUMERALS
012020         DISPLAY '   INVALID EOP VERSION "' WS-I1908P-VERSION '"'
012030               UPON SYSOUT
012040       END-IF
012050       IF WS-I1908P-CATALOG NOT LETTERS-AND-NUMERALS
012060         DISPLAY '   INVALID EOP CATALOG CODE "'
012070               WS-I1908P-CATALOG '"' UPON SYSOUT.
012080     PERFORM 3820-READ-I1908P-FILE.
012090*
012100   3820-READ-I1908P-FILE.
012110     READ I1908P-FILE
012120       AT END
012130         MOVE 'E' TO WS-I1908P-FILE-FLAG
012140       NOT AT END
012150         MOVE WS-I1908P-SEQ TO WS-PREV-I1908P-SEQ
012160         MOVE I1908P-VERSION TO WS-I1908P-VERSION
012170         MOVE I1908P-CATALOG TO WS-I1908P-CATALOG
012180         IF WS-I1908P-SEQ NOT GREATER WS-PREV-I1908P-SEQ
012190           DISPLAY '* I1908P SEQUENCE ERROR "' WS-PREV-I1908P-SEQ
012200              '" "' WS-I1908P-SEQ '" *' UPON SYSOUT
012210           GO TO 9900-DISPLAY-ABORT
012220         END-IF
012230     END-READ.
012240*
012250   3850-EACH-I1788P-CARD.
012260     SET IX-WS-VER-CAT TO +1.
012270     PERFORM
012280         WITH TEST BEFORE
012290         UNTIL (IX-WS-VER-CAT GREATER WS-VER-CAT-ENTRY-COUNT)
012300             OR (WS-VER-CAT-VERSION (IX-WS-VER-CAT) EQUAL
012310                 I1788P-VERSION)
012320       SET IX-WS-VER-CAT UP BY 1
012330     END-PERFORM.
012340     IF IX-WS-VER-CAT GREATER WS-VER-CAT-ENTRY-COUNT
012350       MOVE 'Y' TO WS-RETURN-08-FLAG
012360       DISPLAY 'I1788P CARD "' I1788P-INFO
012370             '" REJECTED FOR VERSION CODE "' I1788P-VERSION
012380             '" - NOT VALID PER I1908P CARDS.' UPON SYSOUT
012390     ELSE
012400       MOVE 'Y' TO WS-VER-CAT-PRICED-FLAG (IX-WS-VER-CAT)
012410             WS-P3-I1788P-LOAD-FLAG
012420       PERFORM
012430           WITH TEST BEFORE
012440           UNTIL IX-WS-VER-CAT NOT LESS WS-VER-CAT-ENTRY-COUNT
012450         SET IX-WS-VER-CAT UP BY 1
012460         IF WS-VER-CAT-VERSION (IX-WS-VER-CAT) EQUAL
012470               I1788P-VERSION
012480           MOVE 'Y' TO WS-VER-CAT-PRICED-FLAG (IX-WS-VER-CAT)
012490         END-IF
012500       END-PERFORM.
012510     PERFORM 3860-READ-I1788P-FILE.
012520*
012530   3860-READ-I1788P-FILE.
012540     READ I1788P-FILE AT END
012550       MOVE 'E' TO WS-I1788P-FILE-FLAG.
012560*
012570   3880-EACH-I1920P-CARD.
012580     IF ((I1920P-BK-SYS-1ST-TWO NUMERIC)
012590             OR ((I1920P-BK-SYS-1ST-TWO ALPHABETIC)
012600                 AND (SPACE NOT EQUAL I1920P-BK-SYS-CHAR-1 AND
012610                       I1920P-BK-SYS-CHAR-2)))
012620         AND (I1920P-BK-SYS-REST NUMERIC)
012630         AND ((I1920P-BANK-BRANCH EQUAL SPACES)
012640             OR (I1920P-BANK-BRANCH LETTERS-AND-NUMERALS))
012650*                * VALID I1920C CARD *
012660       IF (I1920P-BANK-SYSTEM GREATER WS-I1920P-BANK-SYSTEM)
012670           OR (WS-I1920P-BANK-BRANCH NOT EQUAL SPACE)
012680*                * NOT BRANCH CARD FOR SYSTEM WITH SYSTEM CARD *
012690         IF IX-WS-C1920-IN NOT GREATER +250
012700*                * ROOM IN TABLE *
012710           MOVE I1920P-BANK-NUMBER TO WS-I1920P-BANK-NUMBER
012720                 WS-C1920-ENTRY (IX-WS-C1920-IN)
012730           SET  IX-WS-C1920-IN UP BY 1
012740         ELSE
012750           DISPLAY '* I1920P TABLE OVERFLOWED *' UPON SYSOUT
012760           GO TO 9900-DISPLAY-ABORT
012770       ELSE
012780         DISPLAY 'I1920P CARD "' I1920P-BANK-NUMBER
012790               '" DROPPED - COVERED BY SYSTEM CARD "'
012800               WS-I1920P-BANK-NUMBER '".' UPON SYSOUT
012810         MOVE 'Y' TO WS-RETURN-04-FLAG
012820     ELSE
012830       DISPLAY 'I1920P CARD REJECTED FOR INVALID BANK NUMBER "'
012840             I1920P-BANK-NUMBER '".' UPON SYSOUT
012850       MOVE 'Y' TO WS-RETURN-08-FLAG.
012860     PERFORM 3890-READ-I1920P-FILE.
012870*
012880   3890-READ-I1920P-FILE.
012890     READ I1920P-FILE
012900       AT END
012910         MOVE 'E' TO WS-I1920P-FILE-FLAG
012920       NOT AT END
012930         IF I1920P-BANK-NUMBER NOT GREATER WS-I1920P-BANK-NUMBER
012940           DISPLAY '* I1920P SEQUENCE ERROR "'
012950                 WS-I1920P-BANK-NUMBER '" "' I1920P-BANK-NUMBER
012960                 '" *' UPON SYSOUT
012970           GO TO 9900-DISPLAY-ABORT
012980         END-IF
012990       END-READ.
013000*
013010   3900-PASS-3-CLOSE.
013020     CLOSE WORK4-FILE WORK3-FILE WORK2-FILE I1788P-FILE
013030           I1908P-FILE I1920P-FILE.
013040*
013050*
013060   4000-PASS-4.
013070     PERFORM 4800-PASS-4-INIT.
013080     MOVE '1' TO WS-REPORT-NBR.
013090     PERFORM 4100-ONE-REPORT.
013100     MOVE '2' TO WS-REPORT-NBR.
013110     MOVE WS-REPORT-2-TITLE TO WS-HD1-REPORT-TITLE.
013120     PERFORM 4100-ONE-REPORT.
013130     MOVE '3' TO WS-REPORT-NBR.
013140     MOVE WS-REPORT-3-TITLE TO WS-HD1-REPORT-TITLE.
013150     PERFORM 4100-ONE-REPORT.
013160     MOVE '4' TO WS-REPORT-NBR.
013170     MOVE WS-REPORT-4-TITLE TO WS-HD1-REPORT-TITLE.
013180     PERFORM 4100-ONE-REPORT.
013190     PERFORM 4900-PASS-4-CLOSE.
013200*
013210   4100-ONE-REPORT.
013220     MOVE +0 TO WS-PAGE-NUMBER.
013230     IF END-WORK4-FILE
013240         OR (WK4-REPORT-NBR GREATER WS-REPORT-NBR)
013250       PERFORM 4400-PRINT-HEADING-1
013260       MOVE +2 TO WS-LINE-SPACER
013270       MOVE SPACE TO PRINTER1-RECORD
013280       MOVE '(NONE)' TO PRINTER1-RECORD (64:6)
013290       PERFORM 4300-WRITE-PRINTER1-REC
013300     ELSE
013310       MOVE +90 TO WS-LINE-COUNT
013320       PERFORM
013330           WITH TEST AFTER
013340           UNTIL END-WORK4-FILE
013350               OR (WK4-REPORT-NBR GREATER WS-REPORT-NBR)
013360         IF WS-LINE-COUNT GREATER +58
013370           PERFORM 4400-PRINT-HEADING-1
013380           MOVE +2 TO WS-LINE-SPACER
013390           MOVE WS-HEADING-2 TO PRINTER1-RECORD
013400           PERFORM 4300-WRITE-PRINTER1-REC
013410           MOVE +1 TO WS-LINE-SPACER
013420           MOVE WS-HEADING-3 TO PRINTER1-RECORD
013430           PERFORM 4300-WRITE-PRINTER1-REC
013440           MOVE +2 TO WS-LINE-SPACER
013450         ELSE
013460           IF (WK4-PRINT-DATA (1:8) NOT EQUAL WS-HOLD-WK4-BC)
013470             OR ((WK4-PRINT-DATA (1:8) EQUAL SPACE)
013480                   AND (WK4-PRINT-DATA (23:7) NOT EQUAL
013490                         WS-HOLD-WK4-BK-SYS))
013500             MOVE +2 TO WS-LINE-SPACER
013510           ELSE
013520             MOVE +1 TO WS-LINE-SPACER
013530           END-IF
013540         END-IF
013550         MOVE WK4-PRINT-DATA (1:8) TO WS-HOLD-WK4-BC
013560         MOVE WK4-PRINT-DATA (23:7) TO WS-HOLD-WK4-BK-SYS
013570         INSPECT WK4-PRINT-DATA REPLACING ALL ' ' BY '_'
013580         MOVE SPACE TO PRINTER1-RECORD
013590         MOVE WK4-BC-BR-BR TO PR1-BC-BR-BR
013600         MOVE WK4-554-KEY-LEFT TO PR1-554-KEY-LEFT
013610         MOVE WK4-554-KEY-RIGHT TO PR1-554-KEY-RIGHT
013620         MOVE WK4-171-KEY TO PR1-171-KEY
013630         MOVE WK4-171-CATALOG TO PR1-171-CATALOG
013640         MOVE WK4-171-VERSION TO PR1-171-VERSION
013650         MOVE WK4-554-VERSION TO PR1-554-VERSION
013660         PERFORM 4300-WRITE-PRINTER1-REC
013670         PERFORM 4200-READ-WORK4-FILE
013680       END-PERFORM.
013690*
013700   4200-READ-WORK4-FILE.
013710     READ WORK4-FILE
013720       AT END
013730         MOVE 'E' TO WS-WORK4-FILE-FLAG
013740       NOT AT END
013750         IF WK4-SEQ GREATER WS-WK4-SEQ
013760           MOVE WK4-SEQ TO WS-WK4-SEQ
013770         ELSE
013780           DISPLAY '* WORK4 SEQUENCE ERROR "' WS-WK4-SEQ '"'
013790                 UPON SYSOUT
013800           DISPLAY '           FOLLOWED BY "' WK4-SEQ '" *'
013810                 UPON SYSOUT
013820           GO TO 9900-DISPLAY-ABORT
013830         END-IF
013840     END-READ.
013850*
013860   4300-WRITE-PRINTER1-REC.
013870     WRITE PRINTER1-RECORD AFTER WS-LINE-SPACER.
013880     ADD WS-LINE-SPACER TO WS-LINE-COUNT.
013890*
013900   4400-PRINT-HEADING-1.
013910     ADD 1 TO WS-PAGE-NUMBER.
013920     MOVE WS-PAGE-NUMBER TO WS-HD1-PAGE-NUMBER.
013930     WRITE PRINTER1-RECORD FROM WS-HEADING-1 AFTER PAGE.
013940     MOVE +1 TO WS-LINE-COUNT.
013950*
013960   4800-PASS-4-INIT.
013970     OPEN OUTPUT PRINTER1-FILE INPUT WORK4-FILE.
013980     PERFORM 4200-READ-WORK4-FILE.
013990     MOVE WS-PROGRAM-NUMBER TO WS-HD1-PROGRAM-NUMBER.
014000     MOVE WS-SYSOUT-RUN-MONTH TO WS-HD1-RUN-MONTH.
014010     MOVE WS-SYSOUT-RUN-DAY TO WS-HD1-RUN-DAY.
014020     MOVE CPY4-FOUR-DIGIT-YEAR TO WS-HD1-RUN-YEAR.
014030*
014040   4900-PASS-4-CLOSE.
014050     CLOSE PRINTER1-FILE WORK4-FILE.
014060*
014070*
014080   8000-COMMON-INIT.
014090*
014100 COPY CPY005.
014110*
014120     MOVE LS-PARM-LENGTH TO CPY108-PARM-LENGTH.
014130     MOVE LS-PARM-DATA TO CPY108-PARM-DATA.
DLXMIG* DLXMIG Changes start here
DLXMIG*    CALL 'MIS11500' USING CPY108-REC.
DLXMIG* Changes start here
DLXMIG     CALL 'mis11500' USING CPY108-REC.
DLXMIG* Changes end here
DLXMIG* DLXMIG Changes end here
014200     IF CPY108-PARSE-ERROR
014210       DISPLAY '* CALLED PROGRAM MIS115 ABORTED *' UPON SYSOUT
014220       GO TO 9900-DISPLAY-ABORT.
014230     PERFORM
014240           VARYING CPY108-STMT-IX FROM +1 BY +1
014250           UNTIL CPY108-STMT-IX GREATER +5
014260       IF CPY108-RET-STATEMENT (CPY108-STMT-IX) NOT EQUAL SPACE
014270         MOVE CPY108-RET-STATEMENT (CPY108-STMT-IX) TO
014280               WS-DP-STATEMENT
014290         DISPLAY WS-DISPLAY-PARM UPON SYSOUT
014300         MOVE SPACE TO WS-DP-CAPTION
014310       END-IF
014320     END-PERFORM.
014330*
014340     SET CPY108-VAL-IX TO +1.
014350     SEARCH CPY108-RET-VALUES-ENTRY
014360       AT END
014370         DISPLAY '* MISSING PARM KEYWORD "PASS      " *'
014380               UPON SYSOUT
014390         GO TO 9900-DISPLAY-ABORT
014400       WHEN CPY108-RET-KEYWORD (CPY108-VAL-IX) EQUAL 'PASS      '
014410         IF CPY108-RET-VALUE (CPY108-VAL-IX) EQUAL '1         ' OR
014420               '2         ' OR '3         ' OR '4         '
014430           MOVE CPY108-RET-VALUE (CPY108-VAL-IX) TO WS-PASS
014440           MOVE SPACE TO CPY108-RET-KEYWORD (CPY108-VAL-IX)
014450         ELSE
014460           DISPLAY '* INVALID VALUE "' CPY108-RET-VALUE
014470                 (CPY108-VAL-IX) '" FOR PARM KEYWORD "'
014480                 CPY108-RET-KEYWORD (CPY108-VAL-IX) '" *'
014490                 UPON SYSOUT
014500           GO TO 9900-DISPLAY-ABORT
014510         END-IF
014520     END-SEARCH.
014530*
014540     IF WS-PASS EQUAL '1'
014550       SET CPY108-VAL-IX TO +1
014560       SEARCH CPY108-RET-VALUES-ENTRY
014570         AT END
014580           DISPLAY '* MISSING PARM KEYWORD "NON-BILL  " *'
014590                 UPON SYSOUT
014600           GO TO 9900-DISPLAY-ABORT
014610         WHEN CPY108-RET-KEYWORD (CPY108-VAL-IX) EQUAL
014620               'NON-BILL  '
014630           IF CPY108-RET-VALUE (CPY108-VAL-IX) EQUAL
014640                 'INCLUDE   ' OR 'OMIT      '
014650             MOVE CPY108-RET-VALUE (CPY108-VAL-IX) TO
014660                   WS-P1-PARM-NON-BILL-INSTR
014670             MOVE SPACE TO CPY108-RET-KEYWORD (CPY108-VAL-IX)
014680           ELSE
014690             DISPLAY '* INVALID VALUE "' CPY108-RET-VALUE
014700                   (CPY108-VAL-IX) '" FOR PARM KEYWORD "'
014710                   CPY108-RET-KEYWORD (CPY108-VAL-IX) '" *'
014720                   UPON SYSOUT
014730             GO TO 9900-DISPLAY-ABORT
014740           END-IF
014750       END-SEARCH.
014760*
014770     PERFORM
014780           VARYING CPY108-VAL-IX FROM +1 BY +1
014790           UNTIL CPY108-VAL-IX GREATER +5
014800       IF CPY108-RET-KEYWORD (CPY108-VAL-IX) NOT EQUAL SPACE
014810         MOVE 'Y' TO WS-RETURN-04-FLAG
014820         DISPLAY 'INFO ONLY: EXTRANEOUS PARM KEYWORD "'
014830               CPY108-RET-KEYWORD (CPY108-VAL-IX) '".'
014840               UPON SYSOUT
014850       END-IF
014860     END-PERFORM.
014870*
014880*
014890   9000-COMMON-CLOSE.
014900     IF RETURN-08
014910       MOVE 08 TO RETURN-CODE WS-RETURN-CODE
014920     ELSE
014930       IF RETURN-04
014940         MOVE 04 TO RETURN-CODE WS-RETURN-CODE.
014950     DISPLAY WS-RETURN-CODE-LINE UPON SYSOUT.
014960*
014970   9900-DISPLAY-ABORT.
014980     DISPLAY '* ' WS-PROGRAM-NUMBER ' * RUN ABORTED *'
014990           UPON SYSOUT.
015000     IF OPENED-I1636-FILE
015010       PERFORM 1910-CLOSE-I1636-FILE.
015020     IF OPENED-I554-FILE
015030       PERFORM 2910-CLOSE-I554-FILE.
015040     MOVE 16 TO RETURN-CODE WS-RETURN-CODE.
015050     DISPLAY WS-RETURN-CODE-LINE UPON SYSOUT.
015060     STOP RUN.
015070*
015080*
