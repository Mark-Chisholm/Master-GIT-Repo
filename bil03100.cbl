000010 IDENTIFICATION DIVISION.
DLXMIG* DLXMIG Changes start here
DLXMIG*  PROGRAM-ID. BIL03100.
DLXMIG   PROGRAM-ID. "bil03100".
DLXMIG* DLXMIG Changes end here
000060*PROGRAM-NAME. UNITIZE CUTS, LETTERING AND BY-LINES.
000070   AUTHOR. NARAYAN LAKSHMY.
000080   INSTALLATION. DELUXE CORPORATION.
000090   DATE-WRITTEN. 04-01-02.
000100   DATE-COMPILED.
000110******************************************************************
000120*
000130*RUN INSTRUCTIONS                                         04-01-02
000140*
000150*I545 = 545 FROM BIL460                       VARIABLE LRECL=1005
000160*       BILLING TRANSACTION FILE
000170*
000180*I1971P = C1971 INPUT FROM PARMLIB                     LRECL=80
000190*       PRODUCT CODE FILE
000200*
000210*O545 = 545 OUTPUT TO BIL672                  VARIABLE LRECL=1005
000220*       BILLING TRANSACTION FILE
000230*
000240*SYSOUT = REVISION INFO, RUN TOTALS, ERROR MESSAGES;
000250*
000260******************************************************************
000270*
000280*RETURN CODES                                             04-01-02
000290*
000300*  16 = RUN ABORTED.  SEE SYSOUT.
000310*         - EMPTY I545 FILE
000320*         - INCORRECT I545 UPDATE CODE
000330*         - SEQUENCE ERROR; I545 OR I1971P
000340*         - TABLE OVERFLOW; I1971P
000350*
000360*  08 = ERROR. RUN CONTINUED.  SEE SYSOUT.
000370*         - ONE OR MORE I1971P CARDS REJECTED FOR INVALID DATA
000380*
000390*  04 = INFO ONLY.  RUN CONTINUED.  SEE SYSOUT.
000400*         - EMPTY CARD FILE; I1971P
000410*         - CHANGED O545 PRICES
000420*         - TABLE OVER 80% FULL; I1971P
000430*
000440******************************************************************
000450*
000460*NARRATIVE                                                04-01-02
000470*
000480*PURPOSE:
000490*  TO MANIPULATE THE IMPRINT QUANTITY ON SELECTED PRODUCT CODES
000500*    TO INCREASE THE IMPRINT QUANTITY TO REFLECT A UNIT AMOUNT
000510*    CONSISTENT WITH THE MAIN CHECK PACKAGE UNIT COUNTS.
000520*
000530*SOURCE OF INFORMATION:
000540*  THE 545 FILE IN ASCENDING WIP KEY, DESCENDING ERROR FLAG,
000550*        ASCENDING WIP TRANSACTION RECORD NUMBER ORDER
000560*    SORT FIELDS=(8,45,A).
000570*  THE C1971 CARDS IN PRODUCT CODE ORDER
000580*    SORT FIELDS=(1,10,A).
000590*
000600*PROGRAM FLOW:
000610*  IN INITIALIZATION, THIS PROGRAM OPENS FILES, READS THE I545
000620*    FILE & VERIFIES THAT IT ISN'T EMPTY, EDITS & BUILDS A TABLE
000630*    FROM THE INPUT CARD FILE.
000640*  BEFORE DOING ANYTHING ELSE WITH EACH I545 RECORD, THIS PROGRAM
000650*    SEQUENCE CHECKS IT, AND DETERMINES IF IT IS A PRIMARY
000660*    COMPONENT, OR A NON-PRIMARY COMPONENT.
000670*    IF IT'S A PRIMARY COMPONENT, AND ITS UNITS ARE GREATER THAN
000680*      +1 OR LESS THAN -1, THE PROGRAM SAVES THE WIP KEY AND
000690*      NUMBER OF UNITS.
000700*    IF IT'S A NON-PRIMARY COMPONENT, THE PROGRAM FURTHER CHECKS
000710*      IF
000720*        1 - IT IS NOT A RETRIEVAL CREDIT.
000730*        2 - IT HAS A PRODUCT CODE THAT MATCHES THE ENTRY IN THE
000740*          PRODUCT CODE TABLE.
000750*        3 - IT HAS +1 OR -1 UNITS (SAME SIGN AS PRIMARY).
000760*      IF ALL THESE CONDITIONS ARE MET, THE PROGRAM ADJUSTS THE
000770*        UNITS TO THAT OF THE PRIMARY COMPONENT, AND ADJUSTS THE
000780*        IMPRINT QUANTITY ACCORDINGLY AND VARIOUS PRICE FIELDS
000790*        TO REFLECT THE EXTENDED UNITS.
000800*  AT THE END OF THE 545 FILE PROGRAM DISPLAYS RUN TOTALS AND
000810*    ENDS.
000820*
000830******************************************************************
000840*
000850*REVISION HISTORY
000860*
000870*04-01-02  PROGRAM WRITTEN.
000880*          CAPS #29856.                         NARAYAN LAKSHMY 01
000890*
000900*04-15-02  MODIFIED THE PROGRAM TO REMOVE ORDER CHANNELS '6' AND
000910*          'H' FROM THE EXCLUDED LIST FOR UNITIZING.
000920*          CAPS #27469.                         NARAYAN LAKSHMY 02
000930*
000940*05-13-02  MODIFIED THE PROGRAM TO REMOVE ORDER CHANNELS 'I' AND
000950*          'J' FROM THE EXCLUDED LIST FOR UNITIZING.
000960*          CAPS #27470.                        BOOGARAPU PRASAD 03
000970*
000980*05-16-02  MODIFIED TO ADD BC '32117367' TO THE EXCLUDED LIST FOR
000990*          UNITIZING.
001000*          CAPS #30768.                         NARAYAN LAKSHMY 04
001010*
001020*08-12-02  MODIFIED TO REPORT THE CORRECT NUMBER OF  O545 RECORDS
001030*          ON SYSOUT.
001040*          REMEDY #01265                        A DINESH RAVI   05
001050*
001060*01-06-03  MODIFIED THE PROGRAM TO FIX INITIALIZATION ROUTINE AND
001070*          REMOVE ORDER CHANNEL 'G' AND BC 32117367.
001080*          CMN PKG #BILL861 REMEDY #ACM00010474 MOHAMED MAHEEN  06
001090*
001100*05-09-03  MODIFIED THE PROGRAM TO REMOVE CITIBANK HOLDING COMPANY
001110*          '00381' FROM THE EXCLUDED LIST FOR UNITIZING.
001120*          CMN PKG #BILL898 REMEDY #ACM00020157    ASHOK MENON  07
001130*
DLXMIG**----------------------------------------------------------------    
DLXMIG**  DATE      DESCRIPTION                        INITIALS             
DLXMIG** -------  ------------------------------------------------------    
DLXMIG** 101409   UPDATED FOR DLX MIGRATION            COGNIZANT            
DLXMIG** -------  ------------------------------------------------------    

001200******************************************************************
001210*
001220 ENVIRONMENT DIVISION.
001230 CONFIGURATION SECTION.
001240*
DLXMIG* DLXMIG Changes start here
DLXMIG*  SOURCE-COMPUTER. IBM-370.
DLXMIG   SOURCE-COMPUTER. UNIX.
DLXMIG* DLXMIG Changes end here
DLXMIG* DLXMIG Changes start here
DLXMIG*  OBJECT-COMPUTER. IBM-370.
DLXMIG   OBJECT-COMPUTER. UNIX.
DLXMIG* DLXMIG Changes end here
001330*
001340 INPUT-OUTPUT SECTION.
001350   FILE-CONTROL.
DLXMIG     SELECT I545-FILE     ASSIGN RSD-I545.
001370     SELECT I1971P-FILE   ASSIGN RSD-I1971P.
DLXMIG     SELECT O545-FILE     ASSIGN RSD-O545.
001390*
001400 DATA DIVISION.
001410 FILE SECTION.
001420*
001430 FD  I545-FILE
001440*
001450 COPY T0545I.
001460*
001470 FD  I1971P-FILE
001480     RECORDING MODE IS F
001490     BLOCK CONTAINS 0 RECORDS
001500     RECORD CONTAINS 80 CHARACTERS
001510     LABEL RECORDS STANDARD.
001520   01  I1971P-REC.
001530       05  I1971P-PRODUCT.
001540         07  I1971P-PROD-MAJ-GROUP               PIC X(2).
001550         07  I1971P-PROD-PRICE-CATEGORY          PIC X(2).
001560         07  I1043P-PROD-NUMBER                  PIC X(6).
001570       05  FILLER                                PIC X(70).
001580*
001590 FD  O545-FILE
001600*
001610 COPY T0545O.
001620*
001630 WORKING-STORAGE SECTION.
001640*
001650 01  WS-FLAGS-AND-SUCH                           VALUE SPACES.
001660     03  WS-I545-FILE-FLAG          PIC X.
001670         88  END-I545-FILE              VALUE 'E'.
001680     03  WS-I1971P-FILE-FLAG        PIC X.
001690         88  END-I1971P-FILE            VALUE 'E'.
001700     03  WS-RETURN-08-FLAG          PIC X.
001710         88  RETURN-08                  VALUE 'Y'.
001720     03  WS-RETURN-04-FLAG          PIC X.
001730         88  RETURN-04                  VALUE 'Y'.
001740     03  WS-VERIFY-FLAG             PIC X.
001750     03  WS-ERROR-FLAG              PIC X.
001760     03  WS-PRODUCT-MATCH-FLAG      PIC X.
001770         88  WS-FOUND-Y                 VALUE 'Y'.
001780         88  WS-FOUND-N                 VALUE 'N'.
001790*
001800 01  WS-RUN-TOTALS.
001810   03  WS-RT-RECS-TAX-CHANGED     PIC S9(5)      COMP-3 VALUE +0.
001820   03  WS-RT-TAX-CHANGED-AMOUNT   PIC S9(7)V9(2) COMP-3 VALUE +0.
001830   03  WS-RT-RECS-UPCH-CHANGED    PIC S9(5)      COMP-3 VALUE +0.
001840   03  WS-RT-O545-LIVE-REC-CT     PIC S9(7)      COMP-3 VALUE +0.
001850   03  WS-RT-O545-LIVE-REC-GROSS  PIC S9(9)V9(2) COMP-3 VALUE +0.
001860   03  WS-RT-O545-FLAGGED-REC-CT  PIC S9(5)      COMP-3 VALUE +0.
001870   03  WS-RT-I545-LIVE-REC-CT     PIC S9(7)      COMP-3 VALUE +0.
001880   03  WS-RT-I545-LIVE-REC-GROSS  PIC S9(9)V9(2) COMP-3 VALUE +0.
001890   03  WS-RT-I545-GROSS-AFT-XW    PIC S9(9)V9(2) COMP-3 VALUE +0.
001900   03  WS-RT-I545-FLAGGED-REC-CT  PIC S9(5)      COMP-3 VALUE +0.
001910   03  WS-RT-I1971P-CARD-COUNT    PIC S9(3)      COMP-3 VALUE +0.
001920   03  WS-RT-I1971P-REJECT-CT     PIC S9(3)      COMP-3 VALUE +0.
001930*
001940 01  WS-SEQUENCES.
001950   03  WS-I545-SEQ                PIC X(45) VALUE LOW-VALUES.
001960   03  WS-PREV-I545-SEQ           PIC X(45) VALUE LOW-VALUES.
001970*
001980 01  WS-XW-UNITS-WORK.
001990   03  WS-XW-U-PRIMARY-WIP-KEY    PIC X(45).
002000   03  WS-XW-U-PRIMARY-MATCH-UNITS PIC X(2).
002010   03  WS-XW-U-PRIMARY-UNITS      PIC S9(5)      COMP-3.
002020   03  WS-XW-U-RATIO              PIC S9(5)      COMP-3.
002030*
002040 01  WS-C1971-TABLE.
002050   03  WS-C1971-TABLE-COUNT       PIC S9(5)      COMP.
002060   03  WS-C1971-TBL-REC      OCCURS 1 TO 10000 TIMES
002070                             DEPENDING ON WS-C1971-TABLE-COUNT
002080                             INDEXED BY IX-WS-I1971
002090                             PIC X(10).
002100*
002110 01  WS-C1971-REC.
002120   03  WS-C1971-PRODUCT.
002130     05  WS-C1971-PROD-BYTE       OCCURS 10 TIMES
002140                                  INDEXED BY IX-WS-C1971-PROD
002150                                  PIC X.
002160*
002170 01  WS-545-PRODUCT.
002180     05  WS-545-PROD-BYTE        OCCURS 10 TIMES
002190                                 INDEXED BY IX-545-PROD-IX
002200                                  PIC X.
002210*
002220 01  WS-MISC.
002230   03  WS-REC-CNT                 PIC S9(04)   COMP-3   VALUE +0.
002240   03  WS-I1971-IX                PIC S9(04)   COMP-3   VALUE +0.
002250*
002260 01  WS-SEQUENCES                                VALUE LOW-VALUES.
002270   03  WS-I1971P-SEQ              PIC  X(10).
002280*
002290 01  WS-CLOSING-DISPLAYS.
002300   03  WS-RETURN-CODE-LINE.
002310     05  FILLER                   PIC X(14) VALUE 'RETURN CODE ='.
002320     05  WS-RETURN-CODE           PIC 9(2)       DISPLAY VALUE 00.
002330     05  FILLER                   PIC X          VALUE '.'.
002340   03  WS-CD-CARD-LINE.
002350     05  FILLER                   PIC X(3)       VALUE 'I19'.
002360     05  WS-CD-CARD-NBR-3-4       PIC X(2)       VALUE '71'.
002370     05  FILLER                   PIC X(8)       VALUE 'P:'.
002380     05  WS-CD-CARD-COUNT         PIC ZZ9.
002390     05  FILLER                   PIC X(24)   VALUE ' CARDS READ'.
002400     05  WS-CD-CARD-REJ-CT        PIC ZZ9.
002410     05  FILLER                   PIC X(9)      VALUE ' REJECTED'.
002420   03  WS-CD-545-LINE.
002430     05  WS-CD-545-I-OR-O         PIC X          VALUE 'I'.
002440     05  FILLER                   PIC X(6)       VALUE '545:'.
002450     05  WS-CD-545-TOTAL-REC-CT   PIC Z,ZZZ,ZZ9.
002460     05  FILLER                   PIC X(9)      VALUE ' RECORDS '.
002470     05  WS-CD-545-VERB           PIC X(7)       VALUE 'READ'.
002480     05  FILLER                   PIC X(79)      VALUE SPACES.
002490*
002500 COPY CPY109.
002510*
002520 COPY CPY004.
002530*
002540 01  WS-PROGRAM-NUMBER            PIC X(8)       VALUE 'BIL03107'.
002550 01  WS-REVISION-DATE             PIC X(8)       VALUE '05-09-03'.
002560*
002570 LINKAGE SECTION.
002580*
002590 PROCEDURE DIVISION.
002600*
002610   0000-MAIN-LINE.
002620*
002630     PERFORM 8000-INITIALIZE.
002640     PERFORM 1000-EACH-I545-REC UNTIL END-I545-FILE.
002650     PERFORM 9000-CLOSE.
002660     STOP RUN.
002670*
002680   1000-EACH-I545-REC.
002690*
002700     MOVE I545-LENGTH-REST-OF-REC TO O545-LENGTH-REST-OF-REC.
002710     MOVE I545-REST-OF-REC        TO O545-REST-OF-REC.
002720     IF NOT I545-PRICING-SUCCESSFUL
002730       ADD 1 TO WS-RT-I545-FLAGGED-REC-CT
002740                WS-RT-O545-FLAGGED-REC-CT
002750     ELSE
002760       ADD 1 TO WS-RT-I545-LIVE-REC-CT
002770                WS-RT-O545-LIVE-REC-CT
002780       ADD I545-GROSS TO WS-RT-O545-LIVE-REC-GROSS
002790     END-IF.
002800     WRITE O545-REC.
002810     PERFORM 1100-READ-I545-FILE.
002820*
002830   1100-READ-I545-FILE.
002840*
002850     READ I545-FILE AT END
002860       MOVE 'E' TO WS-I545-FILE-FLAG.
002870     IF NOT END-I545-FILE
002880       IF I545-UPDATE-CODE NOT LESS 'D'
002890         MOVE WS-I545-SEQ TO WS-PREV-I545-SEQ
002900         MOVE I545-WIP-MATCH-SEQUENCE TO WS-I545-SEQ
002910         IF WS-I545-SEQ NOT GREATER WS-PREV-I545-SEQ
002920           DISPLAY '* I545 SEQUENCE ERROR "' WS-PREV-I545-SEQ '"'
002930                 UPON SYSOUT
002940           DISPLAY '          FOLLOWED BY "' WS-I545-SEQ '" *'
002950                 UPON SYSOUT
002960           GO TO 9900-DISPLAY-ABORT
002970         ELSE
002980           IF I545-PRICING-SUCCESSFUL
002990             ADD I545-GROSS TO WS-RT-I545-LIVE-REC-GROSS
003000             IF I545-PRIMARY-REC-FROM-WIP
003010               IF (I545-UNITS GREATER +1) OR (I545-UNITS LESS -1)
003020                 MOVE I545-WIP-KEY TO WS-XW-U-PRIMARY-WIP-KEY
003030                 MOVE I545-MATCH-UNITS TO
003040                       WS-XW-U-PRIMARY-MATCH-UNITS
003050                 MOVE I545-UNITS TO WS-XW-U-PRIMARY-UNITS
003060               END-IF
003070             ELSE
003080               MOVE 'N' TO WS-PRODUCT-MATCH-FLAG
003090               MOVE I545-PRODUCT-CODE(3:10) TO WS-545-PRODUCT
003100               PERFORM 1150-MATCH-PRODUCT-TABLE
003110               IF (I545-WIP-KEY EQUAL WS-XW-U-PRIMARY-WIP-KEY)
003120                  AND (((I545-UNITS EQUAL +1)
003130                  AND (WS-XW-U-PRIMARY-UNITS IS POSITIVE))
003140                  OR ((I545-UNITS EQUAL -1)
003150                  AND (WS-XW-U-PRIMARY-UNITS IS NEGATIVE)))
003160                  AND ( WS-FOUND-Y )
003170                  AND ( I545-HOLDING-COMPANY NOT EQUAL
003180                        '00148' AND '01960' )
003190                 DIVIDE WS-XW-U-PRIMARY-UNITS BY I545-UNITS GIVING
003200                       WS-XW-U-RATIO
003210                 MULTIPLY WS-XW-U-RATIO BY I545-IMPRINT-QUANTITY
003220                 MOVE WS-XW-U-PRIMARY-UNITS TO I545-UNITS
003230                 MOVE WS-XW-U-PRIMARY-MATCH-UNITS TO
003240                       I545-MATCH-UNITS
003250                 IF (I545-ORIGINAL-PRICE-CODE EQUAL '1' OR '6')
003260                     AND (I545-CAPS-APPLIED EQUAL +0)
003270                   MULTIPLY WS-XW-U-RATIO BY I545-WVE-RSL-GROSS
003280                       I545-WVE-RSL-TAX I545-WAIVED-UPCHARGE
003290                       I545-GROSS I545-REPORT-BASE
003300                       I545-TAX I545-UPCHARGE
003310                       I545-NORMAL-BASE I545-ADJUSTED-BASE
003320                       I545-GAS-ADJUSTMENTS I545-CAPS-APPLIED
003330                       I545-CAPS-TRIGGERED I545-PRE-UPCHARGE-TAX
003340                   MOVE 'Y' TO WS-RETURN-04-FLAG
003350                 END-IF
003360               END-IF
003370             END-IF
003380           END-IF
003390         END-IF
003400       ELSE
003410         DISPLAY '* I545 UPDATE CODE "' I545-UPDATE-CODE
003420             '" ENCOUNTERED - MUST BE AT LEAST "D" *'
003430               UPON SYSOUT
003440         GO TO 9900-DISPLAY-ABORT
003450       END-IF
003460     END-IF.
003470*
003480   1150-MATCH-PRODUCT-TABLE.
003490*
003500     SET IX-WS-I1971 TO WS-C1971-TABLE-COUNT.
003510     PERFORM WITH TEST AFTER UNTIL WS-FOUND-Y OR WS-FOUND-N
003520       MOVE 'Y' TO WS-PRODUCT-MATCH-FLAG
003530       MOVE WS-C1971-TBL-REC(IX-WS-I1971) TO WS-C1971-REC
003540       IF WS-C1971-PRODUCT NOT EQUAL SPACE
003550         SET IX-WS-C1971-PROD TO +1
003560         PERFORM WITH TEST AFTER
003570               UNTIL WS-FOUND-N OR (IX-WS-C1971-PROD GREATER +10)
003580           IF WS-C1971-PROD-BYTE (IX-WS-C1971-PROD) EQUAL '*' OR
003590                 WS-545-PROD-BYTE (IX-WS-C1971-PROD)
003600             SET IX-WS-C1971-PROD UP BY 1
003610           ELSE
003620             MOVE 'N' TO WS-PRODUCT-MATCH-FLAG
003630           END-IF
003640         END-PERFORM
003650         IF WS-FOUND-N AND (IX-WS-I1971 GREATER +1)
003660           MOVE SPACE TO WS-PRODUCT-MATCH-FLAG
003670           SET IX-WS-I1971 DOWN BY 1
003680         END-IF
003690       END-IF
003700     END-PERFORM.
003710*
003720   8000-INITIALIZE.
003730*
003740 COPY CPY005.
003750*
003760     MOVE +0                     TO WS-C1971-TABLE-COUNT.
003770     OPEN OUTPUT O545-FILE
003780           INPUT I545-FILE I1971P-FILE.
003790     PERFORM 8200-READ-I1971P-FILE.
003800     IF END-I1971P-FILE
003810       DISPLAY 'I1971P IS AN EMPTY FILE.' UPON SYSOUT
003820       MOVE 'Y' TO WS-RETURN-04-FLAG
003830     ELSE
003840       PERFORM 8100-LOAD-C1971-TABLE.
003850     PERFORM 1100-READ-I545-FILE.
003860     IF END-I545-FILE
003870       DISPLAY '* I545 IS AN EMPTY FILE *' UPON SYSOUT
003880       GO TO 9900-DISPLAY-ABORT.
003890*
003900   8100-LOAD-C1971-TABLE.
003910*
003920     PERFORM
003930           WITH TEST AFTER
003940           UNTIL END-I1971P-FILE
003950       IF WS-ERROR-FLAG NOT = 'Y'
003960         IF I1971P-PRODUCT GREATER WS-I1971P-SEQ
003970           MOVE I1971P-PRODUCT TO WS-I1971P-SEQ
003980           ADD 1 TO WS-RT-I1971P-CARD-COUNT
003990         ELSE
004000           DISPLAY '* I1971P SEQUENCE ERROR "' WS-I1971P-SEQ '" "'
004010                 I1971P-PRODUCT '" *' UPON SYSOUT
004020           GO TO 9900-DISPLAY-ABORT
004030         END-IF
004040         IF WS-C1971-TABLE-COUNT NOT LESS +10000
004050           DISPLAY '* C1971 TABLE OVERFLOWED *' UPON SYSOUT
004060           GO TO 9900-DISPLAY-ABORT
004070         ELSE
004080           ADD 1 TO WS-C1971-TABLE-COUNT
004090           MOVE I1971P-REC TO WS-C1971-TBL-REC (IX-WS-I1971)
004100            SET IX-WS-I1971 UP BY 1
004110         END-IF
004120       END-IF
004130       PERFORM 8200-READ-I1971P-FILE
004140     END-PERFORM.
004150     SET IX-WS-I1971 DOWN BY 1.
004160     IF WS-C1971-TABLE-COUNT GREATER +8000
004170       DISPLAY 'C1971 TABLE IS OVER 80% FULL.' UPON SYSOUT
004180       MOVE 'Y' TO WS-RETURN-04-FLAG.
004190*
004200   8200-READ-I1971P-FILE.
004210*
004220     READ I1971P-FILE AT END
004230       MOVE 'E' TO WS-I1971P-FILE-FLAG.
004240     MOVE SPACES TO WS-ERROR-FLAG.
004250     IF NOT END-I1971P-FILE
004260       IF ( I1971P-PROD-MAJ-GROUP NOT NUMERIC ) OR
004270          ( I1971P-PROD-PRICE-CATEGORY NOT NUMERIC )
004280         DISPLAY '1971P MAJ/MIN SHOULD BE NUMERIC  '
004290               'MAJ/MIN - ' I1971P-PROD-MAJ-GROUP
004300               I1971P-PROD-PRICE-CATEGORY ', CARD REJECTED.'
004310               UPON SYSOUT
004320         MOVE 'Y' TO WS-ERROR-FLAG WS-RETURN-08-FLAG
004330         ADD +1 TO WS-RT-I1971P-REJECT-CT
004340       END-IF
004350       IF WS-ERROR-FLAG EQUAL SPACES
004360         MOVE I1971P-PRODUCT TO WS-C1971-PRODUCT
004370         PERFORM VARYING IX-WS-C1971-PROD FROM 5 BY 1
004380                 UNTIL IX-WS-C1971-PROD > 10
004390           IF ( WS-C1971-PROD-BYTE(IX-WS-C1971-PROD) NOT = '*')
004400                AND
004410              ( WS-C1971-PROD-BYTE(IX-WS-C1971-PROD) NOT NUMERIC)
004420             MOVE 'Y' TO WS-ERROR-FLAG
004430           END-IF
004440         END-PERFORM
004450         IF WS-ERROR-FLAG = 'Y'
004460           DISPLAY '1971P PRODUCT NUMBER SHOULD BE NUMERIC OR * '
004470                 'PRODUCT NUMBER - ' WS-C1971-PRODUCT(5:6)
004480                 ', CARD REJECTED.'
004490                 UPON SYSOUT
004500           MOVE 'Y' TO WS-RETURN-08-FLAG
004510           ADD +1 TO WS-RT-I1971P-REJECT-CT
004520         END-IF
004530       END-IF
004540     END-IF.
004550*
004560   9000-CLOSE.
004570*
004580     CLOSE O545-FILE I545-FILE I1971P-FILE.
004590     PERFORM 9200-DISPLAY-RUN-TOTALS.
004600     IF RETURN-08
004610       MOVE 08 TO RETURN-CODE WS-RETURN-CODE
004620     ELSE
004630       IF RETURN-04
004640         MOVE 04 TO RETURN-CODE WS-RETURN-CODE
004650       END-IF
004660     END-IF.
004670     DISPLAY WS-RETURN-CODE-LINE UPON SYSOUT.
004680*
004690   9200-DISPLAY-RUN-TOTALS.
004700*
004710     DISPLAY '-------  ' WS-PROGRAM-NUMBER ' RUN TOTALS  -------'
004720           UPON SYSOUT.
004730     MOVE '71'                    TO WS-CD-CARD-NBR-3-4.
004740     MOVE WS-RT-I1971P-CARD-COUNT TO WS-CD-CARD-COUNT.
004750     MOVE WS-RT-I1971P-REJECT-CT  TO WS-CD-CARD-REJ-CT.
004760     DISPLAY WS-CD-CARD-LINE UPON SYSOUT.
004770     ADD WS-RT-I545-LIVE-REC-CT WS-RT-I545-FLAGGED-REC-CT GIVING
004780           WS-CD-545-TOTAL-REC-CT.
004790     DISPLAY WS-CD-545-LINE UPON SYSOUT.
004800     MOVE 'O'                       TO WS-CD-545-I-OR-O.
004810     MOVE 'WRITTEN'                 TO WS-CD-545-VERB.
004820     ADD WS-RT-O545-LIVE-REC-CT WS-RT-O545-FLAGGED-REC-CT GIVING
004830           WS-CD-545-TOTAL-REC-CT.
004840     DISPLAY WS-CD-545-LINE UPON SYSOUT.
004850*
004860   9900-DISPLAY-ABORT.
004870*
004880     DISPLAY '* ' WS-PROGRAM-NUMBER ' * RUN ABORTED *'
004890           UPON SYSOUT.
004900     MOVE 16 TO RETURN-CODE WS-RETURN-CODE.
004910     DISPLAY WS-RETURN-CODE-LINE UPON SYSOUT.
004920     STOP RUN.
004930*
